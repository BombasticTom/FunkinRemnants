import funkin.modding.module.Module;
import funkin.ui.mainmenu.MainMenuState;
import funkin.graphics.FunkinSprite;
import funkin.ui.title.TitleState;
import flixel.util.FlxTimer;
import funkin.ui.UIState;
import funkin.save.Save;
import flixel.FlxSprite;
import flixel.FlxG;
import Std;

using StringTools;

class RemnantsMainMenu extends Module
{
  public function new()
  {
    super('RemnantsMainMenu');
  }

  var backSpr:FunkinSprite = null;
  var newCredits:FunkinSprite = null;
  var backSpaced:Bool = false;

  override public function onStateChangeBegin(event:StateChangeScriptEvent):Void
  {
    super.onStateChangeBegin(event);

    if (backSpaced
      || backSpr == null
      || !RemnantsContentUtil.remnantsContentEnabled()
      || FlxG.onMobile
      || !Std.isOfType(FlxG.state, MainMenuState)
      || !FlxG.state.uiStateMachine.is(UIState.Exiting)
      || !FlxG.state.controls.BACK_P) return;

    // I don't like going back to the title instantly....
    backSpaced = true;
    backSpr.animation.play('press', true);
    backSpr.setPosition(FlxG.width - backSpr.width, FlxG.height - backSpr.height);

    FlxTimer.wait(0.5, () -> {
      FlxG.switchState(() -> new TitleState());
      backSpr = null;
    });

    event.cancel();
  }

  override public function onStateChangeEnd(event:StateChangeScriptEvent):Void
  {
    super.onStateChangeEnd(event);

    checkAndReplace(event.targetState);
    addRemnantsMainMenuItems(event.targetState);
  }

  override public function onSubStateOpenEnd(event:SubStateScriptEvent):Void
  {
    super.onSubStateOpenEnd(event);

    checkAndReplace(event.targetState);
  }

  public function addRemnantsMainMenuItems(state:FlxState):Void
  {
    if (!RemnantsContentUtil.remnantsContentEnabled() || !Std.isOfType(state, MainMenuState)) return;

    if (!FlxG.onMobile)
    {
      backSpr = FunkinSprite.createSparrow(0, 0, 'mainMenu/backspace');
      backSpr.animation.addByPrefix('press', 'backspace PRESSED', 24, false);
      backSpr.animation.addByPrefix('idle', 'backspace to exit', 24, true);
      backSpr.animation.play('idle', true);
      backSpr.setPosition(FlxG.width - backSpr.width + 15, FlxG.height - backSpr.height + 50);
      backSpr.scrollFactor.set();
      state.add(backSpr);

      backSpaced = false;
    }

    if (Save.instance.modOptions.exists('seenRemnantsCredits')) return;

    var creditsMenuItem:FlxSprite = state.menuItems.getItem('credits');
    if (creditsMenuItem != null)
    {
      var oldCallback:Void->Void = creditsMenuItem.callback;
      creditsMenuItem.callback = () -> {
        Save.instance.modOptions.set('seenRemnantsCredits', true);
        Save.instance.flush();
        oldCallback();
      }
    }

    newCredits = FunkinSprite.createSparrow((creditsMenuItem?.x ?? 0) + 200, (creditsMenuItem?.y ?? 0) - 80, 'freeplay/freeplayCapsule/new');
    newCredits.scrollFactor.set(creditsMenuItem?.scrollFactor?.x ?? 1, creditsMenuItem?.scrollFactor?.y ?? 1);
    newCredits.animation.addByPrefix('newAnim', 'NEW notif', 24, true);
    newCredits.animation.play('newAnim', true);
    newCredits.scale.set(1.25, 1.25);
    newCredits.updateHitbox();
    state.add(newCredits);
  }

  override function onUpdate(event:UpdateScriptEvent)
  {
    super.onUpdate(event);

    if (Std.isOfType(FlxG.state, MainMenuState) && newCredits != null)
    {
      var creditsMenuItem:FlxSprite = FlxG.state?.menuItems?.getItem('credits');
      newCredits.visible = creditsMenuItem?.visible ?? false;
    }
  }

  public function checkAndReplace(group:FlxGroup):Void
  {
    if (!RemnantsContentUtil.remnantsContentEnabled() || group?.members == null) return;

    for (basic in group.members)
    {
      try
      {
        basic.members[0];
        checkAndReplace(basic.members);
        continue;
      }
      catch (e:Exception) {}

      if (!Std.isOfType(basic, FlxSprite)) continue;

      var assetKey:String = basic.graphic?.assetsKey ?? '';
      if (!assetKey.startsWith('assets/images/menu')) continue;

      var suffix:String = assetKey.substring('assets/images/menu'.length, assetKey.length - '.png'.length);
      if (suffix.startsWith('BG')) suffix = suffix.substring('BG'.length);

      var graphic:FlxGraphic = FlxG.bitmap.add(Paths.image('mainMenu/menuBGRemnants' + suffix));
      basic.frames = graphic?.imageFrame ?? null;
    }
  }
}
