import funkin.ui.credits.CreditsState;
import funkin.util.TouchUtil;
import funkin.graphics.video.FunkinVideoSprite;
import funkin.modding.module.Module;
import funkin.graphics.FunkinSprite;
import funkin.audio.FunkinSound;
import flixel.FlxG;
import Std;

using StringTools;

class RemnantsCredits extends Module
{
  final VIDEO_PATH:String = 'grieving';

  public var vid:FunkinVideoSprite;
  public var vidBG:FunkinSprite;

  var playingVideo:Bool = false;

  public function new()
  {
    super('remnantsCredits', 1000, {state: CreditsState});
  }

  override public function onStateChangeEnd(event:StateChangeScriptEvent):Void
  {
    if (!RemnantsContentUtil.remnantsContentEnabled()) return;

    super.onStateChangeEnd(event);

    var bg:FunkinSprite = FunkinSprite.create(0, 0, 'menuDesat');
    bg.setGraphicSize(FlxG.width);
    bg.updateHitbox();
    bg.screenCenter();
    bg.scrollFactor.set();
    bg.alpha = 0.5;
    bg.color = 0xFFB57EDC; // Lavender
    event.targetState.insert(0, bg);

    event.targetState.scrollPaused = true;
    event.targetState.creditsGroup.y += FlxG.height / 4;
    FlxG.sound.music?.stop();

    startVideo();
  }

  public function startVideo():Void
  {
    vidBG = new FunkinSprite();
    vidBG.makeSolidColor(FlxG.width, FlxG.height, 0xFF000000);
    FlxG.state.add(vidBG);

    vid = new FunkinVideoSprite();
    playingVideo = true;

    if (vid != null)
    {
      vid.bitmap.onEncounteredError.add((msg:String) -> {
        trace('[VLC] Encountered an error: $msg');
        stopVideo();
      });

      vid.bitmap.onEndReached.add(stopVideo);

      vid.bitmap.onFormatSetup.add(() -> {
        vid.setGraphicSize(FlxG.initialWidth, FlxG.initialHeight);
        vid.updateHitbox();
        vid.screenCenter();
      });

      FlxG.state.add(vid);

      var filePath:String = Paths.videos(VIDEO_PATH);
      if (vid.load(filePath)) vid.play();
    }
    else
    {
      trace('ALERT: Video is null! Could not play remnants credits!');
      stopVideo();
    }
  }

  public function stopVideo():Void
  {
    vid.stop();
    playingVideo = false;

    FlxG.state.scrollPaused = false;

    FunkinSound.playMusic('creditsSong-remnants',
      {
        startingVolume: 0.0,
        overrideExisting: true,
        restartTrack: true,
        loop: true
      });
    FlxG.sound.music.fadeIn(6, 0, 0.8);
  }

  override public function onUpdate(event:UpdateScriptEvent):Void
  {
    super.onUpdate(event);

    if (vid?.bitmap != null)
    {
      var pressingEnter:Bool = FlxG.keys.pressed.ENTER || FlxG.keys.pressed.SPACE;
      if (FlxG.onMobile) justPressedEnter = TouchUtil.pressed && !TouchUtil.overlaps(backButton);

      if (playingVideo)
      {
        vid.bitmap.rate = pressingEnter ? 2 : 1;

        if (FlxG.state.controls.PAUSE_P || FlxG.keys.justPressed.SHIFT) vid.pause();
        else if (FlxG.state.controls.PAUSE_R || FlxG.keys.justReleased.SHIFT) vid.resume();
      }
      else if (pressingEnter)
      {
        // Move the whole group by the base scroll speed.
        vid.y -= CreditsState.CREDITS_SCROLL_FAST_SPEED * event.elapsed;
      }
      else if (FlxG.state.controls.PAUSE || FlxG.keys.pressed.SHIFT)
      {
        // Stop the whole group from moving.
        vid.y -= CreditsState.CREDITS_SCROLL_PAUSE_SPEED * event.elapsed;
      }
      else
      {
        // Move the whole group by the base scroll speed.
        vid.y -= CreditsState.CREDITS_SCROLL_BASE_SPEED * event.elapsed;
      }

      if (vidBG != null) vidBG.y = vid.y;

      if (vid.y <= -vid.height)
      {
        vid.destroy();
        vid = null;

        vidBG.destroy();
        vidBG = null;

        trace('killed video');
      }
    }
  }
}
