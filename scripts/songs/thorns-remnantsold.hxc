import funkin.save.Save;
import funkin.Preferences;
import funkin.util.TouchUtil;
import funkin.util.FlxTweenUtil;
import funkin.graphics.FunkinSprite;
import flixel.text.FlxTextBorderStyle;
import funkin.modding.module.ModuleHandler;
import funkin.effects.RetroCameraFade;
import funkin.ui.FullScreenScaleMode;
import flixel.util.FlxTimerManager;
import funkin.audio.FunkinSound;
import flixel.tweens.FlxTween;
import funkin.play.song.Song;
import funkin.play.PlayState;
import funkin.util.Constants;
import flixel.util.FlxTimer;
import flixel.tweens.FlxTween;
import flixel.tweens.FlxEase;
import flixel.text.FlxText;
import flixel.FlxSprite;
import flixel.FlxG;

class ThornsOldRemnantsMixSong extends Song
{
  var darkness:FunkinSprite = null;
  var bgSprite:FunkinSprite = null;
  var senpaiCrazy:FunkinSprite = null;
  var localSchoolBoyFoundDead:FlxSprite = null;

  var hasPlayedCutscene:Bool = false;
  var fadeOut:Bool = false;

  var stopSenpai:Bool = false;
  var cutsceneTimerManager:FlxTimerManager;
  var isMobilePauseButtonPressed:Bool = false;
  var localManExplodesFreeSoundEffect:FunkinSound;

  var cutsceneSkipped:Bool = false;
  var canSkipCutscene:Bool = false;

  var black:FunkinSprite;

  public function new()
  {
    super('thorns',
      {
        variation: 'remnantsold'
      });
  }

  public override function isSongNew(currentDifficulty:String, currentVariation:String):Bool
  {
    if (currentVariation == 'remnantsold') return !Save.instance.hasBeatenSong(this.id, null, this.variation);
    return false;
  }

  public override function onCountdownStart(event:CountdownScriptEvent):Void
  {
    super.onCountdownStart(event);

    if (PlayState.instance.camHUD.alpha < 1)
    {
      FlxTween.tween(PlayState.instance.camHUD, {alpha: 1}, 0.7, {ease: FlxEase.linear});
    }

    if (PlayState.instance.currentStage.getNamedProp('pixeloverlay') != null) PlayState.instance.currentStage.getNamedProp('pixeloverlay').alpha = 0;

    // Skip the cutscene if we're playtesting in the Chart Editor.
    if (PlayState.instance.isChartingMode && !hasPlayedCutscene) hasPlayedCutscene = true;
    if (!ModuleHandler.getModule('RemnantsBumpers').scriptCall('getPlayedBumper')
      && ModuleHandler.getModule('RemnantsBumpers').scriptCall('setupBumper'))
    {
      if (bgSprite == null)
      {
        bgSprite = new FunkinSprite(0, 0);
        bgSprite.makeSolidColor(2000, 2500, 0xFF000000);
        bgSprite.cameras = [PlayState.instance.camCutscene];
        bgSprite.zIndex = -1;
        PlayState.instance.add(bgSprite);
        PlayState.instance.refresh();
      }

      ModuleHandler.getModule('RemnantsBumpers').scriptCall('setPlayedBumper', true);
      event.cancel();
    }
    else
    {
      if (!hasPlayedCutscene)
      {
        trace('Pausing countdown to play cutscene.');
        hasPlayedCutscene = true;

        if (bgSprite == null)
        {
          bgSprite = new FunkinSprite(0, 0);
          bgSprite.makeSolidColor(2000, 2500, 0xFF000000);
          bgSprite.cameras = [PlayState.instance.camCutscene];
          bgSprite.zIndex = -1;
          PlayState.instance.add(bgSprite);
          PlayState.instance.refresh();
        }

        startCutscene();
        event.cancel(); // CANCEL THE COUNTDOWN!
      }
    }
  }

  var skipText:FlxText;

  function startCutscene()
  {
    trace('Hit start of song! Starting cutscene.');

    PlayState.instance.disableKeys = true;
    PlayState.instance.camHUD.visible = false;
    PlayState.instance.camCutscene.visible = true;
    PlayState.instance.isInCutscene = true;

    cutsceneTimerManager = new FlxTimerManager();

    skipText = new FlxText(936 * FullScreenScaleMode.wideScale.x, 618 * FullScreenScaleMode.wideScale.y, 0,
      'Skip [ ' + PlayState.instance.controls.getDialogueNameFromToken("CUTSCENE_ADVANCE", true) + ' ]', 20);
    if (FlxG.onMobile)
    {
      skipText.text = 'Skip [Pause Button]';
      skipText.x -= 136;
    }
    skipText.setFormat(Paths.font('vcr.ttf'), 40, 0xFFFFFFFF, "right", FlxTextBorderStyle.OUTLINE, 0xFF000000);
    skipText.scrollFactor.set();
    skipText.borderSize = 2;
    skipText.alpha = 0;
    PlayState.instance.currentStage.add(skipText);

    skipText.cameras = [PlayState.instance.camCutscene];

    darkness = new FunkinSprite(-20, -20).makeSolidColor(FlxG.width * 1.5, FlxG.height * 1.5, 0xFFFFFFFF);
    darkness.color = 0xFF000000;
    darkness.cameras = [PlayState.instance.camCutscene];
    darkness.alpha = 1.0;
    darkness.zIndex = -3;
    PlayState.instance.add(darkness);

    if (bgSprite != null)
    {
      FlxTween.tween(bgSprite, {alpha: 0}, 0.5, null, function() {
        bgSprite.visible = false;
        bgSprite.destroy();
        bgSprite = null;
      });
    }

    senpaiCrazy = FunkinSprite.createSparrow(0, 0, 'remnants/evil/cutscene/senpaiCrazy-remnants');
    senpaiCrazy.cameras = [PlayState.instance.camCutscene];
    senpaiCrazy.animation.addByPrefix('idle', 'Senpai Pre Explosion instance 1', 24, false);
    senpaiCrazy.setGraphicSize(Std.int(senpaiCrazy.width * Constants.PIXEL_ART_SCALE));
    senpaiCrazy.antialiasing = false;
    senpaiCrazy.pixelPerfectRender = true;
    senpaiCrazy.pixelPerfectPosition = true;
    senpaiCrazy.zIndex = -2;

    senpaiCrazy.scrollFactor.set();
    senpaiCrazy.updateHitbox();
    senpaiCrazy.screenCenter();
    senpaiCrazy.x += senpaiCrazy.width / 5;

    senpaiCrazy.alpha = 0;

    PlayState.instance.add(senpaiCrazy);
    PlayState.instance.refresh();

    // Senpai fades in stepwise rather than linear.
    new FlxTimer(cutsceneTimerManager).start(0.3, function(timer:FlxTimer) {
      senpaiCrazy.alpha += 0.15;

      if (senpaiCrazy.alpha < 1)
      {
        timer.reset();
      }
      else
      {
        senpaiCrazy.alpha = 1;

        // Play the actual animation.
        senpaiCrazy.animation.play('idle');

        // Play the SFX alongside it.
        if (!stopSenpai)
        {
          trace('Playing senpai death SFX.');
          localManExplodesFreeSoundEffect = FunkinSound.load(Paths.sound('Senpai_Dies'), 1.0, false, false, false, false, function() {
            trace('Senpai death SFX done.');

            // Called when the sound completes. Clean up the cutscene here.
            // Hard cut to the cutscene.
            PlayState.instance.remove(darkness);
            RetroCameraFade.fadeBlack(PlayState.instance.camCutscene, 6, 1.4);
            RetroCameraFade.fadeBlack(PlayState.instance.camGame, 6, 1.4);
            new FlxTimer(cutsceneTimerManager).start(1.6, _ -> {
              startDialogue();
            });
          });
          localManExplodesFreeSoundEffect.play();
        }

        if (FlxG.random.bool(1)) // 1% chance of this happening.
        {
          new FlxTimer(cutsceneTimerManager).start(1.75, function(deadTime:FlxTimer) {
            // Prevent skipping the cutscene from here.
            cutsceneSkipped = true;
            canSkipCutscene = false;
            skipText?.alpha = 0;
          });

          new FlxTimer(cutsceneTimerManager).start(2.5, function(deadTime:FlxTimer) {
            skipText.visible = false;

            // Prevent skipping the cutscene from here.
            cutsceneSkipped = true;
            canSkipCutscene = false;
            skipText?.alpha = 0;

            PlayState.instance.remove(senpaiCrazy);
            localManExplodesFreeSoundEffect.destroy();

            var localSchoolBoyFoundDead:FlxSprite = new FlxSprite(100, 175).loadGraphic(Paths.image('remnants/memorial'));
            localSchoolBoyFoundDead.cameras = [PlayState.instance.camCutscene];
            localSchoolBoyFoundDead.alpha = 0;
            localSchoolBoyFoundDead.zIndex = -3;
            PlayState.instance.add(localSchoolBoyFoundDead);
            FunkinSound.playOnce(Paths.sound('gameplay/secret/secret', "shared"), 1);

            new FlxTimer(cutsceneTimerManager).start(2.75, function(secret:FlxTimer) {
              FlxTween.tween(localSchoolBoyFoundDead, {alpha: 0}, 1.5,
                {
                  ease: FlxEase.linear,
                  onComplete: function(twn) {
                    PlayState.instance.remove(localSchoolBoyFoundDead);
                    this.endCutscene();
                  }
                });
            });

            new FlxTimer(cutsceneTimerManager).start(0.2, function(timer:FlxTimer) {
              localSchoolBoyFoundDead.alpha += 0.15;

              if (localSchoolBoyFoundDead.alpha < 1)
              {
                timer.reset();
              }
            });
          });
        }
        else // Normal transition.
        {
          // Fade to white 3.2 seconds into the cutscene.
          new FlxTimer(cutsceneTimerManager).start(3.2, function(deadTime:FlxTimer) {
            // Prevent skipping the cutscene from here.
            cutsceneSkipped = true;
            canSkipCutscene = false;
            if (skipText != null) FlxTween.tween(skipText, {alpha: 0}, 0.5, {ease: FlxEase.quadOut});

            // Fade to WHITE.
            RetroCameraFade.fadeWhite(PlayState.instance.camCutscene, 8, 1.4);
            new FlxTimer(cutsceneTimerManager).start(1.4, function(timer:FlxTimer) {
              darkness.color = 0xFFFFFFFF;
              PlayState.instance.remove(senpaiCrazy);

              PlayState.instance.camCutscene.filters = [];
              RetroCameraFade.fadeToBlack(PlayState.instance.camCutscene, 8, 0.8);
            });
          });
        }
      }
    });
  }

  function endCutscene():Void
  {
    PlayState.instance.camHUD.alpha = 0;
    cutsceneTimerManager.clear();
    cutsceneTimerManager.destroy();
    cutsceneTimerManager = null;

    if (bgSprite != null)
    {
      bgSprite.destroy();
      bgSprite = null;
    }

    if (darkness != null) PlayState.instance.remove(darkness);

    if (senpaiCrazy != null) PlayState.instance.remove(senpaiCrazy);

    RetroCameraFade.fadeBlack(PlayState.instance.camCutscene, 6, 1.4);
    RetroCameraFade.fadeBlack(PlayState.instance.camGame, 6, 1.4);
    new FlxTimer().start(1.6, _ -> {
      startDialogue();
    });
  }

  function skipCutscene():Void
  {
    cutsceneSkipped = true;
    canSkipCutscene = false;
    stopSenpai = true;

    if (skipText != null)
    {
      skipText?.destroy();
      skipText = null;
    }

    localManExplodesFreeSoundEffect?.fadeOut(0.5, 0, function() {
      localManExplodesFreeSoundEffect?.stop();
      PlayState.instance.camCutscene.filters = [];
      RetroCameraFade.fadeBlack(PlayState.instance.camCutscene, 6, 1.4);
      RetroCameraFade.fadeBlack(PlayState.instance.camGame, 6, 1.4);
    });

    PlayState.instance.camCutscene.fade(0xFF000000, 0.5, false, null, true);

    new FlxTimer(cutsceneTimerManager).start(0.5, function(_) {
      PlayState.instance.justUnpaused = true;
      PlayState.instance.camCutscene.fade(0xFF000000, 0.5, true, null, true);
      endCutscene();
    });
  }

  function startDialogue()
  {
    PlayState.instance.isInCutscene = false;

    var targetDialogue = 'thorns-remnants';
    if (!Preferences.naughtyness) targetDialogue += "-censored";
    PlayState.instance.disableKeys = false;
    PlayState.instance.camHUD.alpha = 0;
    trace('Playing dialogue...');
    PlayState.instance.startConversation(targetDialogue);
  }

  public override function onDialogueEnd()
  {
    // We may need to wait for the outro to play.
    Countdown.performCountdown();
  }

  /**
   * Replay the cutscene after leaving the song.
   */
  function onCreate(event:ScriptEvent):Void
  {
    super.onCreate(event);

    black = new FunkinSprite(-20, -20);
    black.makeSolidColor(Std.int(FlxG.width * 1.5), Std.int(FlxG.height * 1.5), 0xFF000000);
    black.cameras = [PlayState.instance.camCutscene];
    PlayState.instance.add(black);
    black.alpha = 0;

    PlayState.instance.camHUD.alpha = 0;
    hasPlayedDialogue = false;
  }

  /**
   * Don't replay the cutscene between restarts.
   */
  function onSongRetry(event:ScriptEvent)
  {
    super.onSongRetry(event);

    hasPlayedDialogue = true;

    FlxG.camera.stopFX();
    FlxTween.cancelTweensOf(black);

    black.alpha = 0;
  }

  function onGameOver(event:ScriptEvent):Void
  {
    FlxG.camera.stopFX();
    FlxTween.cancelTweensOf(black);
  }

  function onPause()
  {
    FlxG.camera?.active = false;
    FlxTweenUtil.pauseTweensOf(black);
  }

  function onResume()
  {
    FlxG.camera?.active = true;
    FlxTweenUtil.resumeTweensOf(black);
  }

  function onBeatHit(event:ScriptEvent):Void
  {
    super.onBeatHit(event);

    switch (event.beat)
    {
      case 0:
        FlxTween.tween(black, {alpha: 0.25}, 1.25, {ease: FlxEase.smoothStepInOut});
      case 28:
        FlxTween.tween(black, {alpha: 0.5}, 1.25, {ease: FlxEase.smoothStepOut});
      case 32:
        FlxG.camera.flash(0xFFFFFFFF, 0.5);
        black.alpha = 0;
      case 96:
        FlxTween.tween(black, {alpha: 0.25}, 1.25, {ease: FlxEase.sineOut});
      case 127:
        FlxTween.tween(black, {alpha: 0}, 0.3, {ease: FlxEase.backIn});
      case 256:
        FlxTween.tween(black, {alpha: 0.25}, 1, {ease: FlxEase.sineOut});
      case 320:
        FlxG.camera.flash(0xFFFFFFFF, 0.5);
        black.alpha = 0;
      case 384:
        black.alpha = 1;
    }
  }

  function onUpdate(event:UpdateScriptEvent)
  {
    super.onUpdate(event);

    if (cutsceneTimerManager != null)
    {
      cutsceneTimerManager.update(event.elapsed);
    }

    if (!ModuleHandler.getModule('RemnantsBumpers').scriptCall('getIsPlaying'))
    {
      if (FlxG.onMobile)
      {
        isMobilePauseButtonPressed = TouchUtil.overlapsComplex(PlayState.instance.pauseButton) && TouchUtil.justPressed;
      }

      if (PlayState.instance.isInCutscene)
      {
        if ((PlayState.instance.controls.CUTSCENE_ADVANCE || isMobilePauseButtonPressed) && !cutsceneSkipped)
        {
          if (!canSkipCutscene)
          {
            trace('cant skip yet!');
            FlxTween.tween(skipText, {alpha: 1}, 0.5, {ease: FlxEase.quadOut});
            new FlxTimer().start(0.5, function(_) {
              canSkipCutscene = true;
              trace('can skip!');
            });
          }
        }
      }

      if ((PlayState.instance.controls.CUTSCENE_ADVANCE || isMobilePauseButtonPressed) && canSkipCutscene && !cutsceneSkipped)
      {
        skipCutscene();
        trace('Skipped cutscene!');
      }
    }
  }
}
