import funkin.play.song.Song;
import funkin.save.Save;
import funkin.play.PlayState;
import funkin.graphics.FunkinSprite;
import funkin.audio.FunkinSound;
import funkin.util.FlxTweenUtil;
import flixel.util.FlxTimer;
import flixel.tweens.FlxTween;
import flixel.tweens.FlxEase;
import flixel.FlxG;

class RemnantsPicoSong extends Song
{
  var playedCutscene:Bool = false;

  var fadeOut:Bool = false;
  var fadeHud:Bool = false;

  function new()
  {
    super('remnants',
      {
        variation: 'pico'
      });
  }

  public override function isSongNew(currentDifficulty:String, currentVariation:String):Bool
  {
    if (currentDifficulty == 'easy' || currentDifficulty == 'normal' || currentDifficulty == 'hard')
    {
      return !Save.instance.hasBeatenSong(this.id, null, this.variation);
    }
    return false;
  }

  public override function listAltInstrumentalIds(difficultyId:String, variationId:String):Array<String>
  {
    var results:Array<String> = super.listAltInstrumentalIds(difficultyId, variationId);
    var hasBeatenRemnantsMix = Save.instance.hasBeatenSong(this.id, null, this.variation);
    if (hasBeatenRemnantsMix) return results;
  }

  function onCreate(event:ScriptEvent):Void
  {
    super.onCreate(event);

    PlayState.instance.camHUD.alpha = 0;
    fadeOut = false;
    fadeHud = false;
    playedCutscene = false;
  }

  function onSongRetry(event:ScriptEvent)
  {
    super.onSongRetry(event);

    if (fadeOut)
    {
      PlayState.instance.camCutscene?.stopFX();
      PlayState.instance.camCutscene.fade(0xFF000000, 0.5, true, null, false);
      fadeOut = false;
    }

    if (fadeHud)
    {
      FlxTween.cancelTweensOf(PlayState.instance.camHUD);
      FlxTween.tween(PlayState.instance.camHUD, {alpha: 0}, 0.5, {ease: FlxEase.linear});
      fadeHud = false;
    }
    else
    {
      PlayState.instance.camHUD.visible = false;
    }
  }

  function onGameOver(event:ScriptEvent):Void
  {
    PlayState.instance.camCutscene?.stopFX();
    FlxTween.cancelTweensOf(PlayState.instance.camHUD);
    PlayState.instance.camHUD.alpha = 0;
    fadeOut = false;
    fadeHud = false;
  }

  function onPause()
  {
    PlayState.instance.camCutscene?.active = false;
    FlxTweenUtil.pauseTweensOf(PlayState.instance.camHUD);
  }

  function onResume()
  {
    PlayState.instance.camCutscene?.active = true;
    FlxTweenUtil.resumeTweensOf(PlayState.instance.camHUD);
  }

  function onCountdownStart(event:ScriptEvent):Void
  {
    super.onCountdownStart(event);

    if (!playedCutscene)
    {
      trace('Pausing countdown to play in-game cutscene (`remnants-pico`)');
      event.cancel();

      playCutscene();
      playedCutscene = true;
    }

    PlayState.instance.camHUD.alpha = 0;
    PlayState.instance.camHUD.visible = true;
  }

  function playCutscene():Void
  {
    PlayState.instance.tweenCameraToPosition(620, 515.5, 0);

    var cutsceneMusic:FunkinSound = FunkinSound.load(Paths.music("freeplayRandom-remnants/freeplayRandom-remnants"), 0.0, true);
    var cutsceneAudio:FunkinSound = FunkinSound.load(Paths.sound('gameplay/cutscene/remnants-pico-cutscene'), 1.0);

    var dadPosition:FlxPoint = PlayState.instance.currentStage?.getDadPosition();
    var bfPosition:FlxPoint = PlayState.instance.currentStage?.getBoyfriendPosition();

    var pico:FunkinSprite = FunkinSprite.createTextureAtlas((bfPosition?.x ?? 0) - 260, (bfPosition?.y ?? 0) - 490,
      'stages/sketchRemnants/pico/cutscene/pico', 'shared');
    pico.anim.addByTimeline('anim', pico.anim.getDefaultTimeline(), 24, false);
    pico.shader = PlayState.instance.currentStage?.getBoyfriend()?.shader ?? null;
    pico.zIndex = PlayState.instance.currentStage?.getBoyfriend()?.zIndex ?? 0;
    PlayState.instance?.currentStage?.getBoyfriend()?.visible = false;
    PlayState.instance?.currentStage?.add(pico);

    var remnantsPico:FunkinSprite = FunkinSprite.createTextureAtlas((dadPosition?.x ?? 0) - 237, (dadPosition?.y ?? 0) - 483,
      'stages/sketchRemnants/pico/cutscene/remnants pico', 'shared');
    remnantsPico.anim.addByTimeline('anim', remnantsPico.anim.getDefaultTimeline(), 24, false);
    remnantsPico.shader = PlayState.instance.currentStage?.getDad()?.shader ?? null;
    remnantsPico.zIndex = PlayState.instance.currentStage?.getDad()?.zIndex ?? 0;
    PlayState.instance?.currentStage?.getDad()?.visible = false;
    PlayState.instance?.currentStage?.add(remnantsPico);

    PlayState.instance?.currentStage?.refresh();

    PlayState.instance.currentStage?.getGirlfriend()?.animation?.onFinish?.add(PlayState.instance.currentStage?.getGirlfriend()?.dance);

    cutsceneMusic.play();
    cutsceneMusic.fadeIn(3.0, 0.0, 0.3);

    FlxTimer.wait(2, () -> {
      pico.anim.play('anim', true);
      remnantsPico.anim.play('anim', true);
      cutsceneAudio.play();
    });

    pico.animation.onFinish.addOnce(() -> {
      cutsceneMusic.fadeOut(0.5, 0, (_) -> {
        PlayState.instance?.currentStage?.getBoyfriend()?.visible = true;
        PlayState.instance?.currentStage?.getDad()?.visible = true;

        PlayState.instance.currentStage?.getGirlfriend()?.animation?.onFinish?.remove(PlayState.instance.currentStage?.getGirlfriend()?.dance);

        PlayState.instance?.currentStage?.remove(pico);
        PlayState.instance?.currentStage?.remove(remnantsPico);

        // Wait until next update to destroy these...
        FlxTimer.wait(0.0, () -> {
          pico?.destroy();
          remnantsPico?.destroy();
        });

        PlayState.instance.startCountdown();
      });
    });
  }

  function onBeatHit(event:ScriptEvent):Void
  {
    super.onBeatHit(event);

    if (PlayState.instance.isGameOverState) return;
    switch (event.beat)
    {
      case 12:
        FlxTween.tween(PlayState.instance.camHUD, {alpha: 1}, 1.7, {ease: FlxEase.linear});
        fadeHud = true;

      case 192:
        PlayState.instance.camCutscene.fade(0xFF000000, 6.8, false, null, false);
        fadeOut = true;
        fadeHud = false;

      case 223:
        PlayState.instance.camCutscene.fade(0xFF000000, 0.4, true, null, false);
        fadeOut = false;
        fadeHud = true;

      case 273:
        FlxTween.tween(PlayState.instance.camHUD, {alpha: 0}, 1.7, {ease: FlxEase.linear});
        fadeHud = false;

      case 288:
        PlayState.instance.camCutscene.fade(0xFF000000, 1.7, false, null, false);
        fadeOut = true;
    }
  }
}

