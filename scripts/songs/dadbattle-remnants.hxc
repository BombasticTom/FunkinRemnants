import funkin.modding.module.ModuleHandler;
import funkin.audio.FunkinSound;
import funkin.play.song.Song;
import funkin.play.PlayState;
import funkin.save.Save;
import funkin.Conductor;
import funkin.util.FlxTweenUtil;
import flixel.tweens.FlxTween;
import flixel.tweens.FlxEase;
import flixel.FlxG;

class DadBattleRemnantsMixSong extends Song
{
  var fadeOut:Bool = false;

  public function new()
  {
    super('dadbattle',
      {
        variation: 'remnants'
      });
  }

  public override function isSongNew(currentDifficulty:String, currentVariation:String):Bool
  {
    if (currentVariation == 'remnants') return !Save.instance.hasBeatenSong(this.id, null, this.variation);
    return false;
  }

  function onCountdownStart(event:ScriptEvent):Void
  {
    super.onCountdownStart(event);

    if (!ModuleHandler.getModule('RemnantsBumpers').scriptCall('getPlayedBumper')
      && ModuleHandler.getModule('RemnantsBumpers').scriptCall('setupBumper'))
    {
      event.cancel();
      ModuleHandler.getModule('RemnantsBumpers').scriptCall('setPlayedBumper', true);
    }
  }

  var bfAttackSound:FunkinSound;
  var dadKickSound:FunkinSound;

  var attackerHitTimes:Array<Float> = [];
  var attackerHitStrumID:Array<Float> = [];

  function onCreate():Void
  {
    bfAttackSound = FunkinSound.load(Paths.sound('bfAttack'), 0.5);
    dadKickSound = FunkinSound.load(Paths.sound('dadKick'), 0.5);

    PlayState.instance.camHUD.fade(0xFF000000, 0, false, null, false);
    fadeOut = false;
  }

  function onSongLoaded(event:SongLoadScriptEvent)
  {
    attackerHitTimes = [];

    for (note in event.notes)
    {
      switch (note.kind)
      {
        case 'battle-dodge-remnants':
          attackerHitTimes.push(note.time);
          attackerHitStrumID.push(note.getStrumlineIndex());
      }
    }
  }

  function onUpdate(event:UpdateScriptEvent):Void
  {
    while (attackerHitTimes.length > 0)
    {
      if (Conductor.instance.songPosition < attackerHitTimes[0]) break;

      var time:Float = attackerHitTimes.shift();

      var attacker:BaseCharacter = null;
      var target:BaseCharacter = null;
      var attackSound:FunkinSound = null;

      if (attackerHitStrumID.shift() != 0)
      {
        attacker = PlayState.instance.currentStage.getBoyfriend();
        target = PlayState.instance.currentStage.getDad();
        attackSound = bfAttackSound;
      }
      else
      {
        attacker = PlayState.instance.currentStage.getDad();
        target = PlayState.instance.currentStage.getBoyfriend();
        attackSound = dadKickSound;
      }

      attacker.holdTimer = 0;
      attacker.playAnimation('attack', true);
      attackSound.play(true);

      var higher:Int = Math.max(attacker.zIndex, target.zIndex);
      var lower:Int = Math.min(attacker.zIndex, target.zIndex);
      attacker.zIndex = higher;
      target.zIndex = lower;
      PlayState.instance.currentStage.refresh();
    }
  }

  function onNoteHit(event:HitNoteScriptEvent)
  {
    var main:BaseCharacter = null;
    var opposite:BaseCharacter = null;
    var attackSound:FunkinSound = null;

    if (event.note.noteData.getMustHitNote())
    {
      main = PlayState.instance.currentStage.getBoyfriend();
      opposite = PlayState.instance.currentStage.getDad();
      attackSound = bfAttackSound;
    }
    else
    {
      main = PlayState.instance.currentStage.getDad();
      opposite = PlayState.instance.currentStage.getBoyfriend();
      attackSound = dadKickSound;
    }

    switch (event.note.kind)
    {
      case 'battle-prepare-remnants':
        main.holdTimer = 0;
        main.playAnimation('preAttack', true);

      case 'battle-attack-remnants':
        var getsHit:Bool = FlxG.random.bool(50);

        var animName:String = getsHit ? 'hit' : 'dodge';
        if (!getsHit && opposite.hasAnimation('block')) animName = 'block';

        main.holdTimer = 0;
        opposite.holdTimer = 0;

        main.playAnimation('attack', true);
        opposite.playAnimation(animName, true);

        var higher:Int = Math.max(main.zIndex, opposite.zIndex);
        var lower:Int = Math.min(main.zIndex, opposite.zIndex);
        main.zIndex = higher;
        opposite.zIndex = lower;
        PlayState.instance.currentStage.refresh();

        attackSound.play(true);

        if (getsHit)
        {
          // Reward the player for a random outcome. I feel nice today.
          // This does not apply when the opponents plays the note.
          event.healthChange *= 1.5;

          // Penalize the player for a random outcome. I feel EVIL today.
          // I'm pretty sure this will be a Funkin' Remnants Remnant...
          if (!event.note.noteData.getMustHitNote() && getsHit)
          {
            var penalty:Float = 0.15 * 2;
            if (PlayState.instance.health - penalty >= 0)
            {
              PlayState.instance.health -= penalty;
            }
            else
            {
              PlayState.instance.health = 0.00001;
            }
          }
        }

      case 'battle-dodge-remnants':
        var animName:String = main.hasAnimation('block') ? 'block' : 'dodge';

        main.holdTimer = 0;
        main.playAnimation(animName, true);
    }
  }

  function onNoteMiss(event:NoteScriptEvent)
  {
    switch (event.note.kind)
    {
      case 'battle-dodge-remnants':
        // Penalize the player for not hitting the note.
        event.healthChange -= 0.15 * 2;

        // Play the hit animation instead of a miss one.
        var boyfriend:BaseCharacter = PlayState.instance.currentStage.getBoyfriend();
        boyfriend.holdTimer = 0;
        boyfriend.playAnimation('hit', true);
        event.playSound = false;
    }
  }

  function onSongRetry(event:ScriptEvent)
  {
    super.onSongRetry(event);

    PlayState.instance.camCutscene?.stopFX();
    FlxTween.cancelTweensOf(PlayState.instance.camHUD);

    if (fadeOut)
    {
      PlayState.instance.camCutscene.fade(0xFF000000, 0.5, false, null, false);
      fadeOut = false;
    }
    else
    {
      PlayState.instance.camCutscene.fade(0xFF000000, 0, false, null, false);
    }
  }

  function onGameOver(event:ScriptEvent):Void
  {
    PlayState.instance.camCutscene?.stopFX();
    FlxTween.cancelTweensOf(PlayState.instance.camHUD);
    PlayState.instance.camHUD.alpha = 0;
    fadeOut = false;
  }

  function onPause()
  {
    PlayState.instance.camCutscene?.active = false;
    FlxTweenUtil.pauseTweensOf(PlayState.instance.camHUD);
  }

  function onResume()
  {
    PlayState.instance.camCutscene?.active = true;
    FlxTweenUtil.resumeTweensOf(PlayState.instance.camHUD);
  }

  function onSongStart(event:ScriptEvent):Void
  {
    super.onSongStart(event);

    PlayState.instance.camCutscene.fade(0xFF000000, 6, true, null, false);
    PlayState.instance.camHUD.fade(0xFF000000, 0, true, null, false);
    PlayState.instance.camHUD.alpha = 0;
  }

  function onBeatHit(event:ScriptEvent):Void
  {
    super.onBeatHit(event);

    if (PlayState.instance.isGameOverState) return;
    switch (event.beat)
    {
      case 12:
        FlxTween.tween(PlayState.instance.camHUD, {alpha: 1}, 1.5,
          {
            ease: FlxEase.linear,
            onComplete: function(_) {
              fadeOut = true;
            }
          });
      case 300:
        PlayState.instance.camCutscene.fade(0xFF000000, 1.5, false, null, false);
        fadeOut = false;
    }
  }
}
