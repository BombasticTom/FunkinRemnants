import funkin.graphics.FunkinSprite;
import funkin.modding.module.ModuleHandler;
import funkin.modding.base.ScriptedFunkinSprite;
import funkin.play.cutscene.VideoCutscene;
import flixel.text.FlxTextBorderStyle;
import funkin.data.song.SongRegistry;
import funkin.ui.FullScreenScaleMode;
import flixel.util.FlxTimerManager;
import funkin.play.stage.StageProp;
import funkin.audio.FunkinSound;
import flixel.tweens.FlxTween;
import funkin.play.song.Song;
import funkin.play.Countdown;
import funkin.play.PlayState;
import flixel.tweens.FlxEase;
import funkin.util.TouchUtil;
import flixel.util.FlxTimer;
import flixel.text.FlxText;
import funkin.save.Save;
import funkin.Conductor;
import flixel.FlxG;

class DarnellRemnantsMixSong extends Song
{
  var hasPlayedCutscene:Bool = false;
  var cutsceneSkipped:Bool = false;
  var canSkipCutscene:Bool = false;

  // Cutscene sounds
  var canKnee:FunkinSound;
  var canKick:FunkinSound;
  var fireLight:FunkinSound;

  var gunCock:FunkinSound; // Devious sound name...
  var gunShoot:FunkinSound;

  var darnellLaugh:FunkinSound;
  var neneLaugh:FunkinSound;

  //
  var cutsceneTimerManager:FlxTimerManager;
  var isMobilePauseButtonPressed:Bool = false;

  var cutsceneMusic:FunkinSound;
  var cutsceneConductor:Conductor;
  var bgSprite:FunkinSprite = null;

  public function new()
  {
    super('darnell',
      {
        variation: 'remnants'
      });

    hasPlayedCutscene = false;
  }

  public override function isSongNew(currentDifficulty:String, currentVariation:String):Bool
  {
    if (currentVariation == 'remnants') return !Save.instance.hasBeatenSong(this.id, null, this.variation);
    return false;
  }

  public override function listAltInstrumentalIds(difficultyId:String, variationId:String):Array<String>
  {
    var results:Array<String> = super.listAltInstrumentalIds(difficultyId, variationId);
    var hasBeatenRemnantsMix = Save.instance.hasBeatenSong(this.id, null, this.variation);
    if (difficultyId == 'easy' || difficultyId == 'normal' || difficultyId == 'hard')
    {
      var hasBeatenBFMix = Save.instance.hasBeatenSong(this.id, null, 'bf');
      if (!hasBeatenBFMix) results.remove('bf');
    }
    if (hasBeatenRemnantsMix) return results;
  }

  public override function onCountdownStart(event:CountdownScriptEvent):Void
  {
    super.onCountdownStart(event);

    // Skip the cutscene if we're playtesting in the Chart Editor.
    if (PlayState.instance.isChartingMode && !hasPlayedCutscene) hasPlayedCutscene = true;
    if (!ModuleHandler.getModule('RemnantsBumpers').scriptCall('getPlayedBumper')
      && ModuleHandler.getModule('RemnantsBumpers').scriptCall('setupBumper'))
    {
      if (bgSprite == null)
      {
        bgSprite = new FunkinSprite(0, 0);
        bgSprite.makeSolidColor(2000, 2500, 0xFF000000);
        bgSprite.cameras = [PlayState.instance.camCutscene];
        bgSprite.zIndex = -10000;
        PlayState.instance.add(bgSprite);
        PlayState.instance.refresh();
      }

      ModuleHandler.getModule('RemnantsBumpers').scriptCall('setPlayedBumper', true);
      event.cancel();
    }
    else
    {
      if (!hasPlayedCutscene)
      {
        hasPlayedCutscene = true;
        event.cancel();

        if (bgSprite == null)
        {
          bgSprite = new FunkinSprite(0, 0);
          bgSprite.makeSolidColor(2000, 2500, 0xFF000000);
          bgSprite.cameras = [PlayState.instance.camCutscene];
          bgSprite.zIndex = -10000;
          PlayState.instance.add(bgSprite);
          PlayState.instance.refresh();
        }

        PlayState.instance.camHUD.visible = false;
        introCutscene();
      }
    }
  }

  var skipText:FlxText;

  function introCutscene():Void
  {
    PlayState.instance.isInCutscene = true;
    PlayState.instance.mayPauseGame = false;

    cutsceneTimerManager = new FlxTimerManager();

    skipText = new FlxText(936 * FullScreenScaleMode.wideScale.x, 618 * FullScreenScaleMode.wideScale.y, 0,
      'Skip [ ' + PlayState.instance.controls.getDialogueNameFromToken("CUTSCENE_ADVANCE", true) + ' ]', 20);
    if (FlxG.onMobile)
    {
      skipText.text = 'Skip [Pause Button]';
      skipText.x -= 136;
    }
    skipText.setFormat(Paths.font('vcr.ttf'), 40, 0xFFFFFFFF, "right", FlxTextBorderStyle.OUTLINE, 0xFF000000);
    skipText.scrollFactor.set();
    skipText.borderSize = 2;
    skipText.alpha = 0;
    PlayState.instance.currentStage.add(skipText);

    skipText.cameras = [PlayState.instance.camCutscene];

    var picoPos:Array<Float> = [
      PlayState.instance.currentStage.getBoyfriend().cameraFocusPoint.x,
      PlayState.instance.currentStage.getBoyfriend().cameraFocusPoint.y
    ];
    var darnellPos:Array<Float> = [
      PlayState.instance.currentStage.getDad().cameraFocusPoint.x,
      PlayState.instance.currentStage.getDad().cameraFocusPoint.y
    ];

    var cutsceneDelay:Float = 2;

    cutsceneMusic = FunkinSound.load(Paths.music("darnellCanCutscene/darnellCanCutscene", "weekend1"), true);
    cutsceneMusic.volume = 1;

    cutsceneConductor = new Conductor();

    var songMusicData:Null<SongMusicData> = SongRegistry.instance.parseMusicData('darnellCanCutscene');
    if (songMusicData != null)
    {
      cutsceneConductor.mapTimeChanges(songMusicData.timeChanges);
    }

    cutsceneConductor.onBeatHit.add(onCutsceneBeatHit);

    var cutsceneCan:FunkinSprite = FunkinSprite.createSparrow(darnellPos[0], darnellPos[1], 'remnants/cutscenes/wked1_cutscene_1_can-remnants');
    cutsceneCan.animation.addByPrefix('forward', "can kick quick0", 24, false);
    cutsceneCan.animation.addByPrefix('up', "can kicked up0", 24, false);
    PlayState.instance.currentStage.add(cutsceneCan);
    cutsceneCan.visible = false;

    var spraycanPile = PlayState.instance.currentStage.getNamedProp('spraycanPile');
    cutsceneCan.x = spraycanPile.x + 30;
    cutsceneCan.y = spraycanPile.y - 320;
    cutsceneCan.zIndex = spraycanPile.zIndex - 1;
    PlayState.instance.currentStage.refresh();

    var newCan:ScriptedFunkinSprite = ScriptedFunkinSprite.init('SpraycanAtlasSpriteRemnants', 0, 0);
    var spraycanPile = PlayState.instance.currentStage.getNamedProp('spraycanPile');
    if (newCan != null)
    {
      newCan.x = spraycanPile.x - 100;
      newCan.y = spraycanPile.y - 550;
      newCan.zIndex = 300;
      PlayState.instance.currentStage.add(newCan);
      PlayState.instance.currentStage.refresh();
      newCan.visible = false;
    }
    PlayState.instance.currentStage.getBoyfriend().playAnimation('intro1', true);

    // camera sets up, pico does his animation showing him pissed
    new FlxTimer(cutsceneTimerManager).start(0.1, function(_) {
      PlayState.instance.tweenCameraToPosition(picoPos[0] + 250, picoPos[1], 0);
      PlayState.instance.tweenCameraZoom(1.3, 0, true, FlxEase.quadInOut);
    });

    new FlxTimer(cutsceneTimerManager).start(0.7, function(_) {
      cutsceneMusic.play(false);
      FlxTween.tween(bgSprite, {alpha: 0}, 2, {startDelay: 0.3}, function() {
        bgSprite.visible = false;
        bgSprite.destroy();
        bgSprite = null;
      });
    });

    // move camera out to show everything
    new FlxTimer(cutsceneTimerManager).start(cutsceneDelay, function(_) {
      PlayState.instance.tweenCameraToPosition(darnellPos[0] + 100, darnellPos[1], 2.5, FlxEase.quadInOut);
      PlayState.instance.tweenCameraZoom(0.66, 2.5, true, FlxEase.quadInOut);
    });

    // darnell lights up a can
    new FlxTimer(cutsceneTimerManager).start(cutsceneDelay + 3, function(_) {
      fireLight = FunkinSound.load(Paths.sound('Darnell_Lighter'), 1);
      fireLight.volume = 1;
      fireLight.play(false);

      PlayState.instance.currentStage.getDad().playAnimation('lightCan', true);
    });

    // pico cocks his gun, camera shifts to his side to show this
    new FlxTimer(cutsceneTimerManager).start(cutsceneDelay + 4, function(_) {
      PlayState.instance.currentStage.getBoyfriend().playAnimation('cock', true);
      PlayState.instance.tweenCameraToPosition(darnellPos[0] + 180, darnellPos[1], 0.4, FlxEase.backOut);

      gunCock = FunkinSound.load(Paths.sound('Gun_Prep'), 1);
      gunCock.volume = 1;
      gunCock.play(false);
    });

    // darnell kicks the can up
    new FlxTimer(cutsceneTimerManager).start(cutsceneDelay + 4.4, function(_) {
      PlayState.instance.currentStage.getDad().playAnimation('kickCan', true);

      canKnee = FunkinSound.load(Paths.sound('Kick_Can_UP'), 1);
      canKnee.volume = 1;
      canKnee.play(false);

      cutsceneCan.animation.play('up');
      cutsceneCan.visible = true;
    });

    // darnell knees the can forward
    new FlxTimer(cutsceneTimerManager).start(cutsceneDelay + 4.9, function(_) {
      PlayState.instance.currentStage.getDad().playAnimation('kneeCan', true);

      canKick = FunkinSound.load(Paths.sound('Kick_Can_FORWARD'), 1);
      canKick.volume = 1;
      canKick.play(false);

      cutsceneCan.animation.play('forward');
    });

    // pico shoots the can, it explodes
    new FlxTimer(cutsceneTimerManager).start(cutsceneDelay + 5.1, function(_) {
      PlayState.instance.currentStage.getBoyfriend().playAnimation('intro2', true, true);

      // Prevent skipping the cutscene from here.
      cutsceneSkipped = true;
      canSkipCutscene = false;
      FlxTween.tween(skipText, {alpha: 0}, 0.5, {ease: FlxEase.quadOut});

      gunShoot = FunkinSound.load(Paths.soundRandom('shot', 1, 4), 1);
      gunShoot.volume = 1;
      gunShoot.play(false);

      PlayState.instance.tweenCameraToPosition(darnellPos[0] + 100, darnellPos[1], 1, FlxEase.quadInOut);

      newCan.scriptCall('playCanShot');
      newCan.visible = true;

      cutsceneCan.visible = false;
      new FlxTimer(cutsceneTimerManager).start(1 / 24, function(_) darkenStageProps());
    });

    // darnell laughs
    new FlxTimer(cutsceneTimerManager).start(cutsceneDelay + 5.9, function(_) {
      PlayState.instance.currentStage.getDad().playAnimation('laughCutscene', true);

      darnellLaugh = FunkinSound.load(Paths.sound('cutscene/remnants/darnell_laugh'), 0.6);
      darnellLaugh.volume = 0.6;
      darnellLaugh.play(false);
    });

    // nene spits and laughs
    new FlxTimer(cutsceneTimerManager).start(cutsceneDelay + 6.2, function(_) {
      PlayState.instance.currentStage.getGirlfriend().playAnimation('laughCutscene', true);

      neneLaugh = FunkinSound.load(Paths.sound('cutscene/remnants/nene_laugh'), 0.6);
      neneLaugh.volume = 0.6;
      neneLaugh.play(false);
    });

    // camera returns to normal, cutscene flags set and countdown starts.
    new FlxTimer(cutsceneTimerManager).start(cutsceneDelay + 8, function(_) {
      PlayState.instance.tweenCameraZoom(0.575, 2, true, FlxEase.sineInOut);
      PlayState.instance.tweenCameraToPosition(darnellPos[0] + 180, darnellPos[1], 2, FlxEase.sineInOut);
      endCutscene();
    });
  }

  function onCutsceneBeatHit()
  {
    // Play idle if Darnell isn't busy.
    if (PlayState.instance.currentStage.getDad().isAnimationFinished()
      && PlayState.instance.currentStage.getDad().getCurrentAnimation() != 'lightCan')
    {
      PlayState.instance.currentStage.getDad().dance(true);
    }

    if (PlayState.instance.currentStage.getGirlfriend().isAnimationFinished())
    {
      PlayState.instance.currentStage.getGirlfriend().dance(true);
    }
  }

  function darkenStageProps()
  {
    // Darken the background, then fade it back.
    for (stageProp in PlayState.instance.currentStage.members)
    {
      // Determine if the stage prop is something that should be excluded from darkening.
      if (Std.isOfType(stageProp, StageProp))
      {
        if (stageProp.name == "bf" || stageProp.name == "dad" || stageProp.name == "gf") // This refers to the player.
        {
          // Exclude.
          continue;
        }

        // Newspaper.
        if (stageProp.name == "paper")
        {
          // Exclude.
          continue;
        }
      }

      // Hacky way of selecting PicoPlayable.picoFade.
      if (stageProp.zIndex == (PlayState.instance.currentStage.getBoyfriend().zIndex - 3))
      {
        // Exclude.
        continue;
      }

      // Hacky way of selecting the mist objects.
      // They are not public so this is the only way i think.
      if (stageProp.zIndex == 1000 || stageProp.zIndex == 1001 || stageProp.zIndex == 99 || stageProp.zIndex == 88 || stageProp.zIndex == 39)
      {
        // Exclude.
        continue;
      }

      if (stageProp.zIndex > 299)
      {
        continue;
      }

      if (stageProp.zIndex == 300)
      {
        continue;
      }

      // Skip Text
      if (stageProp == skipText)
      {
        continue;
      }

      // If not excluded, darken.
      stageProp.color = 0xFF111111;
      new FlxTimer().start(1 / 24, (tmr) -> {
        stageProp.color = 0xFF222222;
        FlxTween.color(stageProp, 1.4, 0xFF222222, 0xFFFFFFFF);
      });
    }
  }

  function endCutscene():Void
  {
    cutsceneTimerManager?.clear();
    cutsceneTimerManager?.destroy();
    cutsceneTimerManager = null;

    if (bgSprite != null)
    {
      bgSprite?.destroy();
      bgSprite = null;
    }

    if (skipText != null)
    {
      skipText?.destroy();
      skipText = null;
    }

    canKnee?.stop();
    canKick?.stop();
    fireLight?.stop();

    cutsceneMusic?.stop();

    gunCock?.stop();
    gunShoot?.stop();

    darnellLaugh?.stop();
    neneLaugh?.stop();

    PlayState.instance.isInCutscene = false;
    PlayState.instance.mayPauseGame = true;
    PlayState.instance?.startCountdown();
  }

  function skipCutscene():Void
  {
    cutsceneSkipped = true;
    canSkipCutscene = false;

    PlayState.instance.camCutscene.fade(0xFF000000, 0.5, false, null, true);

    cutsceneMusic?.fadeOut(0.5, 0, function(_) {
      cutsceneMusic?.stop();
      cutsceneMusic?.destroy();
      cutsceneMusic = null;
    });

    new FlxTimer(cutsceneTimerManager).start(0.5, function(_) {
      PlayState.instance.justUnpaused = true;
      PlayState.instance.camCutscene.fade(0xFF000000, 0.5, true, null, true);

      PlayState.instance.cameraFollowPoint.setPosition(PlayState.instance.currentStage.getBoyfriend().cameraFocusPoint.x,
        PlayState.instance.currentStage.getBoyfriend().cameraFocusPoint.y);
      PlayState.instance.resetCamera(true, true, true);

      skipText?.visible = false;
      endCutscene();
    });
  }

  /**
   * Don't replay the cutscene between restarts.
   */
  function onSongRetry(event:ScriptEvent)
  {
    super.onSongRetry(event);

    cutsceneSkipped = true;
    canSkipCutscene = false;
    hasPlayedCutscene = true;
  }

  /**
   * Replay the cutscene after leaving the song.
   */
  function onCreate(event:ScriptEvent):Void
  {
    super.onCreate(event);

    cutsceneSkipped = false;
    canSkipCutscene = false;
    hasPlayedCutscene = false;
  }

  function onUpdate(event:UpdateScriptEvent)
  {
    super.onUpdate(event);

    if (cutsceneConductor != null && cutsceneMusic != null)
    {
      cutsceneConductor.update(cutsceneMusic.time);
    }

    if (cutsceneTimerManager != null)
    {
      cutsceneTimerManager.update(event.elapsed);
    }

    if (!ModuleHandler.getModule('RemnantsBumpers').scriptCall('getIsPlaying'))
    {
      if (FlxG.onMobile)
      {
        isMobilePauseButtonPressed = TouchUtil.overlapsComplex(PlayState.instance.pauseButton) && TouchUtil.justPressed;
      }

      if (PlayState.instance.isInCutscene)
      {
        if ((PlayState.instance.controls.CUTSCENE_ADVANCE || isMobilePauseButtonPressed) && !cutsceneSkipped)
        {
          if (!canSkipCutscene)
          {
            trace('cant skip yet!');
            FlxTween.tween(skipText, {alpha: 1}, 0.5, {ease: FlxEase.quadOut});
            new FlxTimer().start(0.5, function(_) {
              canSkipCutscene = true;
              trace('can skip!');
            });
          }
        }
      }

      if ((PlayState.instance.controls.CUTSCENE_ADVANCE || isMobilePauseButtonPressed) && canSkipCutscene && !cutsceneSkipped)
      {
        skipCutscene();
        trace('Skipped cutscene!');
      }
    }
  }
}
