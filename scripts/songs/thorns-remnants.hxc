import flixel.FlxG;
import funkin.Preferences;
import funkin.save.Save;
import funkin.graphics.FunkinSprite;
import funkin.modding.module.ModuleHandler;
import funkin.effects.RetroCameraFade;
import funkin.audio.FunkinSound;
import flixel.tweens.FlxTween;
import funkin.play.song.Song;
import funkin.play.PlayState;
import funkin.util.Constants;
import flixel.util.FlxTimer;
import flixel.tweens.FlxTween;
import flixel.tweens.FlxEase;
import flixel.FlxG;

class ThornsRemnantsMixSong extends Song
{
  var bgSprite:FunkinSprite;
  var hasPlayedDialogue:Bool;
  var black:FunkinSprite;
  var white:FunkinSprite;
  var fadeBlackTween:FlxTween;
  var fadeWhiteTween:FlxTween;

  public function new()
  {
    super('thorns',
      {
        variation: 'remnants'
      });
  }

  public override function isSongNew(currentDifficulty:String, currentVariation:String):Bool
  {
    if (currentVariation == 'remnants') return !Save.instance.hasBeatenSong(this.id, null, this.variation);
    return false;
  }

  public override function onCountdownStart(event:CountdownScriptEvent):Void
  {
    super.onCountdownStart(event);

    if (PlayState.instance.camHUD.alpha < 1)
    {
      FlxTween.tween(PlayState.instance.camHUD, {alpha: 1}, 0.7, {ease: FlxEase.linear});
    }

    // Skip the cutscene if we're playtesting in the Chart Editor.
    if (PlayState.instance.isChartingMode && !hasPlayedDialogue) hasPlayedDialogue = true;
    if (!ModuleHandler.getModule('RemnantsBumpers').scriptCall('getPlayedBumper')
      && ModuleHandler.getModule('RemnantsBumpers').scriptCall('setupBumper'))
    {
      if (bgSprite == null)
      {
        bgSprite = new FunkinSprite(0, 0);
        bgSprite.makeSolidColor(2000, 2500, 0xFF000000);
        bgSprite.cameras = [PlayState.instance.camCutscene];
        bgSprite.zIndex = -1;
        PlayState.instance.add(bgSprite);
        PlayState.instance.refresh();
      }

      ModuleHandler.getModule('RemnantsBumpers').scriptCall('setPlayedBumper', true);
      event.cancel();
    }
    else
    {
      if (!hasPlayedDialogue)
      {
        trace('Pausing countdown to play cutscene.');

        hasPlayedDialogue = true;

        startCutscene();
        event.cancel(); // CANCEL THE COUNTDOWN!
      }
    }
  }

  function startCutscene()
  {
    trace('Hit start of song! Starting cutscene.');

    PlayState.instance.disableKeys = true;

    PlayState.instance.camHUD.visible = false;
    PlayState.instance.camCutscene.visible = true;

    var darkness = new FunkinSprite(-20, -20).makeSolidColor(FlxG.width * 1.5, FlxG.height * 1.5, 0xFFFFFFFF);
    darkness.color = 0xFF000000;
    darkness.cameras = [PlayState.instance.camCutscene];
    darkness.alpha = 1.0;
    darkness.zIndex = -3;
    PlayState.instance.add(darkness);

    if (bgSprite != null)
    {
      FlxTween.tween(bgSprite, {alpha: 0}, 0.5, null, function() {
        bgSprite.visible = false;
      });
    }

    var senpaiCrazy = FunkinSprite.createSparrow(0, 0, 'remnants/evil/cutscene/senpaiCrazy-remnants');
    senpaiCrazy.cameras = [PlayState.instance.camCutscene];
    senpaiCrazy.animation.addByPrefix('idle', 'Senpai Pre Explosion instance 1', 24, false);
    senpaiCrazy.setGraphicSize(Std.int(senpaiCrazy.width * Constants.PIXEL_ART_SCALE));
    senpaiCrazy.antialiasing = false;
    senpaiCrazy.pixelPerfectRender = true;
    senpaiCrazy.pixelPerfectPosition = true;
    senpaiCrazy.zIndex = -2;

    senpaiCrazy.scrollFactor.set();
    senpaiCrazy.updateHitbox();
    senpaiCrazy.screenCenter();
    senpaiCrazy.x += senpaiCrazy.width / 5;

    senpaiCrazy.alpha = 0;

    PlayState.instance.add(senpaiCrazy);
    PlayState.instance.refresh();

    // Senpai fades in stepwise rather than linear.
    new FlxTimer().start(0.3, function(timer:FlxTimer) {
      senpaiCrazy.alpha += 0.15;

      if (senpaiCrazy.alpha < 1)
      {
        timer.reset();
      }
      else
      {
        senpaiCrazy.alpha = 1;

        // Play the actual animation.
        senpaiCrazy.animation.play('idle');

        // Play the SFX alongside it.
        trace('Playing senpai death SFX.');
        FunkinSound.playOnce(Paths.sound('Senpai_Dies'), 1.0, function() {
          trace('Senpai death SFX done.');
          // Called when the sound completes. Clean up the cutscene here.
          // Hard cut to the cutscene.
          PlayState.instance.remove(darkness);

          RetroCameraFade.fadeBlack(PlayState.instance.camCutscene, 6, 1.4);
          RetroCameraFade.fadeBlack(PlayState.instance.camGame, 6, 1.4);
          new FlxTimer().start(1.6, _ -> {
            startDialogue();
          });
          // PlayState.instance.camCutscene.fade(0xFFEEEEEE, 0.8, true);
        });

        // Fade to white 3.2 seconds into the cutscene.
        new FlxTimer().start(3.2, function(deadTime:FlxTimer) {
          RetroCameraFade.fadeWhite(PlayState.instance.camCutscene, 8, 1.4);
          new FlxTimer().start(1.4, function(timer:FlxTimer) {
            darkness.color = 0xFFFFFFFF;
            PlayState.instance.remove(senpaiCrazy);

            PlayState.instance.camCutscene.filters = [];
            RetroCameraFade.fadeToBlack(PlayState.instance.camCutscene, 8, 0.8);
          });

          // PlayState.instance.camCutscene.fade(0xFFEEEEEE, 1.6, false);
        });
      }
    });
  }

  function startDialogue()
  {
    var targetDialogue = 'thorns-remnants';
    if (!Preferences.naughtyness) targetDialogue += "-censored";
    PlayState.instance.disableKeys = false;
    PlayState.instance.camHUD.alpha = 0;
    trace('Playing dialogue...');
    PlayState.instance.startConversation(targetDialogue);
  }

  /**
   * Don't replay the cutscene between restarts.
   */
  function onSongRetry(event:ScriptEvent)
  {
    super.onSongRetry(event);

    hasPlayedDialogue = true;

    if (fadeBlackTween != null) fadeBlackTween.cancel();
    if (fadeWhiteTween != null) fadeWhiteTween.cancel();

    black.alpha = 0;
    white.alpha = 0;
  }

  /**
   * Replay the cutscene after leaving the song.
   */
  function onCreate(event:ScriptEvent):Void
  {
    super.onCreate(event);

    black = new FunkinSprite(-20, -20);
    black.makeSolidColor(Std.int(FlxG.width * 1.5), Std.int(FlxG.height * 1.5), 0xFF000000);
    black.cameras = [PlayState.instance.camCutscene];
    PlayState.instance.add(black);
    black.alpha = 0;

    white = new FunkinSprite(-20, -20);
    white.makeSolidColor(Std.int(FlxG.width * 1.5), Std.int(FlxG.height * 1.5), 0xFFFFFFFF);
    white.cameras = [PlayState.instance.camCutscene];
    PlayState.instance.add(white);
    white.alpha = 0;

    hasPlayedDialogue = false;
  }

  public override function onDialogueEnd()
  {
    // We may need to wait for the outro to play.
    Countdown.performCountdown();
  }

  function onBeatHit(event:ScriptEvent):Void
  {
    super.onBeatHit(event);

    switch (event.beat)
    {
      case 0:
        fadeBlackTween = FlxTween.tween(black, {alpha: 0.25}, 1.25, {ease: FlxEase.smoothStepInOut});

      case 28:
        fadeBlackTween = FlxTween.tween(black, {alpha: 0.5}, 1.25, {ease: FlxEase.smoothStepOut});

      case 32:
        fadeWhiteTween = FlxTween.tween(white, {alpha: 1}, 0.0001,
          {
            ease: FlxEase.instant,
            onComplete: function(_) {
              fadeWhiteTween = FlxTween.tween(white, {alpha: 0}, 0.5, {ease: FlxEase.linear});
            }
          });
        black.alpha = 0;

      case 96:
        fadeBlackTween = FlxTween.tween(black, {alpha: 0.25}, 1.25, {ease: FlxEase.sineOut});

      case 127:
        fadeBlackTween = FlxTween.tween(black, {alpha: 0}, 0.3, {ease: FlxEase.backIn});

      case 256:
        fadeBlackTween = FlxTween.tween(black, {alpha: 0.25}, 1, {ease: FlxEase.sineOut});

      case 320:
        fadeWhiteTween = FlxTween.tween(white, {alpha: 1}, 0.0001,
          {
            ease: FlxEase.instant,
            onComplete: function(_) {
              fadeWhiteTween = FlxTween.tween(white, {alpha: 0}, 0.5, {ease: FlxEase.linear});
            }
          });
        black.alpha = 0;

      case 384:
        black.alpha = 1;
    }
  }
}

