import funkin.play.song.Song;
import funkin.play.PlayState;
import funkin.Preferences;
import funkin.save.Save;
import funkin.graphics.FunkinSprite;
import funkin.util.FlxTweenUtil;
import flixel.tweens.FlxTween;
import flixel.tweens.FlxEase;
import flixel.FlxSprite;
import flixel.FlxG;

class TormentorSong extends Song
{
  var fadeOut:Bool = false;

  public function new()
  {
    super('tormentor-remnants');
  }

  public override function isSongNew(currentDifficulty:String, currentVariation:String):Bool
  {
    if (currentDifficulty == 'easy' || currentDifficulty == 'normal' || currentDifficulty == 'hard')
    {
      return !Save.instance.hasBeatenSong(this.id, null, '');
    }
    return false;
  }

  function onCreate(event:ScriptEvent):Void
  {
    super.onCreate(event);

    PlayState.instance.camCutscene.fade(0xFF000000, 0, false, null, false);
    PlayState.instance.camHUD.alpha = 0;
    PlayState.instance.disableKeys = true;
    fadeOut = false;
  }

  function onSongRetry(event:ScriptEvent)
  {
    super.onSongRetry(event);

    PlayState.instance.camCutscene?.stopFX();
    FlxTween.cancelTweensOf(PlayState.instance.camHUD);

    PlayState.instance.disableKeys = true;

    if (fadeOut)
    {
      PlayState.instance.camCutscene.fade(0xFF000000, 0.5, false, null, false);
      fadeOut = false;
    }
    else
    {
      PlayState.instance.camCutscene.fade(0xFF000000, 0, false, null, false);
    }
  }

  function onGameOver(event:ScriptEvent):Void
  {
    PlayState.instance.camCutscene?.stopFX();
    FlxTween.cancelTweensOf(PlayState.instance.camHUD);
    PlayState.instance.camHUD.alpha = 0;
    fadeOut = false;
  }

  function onPause()
  {
    PlayState.instance.camCutscene?.active = false;
    FlxTweenUtil.pauseTweensOf(PlayState.instance.camHUD);
  }

  function onResume()
  {
    PlayState.instance.camCutscene?.active = true;
    FlxTweenUtil.resumeTweensOf(PlayState.instance.camHUD);
  }

  function onCountdownStart(event:ScriptEvent):Void
  {
    super.onCountdownStart(event);

    PlayState.instance.camHUD.alpha = 0;
  }

  function onSongStart(event:ScriptEvent):Void
  {
    super.onSongStart(event);

    PlayState.instance.camCutscene.fade(0xFF000000, 3, true, null, false);
  }

  function onBeatHit(event:ScriptEvent):Void
  {
    super.onBeatHit(event);

    if (PlayState.instance.isGameOverState) return;
    switch (event.beat)
    {
      case 14:
        PlayState.instance.camCutscene.fade(0xFF000000, 0.15, false, null, false);
      case 16:
        PlayState.instance.camCutscene.fade(0xFF000000, 0.15, true, null, false);
        PlayState.instance.disableKeys = false;
        fadeOut = true;
      case 27:
        FlxTween.tween(PlayState.instance.camHUD, {alpha: 1}, 1.81, {ease: FlxEase.linear});
      case 320:
        PlayState.instance.camCutscene.fade(0xFF000000, 0, false, null, false);
        PlayState.instance.disableKeys = true;
        fadeOut = false;
    }
  }
}
