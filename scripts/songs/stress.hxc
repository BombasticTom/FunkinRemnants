import funkin.play.song.Song;
import funkin.play.PlayState;
import funkin.save.Save;
import funkin.play.PlayStatePlaylist;
import funkin.util.Constants;
import funkin.play.GameOverSubState;
import funkin.modding.base.ScriptedFlxSpriteGroup;
import funkin.play.cutscene.VideoCutscene;
import openfl.filters.ShaderFilter;
import funkin.graphics.shaders.DropShadowScreenspace;
import flixel.FlxCamera;
import flixel.FlxG;
import flixel.math.FlxPoint;
import flixel.text.FlxText;
import flixel.text.FlxTextBorderStyle;
import funkin.play.character.BaseCharacter;
import funkin.play.character.CharacterDataParser;
import funkin.data.song.SongRegistry;
import funkin.graphics.adobeanimate.FlxAtlasSprite;
import funkin.modding.base.ScriptedFlxAtlasSprite;
import funkin.audio.FunkinSound;
import funkin.Conductor;
import funkin.play.stage.Stage;
import funkin.graphics.FunkinSprite;
import flixel.tweens.FlxEase;
import flixel.tweens.FlxTween;
import flixel.util.FlxTimer;
import flixel.util.FlxTimerManager;
import funkin.Preferences;
import funkin.util.TouchUtil;
import funkin.util.HapticUtil;
import funkin.ui.FullScreenScaleMode;

class StressSong extends Song
{
  var hasPlayedCutscene:Bool;
  var tankmanGroup:TankmanSpriteGroup = null;
  var isMobilePauseButtonPressed:Bool = false;

  public function new()
  {
    super('stress');

    hasPlayedCutscene = false;
  }

  /**
   * Replay the cutscene after leaving the song.
   */
  function onCreate(event:ScriptEvent):Void
  {
    super.onCreate(event);

    hasPlayedCutscene = false;
    cutsceneSkipped = false;
    canSkipCutscene = false;
    startedTalking = false;
  }

  public override function isSongNew(currentDifficulty:String, currentVariation:String):Bool
  {
    if (currentVariation == 'pico') return !Save.instance.hasBeatenSong(this.id, null, 'pico');
    if (currentVariation == 'remnants') return !Save.instance.hasBeatenSong(this.id, null, 'remnants');

    return false;
  }

  public override function listAltInstrumentalIds(difficultyId:String, variationId:String):Array<String>
  {
    if (difficultyId == 'easy' || difficultyId == 'normal' || difficultyId == 'hard')
    {
      var hasBeatenPicoMix = Save.instance.hasBeatenSong(this.id, null, 'pico');
			// var hasBeatenRemnantsMix = Save.instance.hasBeatenSong(this.id, null, 'remnants');

      switch (variationId)
      {
        case 'pico':
          // return hasBeatenPicoMix ? [''] : [];
          // No Pico mix on BF instrumental, sorry!
          return [];
        case 'remnants':
					// if (hasBeatenRemnantsMix) return hasBeatenPicoMix ? ['pico', ''] : [];
          // Remnants Mix Pitch is different!
          return [];
        default:
          // return [(hasBeatenPicoMix) ? 'pico' : null, (hasBeatenRemnantsMix) ? 'remnants' : null];
          // Remnants Mix Pitch is different!
          return hasBeatenPicoMix ? ['pico'] : [];
      }
    }
    return [];
  }

  public override function onCountdownStart(event:CountdownScriptEvent):Void
  {
    super.onCountdownStart(event);

    // Skip the cutscene unless we are in Story Mode, or Pico Mix specifically.
    if (PlayState.instance.currentVariation != 'pico' && PlayState.instance.currentVariation != 'remnants' && !PlayStatePlaylist.isStoryMode)
      hasPlayedCutscene = true;

    if (!hasPlayedCutscene)
    {
      trace('Pausing countdown to play a cutscene (`stress`)');

      hasPlayedCutscene = true;

      event.cancel(); // CANCEL THE COUNTDOWN!
      startVideo(PlayState.instance.currentVariation == 'pico', PlayState.instance.currentVariation == 'remnants');
    }
    else
    {
      hasPlayedCutscene = true;
      cutsceneSkipped = true;
      canSkipCutscene = true;
    }

    if (tankmanGroup != null && !tankmanGroup.scriptCall('isValid'))
    {
      // Destroy the tankman group if it's not valid.
      tankmanGroup.destroy();
      PlayState.instance.currentStage.remove(tankmanGroup);
      tankmanGroup = null;
    }

    if (tankmanGroup == null)
    {
      // Initialize the tankman group if it's not available.
      trace('Initializing tankman group... ' + PlayState.instance.currentVariation);

      var useRim:Bool = true;
      if (PlayState.instance.currentVariation != 'erect' || PlayState.instance.currentVariation != 'pico') useRim = false;
      tankmanGroup = ScriptedFlxSpriteGroup.init('TankmanSpriteGroup', useRim);
    }

    if (tankmanGroup != null)
    {
      // resets the tankmen!
      tankmanGroup.scriptCall('reset');

      tankmanGroup.zIndex = 30;
      PlayState.instance.currentStage.add(tankmanGroup);
      PlayState.instance.currentStage.refresh();
    }
    else
    {
      trace('Failed to initialize tankman group!');
    }
  }

  function onSongStart(event:ScriptEvent):Void
  {
    super.onSongStart(event);
  }

  function startVideo(isPico:Bool, isRemnants:Bool)
  {
    var videoPath = 'stressCutscene';
    if (isPico) videoPath = 'stressPicoCutscene';
    if (Constants.CENSOR_EXPLETIVES)
    {
      videoPath += '-censored';
    }

    if (isRemnants)
    {
      stressIntro();
    }
    else
    {
      VideoCutscene.play(Paths.videos(videoPath));
    }
  }

  var tankTalk:FunkinSound = null;
  var tankTalk2:FunkinSound = null;

  var gunPoint:FunkinSound = null;
  var demonEye:FunkinSound = null;
  var gunHit:FunkinSound = null;
  var steveDies:FunkinSound = null;

  var cutsceneMusic:FunkinSound = null;
  var cutsceneConductor:Conductor;

  var censorBeep:FunkinSound = null;
  var censorSprite:FunkinSprite;

  var fakeBF:BaseCharacter;
  var fakeGF:BaseCharacter;

  var gfCutscene:ScriptedFlxAtlasSprite;
  var picoCutscene:ScriptedFlxAtlasSprite;

  var gfFaceplant:FunkinSprite;
  var gfFail:Bool = FlxG.random.bool(0.5);

  var skipText:FlxText;
  var cutsceneSkipped:Bool = false;
  var canSkipCutscene:Bool = false;
  var pressedSkip:Bool = false;

  /**
   * In-game cutscene function for Remnants Mix.
   * Creates everything needed for it and plays it before the song starts.
   */
  function stressIntro()
  {
    // Cutscene stuff
    PlayState.instance.togglePauseButton();
    PlayState.instance.isInCutscene = true;
    PlayState.instance.mayPauseGame = false;

    PlayState.instance.camHUD.visible = false;

    // Disable the tank so it doesn't looks weird due to camera movement.
    var tankRolling = PlayState.instance.currentStage.getNamedProp('tankRolling');
    tankRolling?.active = false;
    tankRolling?.visible = false;

    // Disable the tank so it doesn't looks weird due to camera movement.
    var tankRollingExtra = PlayState.instance.currentStage.getNamedProp('tankRollingExtra');
    tankRollingExtra?.active = false;
    tankRollingExtra?.visible = false;

    // Start the manager.
    remnantsTimerManager = new FlxTimerManager();

    // Skip text.
    skipText = new FlxText(936 * FullScreenScaleMode.wideScale.x, 618 * FullScreenScaleMode.wideScale.y, 0,
      'Skip [ ' + PlayState.instance.controls.getDialogueNameFromToken("CUTSCENE_ADVANCE", true) + ' ]', 20);
    if (FlxG.onMobile)
    {
      skipText.text = 'Skip [Pause Button]';
      skipText.x -= 136;
    }
    skipText.setFormat(Paths.font('vcr.ttf'), 40, 0xFFFFFFFF, "right", FlxTextBorderStyle.OUTLINE, 0xFF000000);
    skipText.scrollFactor.set();
    skipText.borderSize = 2;
    skipText.alpha = 0;

    PlayState.instance.currentStage.add(skipText);

    skipText.cameras = [PlayState.instance.camCutscene];

    // Hide the real characters.
    PlayState.instance.currentStage.getDad().visible = false;
    PlayState.instance.currentStage.getBoyfriend().visible = false;
    PlayState.instance.currentStage.getGirlfriend().visible = false;

    // Camera shorters
    var bfPos:Array<Float> = [
      PlayState.instance.currentStage.getBoyfriend().cameraFocusPoint.x,
      PlayState.instance.currentStage.getBoyfriend().cameraFocusPoint.y
    ];
    var gfPos:Array<Float> = [
      PlayState.instance.currentStage.getGirlfriend().cameraFocusPoint.x,
      PlayState.instance.currentStage.getGirlfriend().cameraFocusPoint.y
    ];
    var dadPos:Array<Float> = [
      PlayState.instance.currentStage.getDad().cameraFocusPoint.x,
      PlayState.instance.currentStage.getDad().cameraFocusPoint.y
    ];

    PlayState.instance.camCutscene.fade(0xFF000000, 1.5, true, null, true);

    // Create the music instance.
    cutsceneMusic = FunkinSound.load(Paths.music("klaskiiRomper/klaskiiRomper", "shared"), true);
    cutsceneMusic.volume = 0;

    // Setup the audios.
    tankTalk = FunkinSound.load(Paths.sound('week7/stressCutscene1'), 1.0);
    tankTalk2 = FunkinSound.load(Paths.sound('week7/stressCutscene2'), 1.0);
    if (!Preferences.naughtyness) censorBeep = FunkinSound.load(Paths.sound("week7/censorBeep"), 1.0);
    gunPoint = FunkinSound.load(Paths.sound("week7/gunPoint"), 1.0);
    demonEye = FunkinSound.load(Paths.sound("week7/demonEye"), 1.0);
    gunHit = FunkinSound.load(Paths.sound("week7/picoArrives"), 1.0);
    steveDies = FunkinSound.load(Paths.sound("week7/steveDies"), 1.0);

    // For Fake GF dancing.
    cutsceneConductor = new Conductor();
    var songMusicData:Null<SongMusicData> = SongRegistry.instance.parseMusicData('klaskiiRomper');
    if (songMusicData != null) cutsceneConductor.mapTimeChanges(songMusicData.timeChanges);
    cutsceneConductor.onBeatHit.add(onCutsceneBeatHit);

    // Create the Tankman atlas.
    var dad = PlayState.instance.currentStage.getDad();
    tankmanCutscene = ScriptedFlxAtlasSprite.init('TankCutsceneRemnants', 0, 0);
    tankmanCutscene.setPosition(dad.x + 425, dad.y + 215);
    tankmanCutscene.playAnimation("GOD EFFING DAMMIT"); // Loading purpose!
    tankmanCutscene.zIndex = dad.zIndex + 1;

    // Create the Girlfriend atlas.
    var gf = PlayState.instance.currentStage.getGirlfriend();
    gfCutscene = ScriptedFlxAtlasSprite.init('GFCutsceneRemnants', 0, 0);
    gfCutscene.setPosition(gf.x - 475, gf.y - 60);
    gfCutscene.zIndex = gf.zIndex;
    gfCutscene.visible = false;

    // Create the Pico atlas.
    picoCutscene = ScriptedFlxAtlasSprite.init('PicoCutsceneRemnants', 0, 0);
    picoCutscene.setPosition(gfCutscene.x, gfCutscene.y);
		picoCutscene.zIndex = gf.zIndex;
    picoCutscene.visible = false;

    // Create a fake boyfriend.
    var boyfriend = PlayState.instance.currentStage.getBoyfriend();
    fakeBF = CharacterDataParser.fetchCharacter('bf-remnants');
    fakeBF.animation.curAnim.curFrame = 12; // start on the last idle frame.
    fakeBF.setPosition(boyfriend.x - 5, boyfriend.y + 20);
    fakeBF.zIndex = boyfriend.zIndex;
    fakeBF.flipX = false;

    if (gfFail)
    {
      gfFaceplant = FunkinSprite.createSparrow(0, 0, "stages/week7Remnants/cutscene/gfFail");
      gfFaceplant.setPosition(gf.originalPosition.x + 475, gf.originalPosition.y + 550);
      gfFaceplant.animation.addByPrefix('faceplant', "girlfriend face plant0", 24, false);
      gfFaceplant.zIndex = boyfriend.zIndex + 1;
      gfFaceplant.visible = false;
    }

    // Create a fake girlfriend.
    fakeGF = CharacterDataParser.fetchCharacter('gf-tankmen-remnants');
    fakeGF.setPosition(gf.x + 15, gf.y + 135);
    fakeGF.zIndex = gf.zIndex;

    // Add everything.
    PlayState.instance.currentStage.add(fakeBF);
    PlayState.instance.currentStage.add(fakeGF);
    PlayState.instance.currentStage.add(gfCutscene);
    PlayState.instance.currentStage.add(picoCutscene);
    PlayState.instance.currentStage.add(tankmanCutscene);
    if (gfFail) PlayState.instance.currentStage.add(gfFaceplant);
    PlayState.instance.currentStage.refresh(); // Apply z-index.

    /** Cutscene Time! */
    // Camera starts on Tankman.
    // It has a short delay for prevent lag.
    new FlxTimer(remnantsTimerManager).start(0.1, function(tmr)
    {
      PlayState.instance.tweenCameraToPosition(dadPos[0] - 100, dadPos[1], 1.5, FlxEase.quadInOut);
      PlayState.instance.tweenCameraZoom(0.86 * 1.15, 0.01, true, FlxEase.quadInOut);

      // Play the dialogue.
      tankmanCutscene.playAnimation("GOD EFFING DAMMIT");
    });

    // Focus camera on GF.
    new FlxTimer(remnantsTimerManager).start(15.9, function(tmr:FlxTimer)
		{
      PlayState.instance.tweenCameraToPosition(gfPos[0] - 150, gfPos[1], 0.5, FlxEase.elasticInOut);
      PlayState.instance.tweenCameraZoom(0.86 * 1.3, 2.1, true, FlxEase.quadInOut);
      gunPoint.play(true);

      // Destroy Fake GF.
      if (fakeGF != null)
      {
        PlayState.instance.currentStage.remove(fakeGF);
        fakeGF.destroy();
        fakeGF = null;
      }

      gfCutscene.visible = true; // Show GF getting gunpointed.
      gfCutscene.playAnimation("Gun Point");

      // Play the demon eye sound and animation.
      new FlxTimer(remnantsTimerManager).start(1.24, function(swagTimer:FlxTimer)
			{
        demonEye.play(true);
        gfCutscene.playAnimation("Demon Eye");
        gfCutscene.anim.onComplete.add(function()
        {
          // Pico Arrives (Also GF getting gunpointed is destroyed).
          PlayState.instance.currentStage.remove(gfCutscene);
          gfCutscene.destroy();
          gfCutscene = null;

          // Zoom out, hide fake bf (unless you got the rare variant) and play the sound.
          PlayState.instance.tweenCameraZoom(0.8, 0.1, true, FlxEase.elasticInOut);
          gunHit.play(true);

          // Summon pico cutscene act.
          picoCutscene.visible = true;
          picoCutscene.playAnimation("Pico Arrives");

          // Steve dies.
          new FlxTimer(remnantsTimerManager).start(1.05, function(swagTimer:FlxTimer) {
            picoCutscene.playAnimation("Steve Dies");
            steveDies.play(true);
          });

          // Pico goes to idle.
          new FlxTimer(remnantsTimerManager).start(3.5, function(swagTimer:FlxTimer) {
            picoCutscene.playAnimation("Idle");
          });

          // Real pico-speaker shows up.
          new FlxTimer(remnantsTimerManager).start(5.2, function(swagTimer:FlxTimer)
          {
            PlayState.instance.currentStage.getGirlfriend().visible = true;
            PlayState.instance.currentStage.remove(picoCutscene);
            picoCutscene.destroy();
            picoCutscene = null;
          });

          // Do boyfriend's part after 0.08 miliseconds.
          new FlxTimer(remnantsTimerManager).start(0.08, function(swagTimer:FlxTimer)
          {
            PlayState.instance.currentStage.getBoyfriend().visible = (!gfFail) ? true : false;
            fakeBF.visible = (!gfFail) ? false : true;

            if (gfFail)
            {
              // GF epic fail woooo!
              gfFaceplant.animation.play("faceplant");
              gfFaceplant.visible = true;

              PlayState.instance.currentStage.getBoyfriend().characterId = 'bf-remnants';

              // Lower the camera a bit just so you can see gf epic fall.
              PlayState.instance.tweenCameraToPosition(gfPos[0] - 150, gfPos[1] + 200, 0.15, FlxEase.quadInOut);

              // BF dodges GF.
              if (fakeBF.hasAnimation('dodge') && fakeBF != null)
              {
                fakeBF.playAnimation('dodge', true, true);
              }
            }
            else
            {
              // Play the BF catching GF animation.
              if (PlayState.instance.currentStage.getBoyfriend().hasAnimation('bfCatch'))
              {
                PlayState.instance.currentStage.getBoyfriend().playAnimation('bfCatch', true, true);
              }
            }
          });

          // Play tankman anim part 2
          new FlxTimer(remnantsTimerManager).start(2.3, function(gayLol:FlxTimer)
          {
            tankmanCutscene.playAnimation("Sexually Ambiguous");
            tankTalk2.play(true);
          });

          // focus camera to tankman
          new FlxTimer(remnantsTimerManager).start(3, function(weedShitBaby:FlxTimer)
          {
            PlayState.instance.tweenCameraToPosition(dadPos[0], dadPos[1], 2, FlxEase.quadInOut);
          });
        });
      });
		});

    // Cunt scene.
    new FlxTimer(remnantsTimerManager).start(32.3, function(cunt:FlxTimer)
		{
      // Set the zoom to this value instantly.
      PlayState.instance.tweenCameraZoom(0.86 * 1.4, 0.1, true);

      // Elastic out the zoom a bit and focus to boyfriend.
      PlayState.instance.tweenCameraToPosition(bfPos[0] + 150, bfPos[1], 0.1);
      PlayState.instance.tweenCameraZoom(0.86 * 1.4 + 0.1, 0.5, true, FlxEase.elasticOut);

      // Cutting off skipping here.
      // Really don't think its needed after this point and it saves problems from happening.
      FlxTween.tween(skipText, {alpha: 0}, 0.5, {ease: FlxEase.quadIn,
        onComplete: _ -> {
          skipText.visible = false;
        }
      });

      // Play the 'singUPmiss' animation (if found).
      var whichBF = (gfFail) ? fakeBF : PlayState.instance.currentStage.getBoyfriend();
      if (whichBF != null)
      {
        if (whichBF.hasAnimation('singUPmiss'))
        {
          whichBF.playAnimation('singUPmiss', true, true);
        }
        whichBF.animation.onFinish.add(function(name:String = '')
        {
          // fast focus to tankman.
          PlayState.instance.tweenCameraToPosition(dadPos[0], dadPos[1], 0.01);
          PlayState.instance.tweenCameraZoom((0.86 * 1.5) / 1.4, 0.01, true);

          // remove the callback after it's done.
          whichBF.animation.onFinish.removeAll();
        });
      }
		});

    // Camera returns to normal, cutscene flags set and countdown starts.
    // All of this after 35 seconds to be exacts.
    new FlxTimer(remnantsTimerManager).start(35, function(tmr:FlxTimer)
    {
      // Call the ending function.
      endStressIntro();
    });

    // Censor system.
    // I'm proud of this thing.
    if (!Preferences.naughtyness)
    {
      var dad = PlayState.instance.currentStage.getDad();
      censorSprite = FunkinSprite.createSparrow(0, 0, "stages/week7Remnants/cutscene/censor");
      censorSprite.animation.addByPrefix('censor', "mouth censor0", 24, true);
      censorSprite.animation.play('censor');
      censorSprite.zIndex = dad.zIndex + 2;
      censorSprite.visible = false;

      PlayState.instance.currentStage.add(censorSprite);
      PlayState.instance.currentStage.refresh(); // Apply z-index.

      // Well played you little s##t!
      censorEvent(tankTalk, [dad.x + 145, dad.y + 165], [4.55, 0.2]); // dad.x + 145, dad.y + 165

      // Don't you have ###### ## ##### ##?
      censorEvent(tankTalk2, [dad.x + 130, dad.y + 150], [25.9, 0.9]); // dad.x + 130, dad.y + 150

      // Let's rock!, you little c####s!
      censorEvent(tankTalk2, [dad.x + 220, dad.y + 185], [31.5, 0.4]); // dad.x + 220, dad.y + 185

      // You little c####s!
      censorEvent(tankTalk2, [dad.x + 160, dad.y + 160], [34.6, 0.5]); // dad.x + 160, dad.y + 160
    }
  }

  /**
   * Used for mouth censor events.
   * "Uh-Oh!" that's a bad word!
   */
  function censorEvent(sound:FunkinSound, position:Null<Float>, time:Null<Float>):Void
  {
    // For some reason, you can't use functions variables inside of FlxTimer functions.
    var jeffSound:Bool = sound;
    var censorTime:Null<Float> = time;
    var censorPos:Null<Float> = position;

    // Censor the dialogue only if censorSprite exists.
    if (censorSprite != null)
    {
      // Start a timer for when the censor starts.
      new FlxTimer(remnantsTimerManager).start(censorTime[0], function(censorStart:FlxTimer)
      {
        // Show the censor sprite and position it to the desired offset.
        censorSprite.setPosition(censorPos[0], censorPos[1]);
        censorSprite.visible = true;

        // Play the beep sound and mute tankman talking.
        if (censorBeep != null) censorBeep.play(false);
        if (jeffSound != null) jeffSound.volume = 0;

        // Start a timer for when the censor ends.
        new FlxTimer(remnantsTimerManager).start(censorTime[1], function(censorEnd:FlxTimer)
        {
          // Hide the censor sprite, unmute tankman dialogue and stop the beep sound.
          censorSprite.visible = false;
          if (censorBeep != null) censorBeep.stop();
          if (jeffSound != null) jeffSound.volume = 1;
        });
      }); 
    }
  }

  function onCutsceneBeatHit()
  {
    if (PlayState.instance.currentStage.getBoyfriend().isAnimationFinished())
    {
      PlayState.instance.currentStage.getBoyfriend().dance(true);
      PlayState.instance.currentStage.getBoyfriend().animation.curAnim.curFrame = 12;

      if (!gfFail)
      {
        if (PlayState.instance.currentStage.getBoyfriend().getCurrentAnimation() == 'singUPmiss')
        {
          // Making sure to remove the callback after it's done.
          PlayState.instance.currentStage.getBoyfriend().animation.onFinish = null;
        }
      }
    }

    var sniper = PlayState.instance.currentStage.getNamedProp('watchtower');
    if (sniper != null && sniper.isAnimationFinished()) sniper.dance(true);

    for (man in 0...6)
    {
      var tank = PlayState.instance.currentStage.getNamedProp('tankmanAudience' + man);
      if (tank.isAnimationFinished()) tank.dance(true);
    }

    var steveCheer = PlayState.instance.currentStage.getNamedProp('tankmanBopperCheer1');
    if (steveCheer.isAnimationFinished()) steveCheer.dance(true);

    if (fakeBF != null)
    {
      if (fakeBF.isAnimationFinished())
      {
        fakeBF.dance(true);
        fakeBF.animation.curAnim.curFrame = 12;

        if (gfFail)
        {
          if (fakeBF.getCurrentAnimation() == 'singUPmiss')
          {
            // making sure to remove the callback after it's done.
            fakeBF.animation.onFinish = null;
          }
        }
      }
    }

    if (fakeGF != null)
    {
      if (fakeGF.isAnimationFinished())
      {
        fakeGF.dance(true);
      }
    }
  }

  var deathBFNormal:FunkinSprite;

  override function onGameOver(event:ScriptEvent):Void
  {
		super.onGameOver(event);

    if (gfFail) // Default BF death when GF faceplant.
    {
      new FlxTimer().start(0.05, _ -> {
        deathBFNormal = FunkinSprite.createSparrow(0, 0, "characters/bf-remnants");
        deathBFNormal.animation.addByPrefix('firstDeath', "BF dies", 24, false);
        deathBFNormal.animation.addByPrefix('deathLoop', "BF Dead Loop", 24, true);
        deathBFNormal.animation.addByPrefix('deathConfirm', "BF Dead confirm", 24, false);
        deathBFNormal.setPosition(GameOverSubState.instance.boyfriend.x, GameOverSubState.instance.boyfriend.y);
        deathBFNormal.zIndex = GameOverSubState.instance.boyfriend.zIndex + 1;
        deathBFNormal.animation.play("firstDeath");
        deathBFNormal.animation.onFinish.add(function(name:String) {
          deathBFNormal.animation.play("deathLoop");
        });

        GameOverSubState.instance.add(deathBFNormal);
        GameOverSubState.instance.refresh();
      });
    }

    if (GameOverSubState.instance != null)
    {
      if ((GameOverSubState.instance.controls.ACCEPT || (TouchUtil.pressAction() && !TouchUtil.overlaps(backButton) && GameOverSubState.instance.canInput)))
      {
        deathBFNormal?.animation.onFinish.removeAll();
        deathBFNormal?.animation.onFinish = null;

        deathBFNormal?.animation.play("deathConfirm");
      }
    }
	}

  /**
   * Don't replay the cutscene between restarts.
   */
  function onSongRetry(event:ScriptEvent)
  {
    super.onSongRetry(event);

    hasPlayedCutscene = true;

    // resets the tankmen!
    if (tankmanGroup != null)
    {
      tankmanGroup.scriptCall('reset');
    }
    if (PlayState.instance.currentStage.getGirlfriend() != null)
    {
      PlayState.instance.currentStage.getGirlfriend().scriptCall('reset');
      trace('reset pico!');
    }
  }

  // Pico Mix
  var hasPlayedEndCutscene:Bool = false;
  var rimlightCamera:FlxCamera;
  var tankmanCutscene:ScriptedFlxAtlasSprite;
  var screenspaceRimlight:DropShadowScreenspace = new DropShadowScreenspace();

  // Remnants Mix
  var startedTalking:Bool = false;
  var remnantsTimerManager:FlxTimerManager;

  function onUpdate(event:UpdateScriptEvent):Void
  {
    super.onUpdate(event);

    if (PlayState.instance.isInCutscene)
    {
      if (cutsceneTimerManager != null) cutsceneTimerManager.update(event.elapsed);
      if (remnantsTimerManager != null) remnantsTimerManager.update(event.elapsed);
      if (cutsceneConductor != null && cutsceneMusic != null) cutsceneConductor.update(cutsceneMusic.time);

      if (!startedTalking && tankmanCutscene != null && tankmanCutscene.anim.curFrame >= 1 && tankTalk != null)
      {
        startedTalking = true;
        tankTalk.play(true);

        // Play the music.
        cutsceneMusic.play(false);
        cutsceneMusic.fadeIn(5.0, 0.0, 0.25);
      }

      if (rimlightCamera != null)
      {
        rimlightCamera.focusOn(new FlxPoint(PlayState.instance.camGame.viewLeft + PlayState.instance.camGame.viewWidth / 2,
          PlayState.instance.camGame.viewTop + PlayState.instance.camGame.viewHeight / 2));
        rimlightCamera.zoom = PlayState.instance.camGame.zoom;
      }
    }
    else
    {
      if (gfFail && fakeBF != null)
      {
        fakeBF.playAnimation(PlayState.instance.currentStage.getBoyfriend().getCurrentAnimation());
        fakeBF.setPosition(PlayState.instance.currentStage.getBoyfriend().x - 5, PlayState.instance.currentStage.getBoyfriend().y + 20);
        fakeBF.animation.curAnim.curFrame = PlayState.instance.currentStage.getBoyfriend().animation.curAnim.curFrame;
      }
    }

    if (FlxG.onMobile)
    {
      isMobilePauseButtonPressed = TouchUtil.overlapsComplex(PlayState.instance.pauseButton) && TouchUtil.justPressed;
    }

    if (PlayState.instance.currentVariation == 'remnants')
    {
      if ((PlayState.instance.controls.CUTSCENE_ADVANCE || isMobilePauseButtonPressed) && cutsceneSkipped == false)
      {
        if (!canSkipCutscene)
        {
          trace('cant skip yet!');
          if (skipText != null)
          {
            FlxTween.tween(skipText, {alpha: 1}, 0.5, {ease: FlxEase.quadOut});
            new FlxTimer().start(0.5, _ -> {
              canSkipCutscene = true;
              trace('can skip!');
            });
          }
        }
      }

      if ((PlayState.instance.controls.CUTSCENE_ADVANCE || isMobilePauseButtonPressed)
        && cutsceneSkipped == false
        && canSkipCutscene == true)
      {
        pressedSkip = true;
        endStressIntro();
        trace('skipped');
      }
    }
  }

  var bgSprite:FunkinSprite;
  public override function onSongEnd(event:CountdownScriptEvent):Void
  {
    super.onSongEnd(event);
    if (PlayState.instance.currentVariation != 'pico') hasPlayedEndCutscene = true;
    // only play this on those variants..

    if (!hasPlayedEndCutscene)
    {
      hasPlayedEndCutscene = true;

      event.cancel();

      // trace('Adding black background behind cutscene over UI');
      bgSprite = new FunkinSprite(0, 0);
      bgSprite.makeSolidColor(2000, 2500, 0xFF000000);
      bgSprite.cameras = [PlayState.instance.camCutscene]; // Show over the HUD but below the video.
      // this
      bgSprite.zIndex = -10000;
      PlayState.instance.add(bgSprite);
      PlayState.instance.refresh();
      bgSprite.alpha = 0;

      startEndCutscene();
    }
    else
    {
      // Make sure the cutscene can play again next time!
      hasPlayedEndCutscene = false;
      // DO NOT CANCEL THE EVENT!
    }
  }

  var cutsceneTimerManager:FlxTimerManager;
  function startEndCutscene()
  {
    var picoPos:Array<Float> = [
      PlayState.instance.currentStage.getBoyfriend().cameraFocusPoint.x,
      PlayState.instance.currentStage.getBoyfriend().cameraFocusPoint.y
    ];
    var otisPos:Array<Float> = [
      PlayState.instance.currentStage.getGirlfriend().cameraFocusPoint.x,
      PlayState.instance.currentStage.getGirlfriend().cameraFocusPoint.y
    ];
    var tankmanPos:Array<Float> = [
      PlayState.instance.currentStage.getDad().cameraFocusPoint.x,
      PlayState.instance.currentStage.getDad().cameraFocusPoint.y
    ];

    cutsceneTimerManager = new FlxTimerManager();

    // Disable player input during cutscene, so you can't get a gameover during cutscene
    PlayState.instance.isInCutscene = true;
    PlayState.instance.camHUD.visible = false;

    rimlightCamera = new FlxCamera();
    FlxG.cameras.insert(rimlightCamera, (FlxG.onMobile) ? -4 : -2, false);
    rimlightCamera.bgColor = 0x00FFFFFF; // Show the game scene behind the camera.

    screenspaceRimlight.baseBrightness = -46;
    screenspaceRimlight.baseHue = -38;
    screenspaceRimlight.baseContrast = -25;
    screenspaceRimlight.baseSaturation = -20;

    screenspaceRimlight.angle = 45;
    screenspaceRimlight.threshold = 0.3;

    var rimlightFilter:ShaderFilter = new ShaderFilter(screenspaceRimlight);

    rimlightCamera.filters = [rimlightFilter];

    tankmanCutscene = ScriptedFlxAtlasSprite.init('TankmanEndingSprite', 0, 0);
    tankmanCutscene.setPosition(PlayState.instance.currentStage.getDad().x + 723, PlayState.instance.currentStage.getDad().y + 145);

    PlayState.instance.tweenCameraToPosition(tankmanPos[0] + 320, tankmanPos[1] - 70, 2.8, FlxEase.expoOut);
    PlayState.instance.tweenCameraZoom(0.65, 2, true, FlxEase.expoOut);

    PlayState.instance.currentStage.getDad().visible = false;
    PlayState.instance.currentStage.add(tankmanCutscene);
    tankmanCutscene.cameras = [rimlightCamera];

    tankmanCutscene.scriptCall('doAnim');

    new FlxTimer(cutsceneTimerManager).start(176 / 24, _ -> {
      PlayState.instance.currentStage.getBoyfriend().playAnimation('laughEnd', true);
    });

    new FlxTimer(cutsceneTimerManager).start(270 / 24, _ -> {
      PlayState.instance.tweenCameraToPosition(tankmanPos[0] + 320, tankmanPos[1] - 370, 2, FlxEase.quadInOut);
      FlxTween.tween(bgSprite, {alpha: 1}, 2, null);
    });

    new FlxTimer(cutsceneTimerManager).start(320 / 24, _ -> {
      FlxG.cameras.remove(rimlightCamera);
      rimlightCamera = null;
      PlayState.instance.endSong(true);
    });
  }

  /**
   * For Remnants Mix cutscene.
   */
  function endStressIntro()
  {
    var dadPos:Array<Float> = [
      PlayState.instance.currentStage.getDad().cameraFocusPoint.x,
      PlayState.instance.currentStage.getDad().cameraFocusPoint.y
    ];

    cutsceneSkipped = true;
    hasPlayedCutscene = true;
    if (pressedSkip) PlayState.instance.camCutscene.fade(0xFF000000, 0.5, false, null, true);

    // fade out and stop the music!!!!!!
    cutsceneMusic.fadeOut(0.5, 0.0, function()
    {
      if (cutsceneMusic != null)
        cutsceneMusic.stop();
    });

    new FlxTimer().start(0.5, _ ->
    {
      PlayState.instance.camHUD.visible = true;

      if (pressedSkip) PlayState.instance.camCutscene.fade(0xFF000000, 0.5, true, null, true);

      fakeBF?.visible = (gfFail) ? true : false;

      gfFaceplant?.animation.play("faceplant");
      gfFaceplant?.animation.curAnim.curFrame = 116;
      gfFaceplant?.visible = (gfFail) ? true : false;

      PlayState.instance.currentStage.getBoyfriend().visible = (!gfFail) ? true : false;
      PlayState.instance.currentStage.getGirlfriend().visible = true;
      PlayState.instance.currentStage.getDad().visible = true;
      
      if (fakeGF != null)
      {
        PlayState.instance.currentStage.remove(fakeGF);
        fakeGF.destroy();
        fakeGF = null;
      }

      if (tankmanCutscene != null)
      {
        PlayState.instance.currentStage.remove(tankmanCutscene);
        tankmanCutscene.destroy();
        tankmanCutscene = null;
      }

      if (picoCutscene != null)
      {
        PlayState.instance.currentStage.remove(picoCutscene);
        picoCutscene.destroy();
        picoCutscene = null;
      }

      if (gfCutscene != null)
      {
        PlayState.instance.currentStage.remove(gfCutscene);
        gfCutscene.destroy();
        gfCutscene = null;
      }

      var tankRolling = PlayState.instance.currentStage.getNamedProp('tankRolling');
      tankRolling?.active = true;
      tankRolling?.visible = true;

      var tankRollingExtra = PlayState.instance.currentStage.getNamedProp('tankRollingExtra');
      tankRollingExtra?.active = true;
      tankRollingExtra?.visible = true;

      tankTalk?.stop();
      gunHit?.stop();
      demonEye?.stop();
      gunPoint?.stop();
      steveDies?.stop();
      if (pressedSkip) tankTalk2?.stop();

      skipText?.visible = false;
      remnantsTimerManager?.clear();

      PlayState.instance.justUnpaused = true;
      PlayState.instance.tweenCameraZoom(0.86, 2, true, FlxEase.sineInOut);
      PlayState.instance.tweenCameraToPosition(dadPos[0], dadPos[1], 2, FlxEase.sineInOut);
      PlayState.instance.isInCutscene = false;
      PlayState.instance.startCountdown();

      PlayState.instance.togglePauseButton(true);
      PlayState.instance.isInCutscene = false;
      PlayState.instance.mayPauseGame = true;
    });
  }

  function kill():Void
  {
    cleanupTankmanGroup();

    if (cutsceneTimerManager != null) cutsceneTimerManager.destroy();
    if (remnantsTimerManager != null) remnantsTimerManager.destroy();
  }

  function cleanupTankmanGroup():Void
  {
    if (tankmanGroup != null)
    {
      PlayState.instance.currentStage.remove(tankmanGroup);
      tankmanGroup.destroy();
      tankmanGroup = null;
    }
  }
}
