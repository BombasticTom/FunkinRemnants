import funkin.modding.module.ModuleHandler;
import funkin.play.song.Song;
import funkin.play.PlayState;
import funkin.save.Save;
import funkin.graphics.FunkinSprite;
import funkin.util.FlxTweenUtil;
import flixel.util.FlxTimer;
import flixel.FlxG;
import flixel.tweens.FlxTween;
import flixel.tweens.FlxEase;
import funkin.audio.FunkinSound;

class SouthRemnantsMixSong extends Song
{
  var black:FunkinSprite;
  var fadeOut:Bool = false;

  var hasPlayedMonsterCutscene:Bool = false;
  var mosnerCutsceneAudio:FunkinSound;

  public function new()
  {
    super('south',
      {
        variation: 'remnants'
      });
  }

  public override function isSongNew(currentDifficulty:String, currentVariation:String):Bool
  {
    if (currentVariation == 'remnants') return !Save.instance.hasBeatenSong(this.id, null, this.variation);
    return false;
  }

  function onCountdownStart(event:ScriptEvent):Void
  {
    super.onCountdownStart(event);

    PlayState.instance.camHUD.alpha = 0;

    if (!ModuleHandler.getModule('RemnantsBumpers').scriptCall('getPlayedBumper')
      && ModuleHandler.getModule('RemnantsBumpers').scriptCall('setupBumper'))
    {
      event.cancel();
      ModuleHandler.getModule('RemnantsBumpers').scriptCall('setPlayedBumper', true);
    }

    hasPlayedMonsterCutscene = false;
  }

  function onCreate(event:ScriptEvent):Void
  {
    super.onCreate(event);

    mosnerCutsceneAudio = FunkinSound.load(Paths.sound('mosnerCutscene'), 1.0, false, true);

    black = new FunkinSprite(-20, -20);
    black.makeSolidColor(Std.int(FlxG.width * 1.5), Std.int(FlxG.height * 1.5), 0xFFFFFFFF);
    black.color = 0xFF000000;
    black.cameras = [PlayState.instance.camCutscene];
    PlayState.instance.add(black);

    PlayState.instance.camHUD.alpha = 0;
    fadeOut = false;

    var boyfriend:BaseCharacter = PlayState.instance?.currentStage?.getBoyfriend() ?? null;
    boyfriend.shouldAlternate = true;
    boyfriend.idleSuffix = '-spooky';

    var gf:BaseCharacter = PlayState.instance?.currentStage?.getGirlfriend() ?? null;
    gf.shouldAlternate = true;
    gf.idleSuffix = '-spooky';
  }

  function onSongRetry(event:ScriptEvent)
  {
    super.onSongRetry(event);

    FlxG.camera.stopFX();
    FlxTween.cancelTweensOf(black);
    FlxTween.cancelTweensOf(PlayState.instance.camHUD);

    if (fadeOut)
    {
      FlxTween.tween(black, {alpha: 1}, 0.5, {ease: FlxEase.linear});
      FlxTween.tween(PlayState.instance.camHUD, {alpha: 0}, 0.5, {ease: FlxEase.linear});
      fadeOut = false;
    }
    else
    {
      black.alpha = 1;
    }
  }

  function onGameOver(event:ScriptEvent):Void
  {
    FlxG.camera.stopFX();
    FlxTween.cancelTweensOf(PlayState.instance.camHUD);
    PlayState.instance.camHUD.alpha = 0;
    fadeOut = false;
  }

  function onPause()
  {
    FlxG.camera?.active = false;
    FlxTweenUtil.pauseTweensOf(black);
    FlxTweenUtil.pauseTweensOf(PlayState.instance.camHUD);
  }

  function onResume()
  {
    FlxG.camera?.active = true;
    FlxTweenUtil.resumeTweensOf(black);
    FlxTweenUtil.resumeTweensOf(PlayState.instance.camHUD);
  }

  function onSongStart(event:ScriptEvent):Void
  {
    FlxTween.tween(black, {alpha: 0.5}, 5.64, {ease: FlxEase.smootherStepOut});
  }

  function onBeatHit(event:ScriptEvent):Void
  {
    super.onBeatHit();

    if (PlayState.instance.isGameOverState) return;
    switch (event.beat)
    {
      case 29:
        FlxTween.tween(PlayState.instance.camHUD, {alpha: 1}, 1, {ease: FlxEase.linear});
        fadeOut = true;
      case 32:
        FlxG.camera.flash(0xFFFFFFFF, 0.5);
        PlayState.instance.currentStage.changeAmbiance(true, 0.0001);
        black.alpha = 0;
      case 61:
        FlxTween.tween(black, {alpha: 0.7}, 0.35, {ease: FlxEase.backIn});
      case 64:
        FlxTween.tween(black, {alpha: 0}, 1, {ease: FlxEase.linear});
        PlayState.instance.currentStage.changeAmbiance(false, 1);
      case 96:
        FlxTween.tween(black, {alpha: 0.5}, 1, {ease: FlxEase.linear});
        PlayState.instance.currentStage.changeAmbiance(true, 1);
      case 123:
        FlxTween.tween(black, {alpha: 0.7}, 0.3, {ease: FlxEase.linear});
      case 128:
        FlxTween.tween(black, {alpha: 0.5}, 1, {ease: FlxEase.linear});
      case 155:
        FlxTween.tween(black, {alpha: 0.7}, 0.3, {ease: FlxEase.linear});
      case 157:
        FlxTween.tween(black, {alpha: 0.9}, 1, {ease: FlxEase.linear});
        fadeOut = false;
      case 160:
        FlxG.camera.flash(0xFFFFFFFF, 0.5);
        black.alpha = 0.5;
        fadeOut = true;
      case 192:
        FlxTween.tween(black, {alpha: 0}, 1, {ease: FlxEase.linear});
        PlayState.instance.currentStage.changeAmbiance(false, 1);
      case 222:
        FlxTween.tween(black, {alpha: 0.7}, 0.3, {ease: FlxEase.linear});
        PlayState.instance.currentStage.changeAmbiance(true, 0.3);
      case 224:
        FlxTween.tween(black, {alpha: 0}, 0.5, {ease: FlxEase.linear});
      case 239:
        FlxTween.tween(black, {alpha: 1}, 0.5, {ease: FlxEase.linear});
        fadeOut = false;
    }
  }

  override public function onSongEnd(event:ScriptEvent):Void
  {
    if (!hasPlayedMonsterCutscene)
    {
      event.cancel();
      hasPlayedMonsterCutscene = true;

      PlayState.instance?.camHUD?.visible = false;

      mosnerCutsceneAudio.play();

      black.color = 0xFFFFFFFF;
      FlxTween.tween(black, {alpha: 0}, 6 * (1 / 24));

      var currentStage:Stage = PlayState.instance?.currentStage;
      currentStage?.changeAmbiance(true, 0.0001);

      currentStage?.getBoyfriend()?.visible = false;
      currentStage?.getGirlfriend()?.visible = false;
      currentStage?.getDad()?.visible = false;

      var bfPos:FlxPoint = currentStage?.getBoyfriendPosition();
      var gfPos:FlxPoint = currentStage?.getGirlfriendPosition();
      var dadPos:FlxPoint = currentStage?.getGirlfriendPosition();

      var bfSquint:FunkinSprite = FunkinSprite.createTextureAtlas(0, 0, 'remnants/monster cutscene/BF Squinting', 'week2');
      bfSquint.anim.addByTimeline('anim', bfSquint.anim.getDefaultTimeline(), 24, false);
      bfSquint.anim.play('anim', true);
      bfSquint.updateHitbox();
      bfSquint.setPosition(bfPos.x - (bfSquint.width / 2), bfPos.y - bfSquint.height);
      bfSquint.y += 26;
      bfSquint.zIndex = currentStage?.getBoyfriend()?.zIndex ?? 0;
      currentStage?.add(bfSquint);

      var gfSquint:FunkinSprite = FunkinSprite.createTextureAtlas(0, 0, 'remnants/monster cutscene/GF Squinting', 'week2');
      gfSquint.anim.addByTimeline('anim', gfSquint.anim.getDefaultTimeline(), 24, false);
      gfSquint.anim.play('anim', true);
      gfSquint.updateHitbox();
      gfSquint.setPosition(gfPos.x - (gfSquint.width / 2), gfPos.y - gfSquint.height);
      gfSquint.x -= 12;
      gfSquint.zIndex = currentStage?.getGirlfriend()?.zIndex ?? 0;
      currentStage?.add(gfSquint);

      var mosner:FunkinSprite = FunkinSprite.createTextureAtlas(0, 0, 'remnants/monster cutscene/moster appear', 'week2');
      mosner.anim.addByTimeline('anim', mosner.anim.getDefaultTimeline(), 24, false);
      mosner.anim.addBySymbol('evilAssFace', 'monster face baked', 24, true);
      mosner.anim.play('anim', true);
      mosner.updateHitbox();
      mosner.setPosition(dadPos.x - (mosner.width / 2), dadPos.y - mosner.height);
      mosner.x -= 20;
      mosner.y += 485;
      mosner.zIndex = currentStage?.getDad()?.zIndex ?? 0;
      currentStage?.add(mosner);

      currentStage?.refresh();

      FlxTimer.wait(121 * (1 / 24), () -> {
        FlxTween.tween(black, {alpha: 1}, 15 * (1 / 24));
        black.color = 0xFF000000;
      });

      FlxTimer.wait(148 * (1 / 24), () -> {
        mosner.alpha = 0;
        mosner.anim.play('evilAssFace', true);
        PlayState.instance?.add(mosner);

        mosner.x += 325;
        mosner.y += 111;

        FlxTween.tween(mosner, {alpha: 0.3}, 6 * (1 / 24));

        black.alpha = 0;
        currentStage?.remove(mosner);
        currentStage?.visible = false;
      });

      FlxTimer.wait(205 * (1 / 24), () -> {
        FlxTween.tween(mosner, {alpha: 0}, 7 * (1 / 24));
      });

      FlxTimer.wait(216 * (1 / 24), () -> {
        bfSquint.destroy();
        gfSquint.destroy();
        mosner.destroy();

        PlayState.instance?.endSong(true);
        currentStage?.visible = true;
      });
    }
    super.onSongEnd(event);
  }
}
