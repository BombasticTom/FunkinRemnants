import funkin.modding.module.ModuleHandler;
import funkin.play.song.Song;
import funkin.play.PlayState;
import funkin.save.Save;
import funkin.graphics.FunkinSprite;
import funkin.Preferences;
import funkin.util.FlxTweenUtil;
import flixel.FlxG;
import flixel.tweens.FlxTween;
import flixel.tweens.FlxEase;

class MonsterRemnantsMixSong extends Song
{
  var black:FunkinSprite;

  public function new()
  {
    super('monster',
      {
        variation: 'remnants'
      });
  }

  public override function isSongNew(currentDifficulty:String, currentVariation:String):Bool
  {
    if (currentVariation == 'remnants') return !Save.instance.hasBeatenSong(this.id, null, this.variation);
    return false;
  }

  function onCountdownStart(event:ScriptEvent):Void
  {
    super.onCountdownStart(event);

    PlayState.instance.camHUD.alpha = 0;

    if (!ModuleHandler.getModule('RemnantsBumpers').scriptCall('getPlayedBumper')
      && ModuleHandler.getModule('RemnantsBumpers').scriptCall('setupBumper'))
    {
      event.cancel();
      ModuleHandler.getModule('RemnantsBumpers').scriptCall('setPlayedBumper', true);
    }
  }

  function onCreate(event:ScriptEvent):Void
  {
    super.onCreate(event);

    black = new FunkinSprite(-20, -20);
    black.makeSolidColor(Std.int(FlxG.width * 1.5), Std.int(FlxG.height * 1.5), 0xFF000000);
    black.cameras = [PlayState.instance.camCutscene];
    PlayState.instance.add(black);

    black.alpha = 1;
    PlayState.instance.camHUD.alpha = 0;

    if (!Preferences.downscroll) // Move pop ups
    {
      PlayState.instance.comboPopUps.offsets = [550, 350]; // Upscroll Offset
    }
    else
    {
      PlayState.instance.comboPopUps.offsets = [550, -190]; // Downscroll Offset
    }
  }

  function onSongRetry(event:ScriptEvent)
  {
    super.onSongRetry(event);

    FlxG.camera.stopFX();
    FlxTween.cancelTweensOf(black);
    FlxTween.cancelTweensOf(PlayState.instance.camHUD);

    black.alpha = 1;
  }

  function onGameOver(event:ScriptEvent):Void
  {
    FlxG.camera.stopFX();
    FlxTween.cancelTweensOf(PlayState.instance.camHUD);
    PlayState.instance.camHUD.alpha = 0;
  }

  function onPause()
  {
    FlxG.camera?.active = false;
    FlxTweenUtil.pauseTweensOf(black);
    FlxTweenUtil.pauseTweensOf(PlayState.instance.camHUD);
  }

  function onResume()
  {
    FlxG.camera?.active = true;
    FlxTweenUtil.resumeTweensOf(black);
    FlxTweenUtil.resumeTweensOf(PlayState.instance.camHUD);
  }

  function onSongStart(event:ScriptEvent):Void
  {
    super.onSongStart(event);

    FlxTween.tween(black, {alpha: 0.8}, 7, {ease: FlxEase.smootherStepOut});
  }

  function onBeatHit(event:ScriptEvent):Void
  {
    super.onBeatHit(event);

    if (PlayState.instance.isGameOverState) return;
    switch (event.beat)
    {
      case 8:
        FlxTween.tween(PlayState.instance.camHUD, {alpha: 1}, 3, {ease: FlxEase.linear});
      case 60:
        FlxTween.tween(black, {alpha: 0.5}, 6, {ease: FlxEase.backIn});
      case 96:
        FlxTween.tween(black, {alpha: 0.3}, 8, {ease: FlxEase.backIn});
      case 128:
        FlxG.camera.flash(0xFFFFFFFF, 0.5);
        black.alpha = 0;
      case 227:
        FlxTween.tween(black, {alpha: 0.3}, 0.3, {ease: FlxEase.linear});
      case 322:
        FlxTween.tween(black, {alpha: 0.5}, 0.5, {ease: FlxEase.linear});
      case 340:
        FlxTween.tween(black, {alpha: 0.7}, 6, {ease: FlxEase.linear});
      case 356:
        FlxTween.tween(black, {alpha: 1}, 0.4, {ease: FlxEase.linear});
    }
  }
}

