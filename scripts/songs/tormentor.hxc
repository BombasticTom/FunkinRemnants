import funkin.play.song.Song;
import funkin.save.Save;
import funkin.play.PlayState;
import funkin.Preferences;
import flixel.FlxSprite;
import funkin.graphics.FunkinSprite;
import flixel.FlxG;
import flixel.tweens.FlxTween;
import flixel.tweens.FlxEase;

class TormentorSong extends Song
{
  var hasPlayedCutscene:Bool;
  var black:FunkinSprite;
  var fadeBlackTween:FlxTween;
  var hudTween:FlxTween;

  public function new()
  {
    super('tormentor');
  }

  public override function isSongNew(currentDifficulty:String, currentVariation:String):Bool
  {
    if (currentDifficulty == 'easy' || currentDifficulty == 'normal' || currentDifficulty == 'hard')
    {
      return !Save.instance.hasBeatenSong(this.id, null, '');
    }
    return false;
  }

  function onCreate(event:ScriptEvent):Void
  {
    super.onCreate(event);

    black = new FunkinSprite(-20, -20);
    black.makeSolidColor(Std.int(FlxG.width * 1.5), Std.int(FlxG.height * 1.5), 0xFF000000);
    black.cameras = [PlayState.instance.camCutscene];
    PlayState.instance.add(black);
    black.alpha = 1;

    PlayState.instance.camHUD.alpha = 0;
    PlayState.instance.disableKeys = true;
  }

  function onSongRetry(event:ScriptEvent)
  {
    super.onSongRetry(event);

    if (fadeBlackTween != null) fadeBlackTween.cancel();
    if (hudTween != null) hudTween.cancel();

    black.alpha = 1;
    PlayState.instance.camHUD.alpha = 0;
    PlayState.instance.disableKeys = true;
  }

  function onSongStart(event:ScriptEvent):Void
  {
    super.onSongStart(event);

    fadeBlackTween = FlxTween.tween(black, {alpha: 0}, 3, {ease: FlxEase.linear});
  }

  function onBeatHit(event:ScriptEvent):Void
  {
    super.onBeatHit(event);

    switch (event.beat)
    {
      case 14:
        fadeBlackTween = FlxTween.tween(black, {alpha: 1}, 0.1, {ease: FlxEase.linear});

      case 16:
        fadeBlackTween = FlxTween.tween(black, {alpha: 0}, 0.1, {ease: FlxEase.linear});
        hudTween = FlxTween.tween(PlayState.instance.camHUD, {alpha: 1}, 0.1, {ease: FlxEase.linear});

        PlayState.instance.disableKeys = false;

      case 320:
        black.alpha = 1;
        PlayState.instance.disableKeys = true;
    }
  }
}

