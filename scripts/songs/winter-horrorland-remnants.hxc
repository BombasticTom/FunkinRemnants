import funkin.modding.module.ModuleHandler;
import funkin.modding.base.ScriptedFlxRuntimeShader;
import flixel.text.FlxTextBorderStyle;
import funkin.ui.FullScreenScaleMode;
import funkin.graphics.FunkinSprite;
import flixel.util.FlxTimerManager;
import openfl.filters.ShaderFilter;
import funkin.audio.FunkinSound;
import funkin.util.FlxTweenUtil;
import flixel.tweens.FlxTween;
import funkin.play.song.Song;
import flixel.tweens.FlxEase;
import funkin.play.PlayState;
import funkin.util.TouchUtil;
import flixel.util.FlxTimer;
import flixel.text.FlxText;
import funkin.save.Save;
import funkin.Conductor;
import flixel.FlxSprite;
import flixel.FlxG;

class WinterHorrorlandRemnantsMixSong extends Song
{
  var hasPlayedCutscene:Bool = false;
  var onSpookChristmas:Bool = false;

  var cutsceneTimerManager:FlxTimerManager;
  var isMobilePauseButtonPressed:Bool = false;

  var santaDead:ScriptedFunkinSprite = null;
  var spookBeforeChristmas:FunkinSound = null;

  var cutsceneSkipped:Bool = false;
  var canSkipCutscene:Bool = false;
  var overlay:Bool = false;
  var black:FunkinSprite;

  var redFilter:ShaderFilter;

  public function new()
  {
    super('winter-horrorland',
      {
        variation: 'remnants'
      });
  }

  public override function isSongNew(currentDifficulty:String, currentVariation:String):Bool
  {
    if (currentVariation == 'remnants') return !Save.instance.hasBeatenSong(this.id, null, this.variation);
    return false;
  }

  /**
   * Don't replay the cutscene between restarts.
   */
  function onSongRetry(event:ScriptEvent)
  {
    super.onSongRetry(event);

    onSpookChristmas = false;
    cutsceneSkipped = false;
    canSkipCutscene = false;
    hasPlayedCutscene = true;

    if (overlay)
    {
      FlxTween.cancelTweensOf(black);
      black.alpha = 1;
      PlayState.instance.currentStage.getNamedProp('overlay').alpha = 0;

      while (PlayState.instance.camHUD.filters.contains(redFilter))
      {
        PlayState.instance.camHUD.remove(redFilter);
      }

      FlxTween.tween(black, {alpha: 0}, 1, {ease: FlxEase.linear});
      overlay = false;
    }
  }

  /**
   * Replay the cutscene after leaving the song.
   */
  function onCreate(event:ScriptEvent):Void
  {
    super.onCreate(event);

    black = new FunkinSprite(-20, -20);
    black.makeSolidColor(Std.int(FlxG.width * 1.5), Std.int(FlxG.height * 1.5), 0xFF000000);
    black.cameras = [PlayState.instance.camCutscene];
    PlayState.instance.add(black);
    black.alpha = 0;

    onSpookChristmas = false;
    cutsceneSkipped = false;
    canSkipCutscene = false;
    hasPlayedCutscene = false;
    overlay = false;

    var redShader:FlxRuntimeShader = ScriptedFlxRuntimeShader.init('OneColorShader');
    redShader.setColor(0xFFFF0000);
    redFilter = new ShaderFilter(redShader);
    PlayState.instance.camHUD.filters = [];
  }

  function onUpdate(event:UpdateScriptEvent)
  {
    super.onUpdate(event);

    if (cutsceneTimerManager != null)
    {
      cutsceneTimerManager.update(event.elapsed);
    }

    if (!ModuleHandler.getModule('RemnantsBumpers').scriptCall('getIsPlaying'))
    {
      if (FlxG.onMobile)
      {
        isMobilePauseButtonPressed = TouchUtil.overlapsComplex(PlayState.instance.pauseButton) && TouchUtil.justPressed;
      }

      if (onSpookChristmas)
      {
        if ((PlayState.instance.controls.CUTSCENE_ADVANCE || isMobilePauseButtonPressed) && !cutsceneSkipped)
        {
          if (!canSkipCutscene)
          {
            trace('cant skip yet!');
            FlxTween.tween(skipText, {alpha: 1}, 0.5, {ease: FlxEase.quadOut});
            new FlxTimer().start(0.5, function(_) {
              canSkipCutscene = true;
              trace('can skip!');
            });
          }
        }
      }

      if ((PlayState.instance.controls.CUTSCENE_ADVANCE || isMobilePauseButtonPressed) && canSkipCutscene && !cutsceneSkipped)
      {
        skipCutscene();
        trace('Skipped cutscene!');
      }
    }
  }

  var skipText:FlxText;

  function runCutscene()
  {
    onSpookChristmas = true;
    PlayState.instance.currentStage.getDad().visible = false;
    PlayState.instance.camHUD.visible = false;

    cutsceneTimerManager = new FlxTimerManager();

    skipText = new FlxText(936 * FullScreenScaleMode.wideScale.x, 618 * FullScreenScaleMode.wideScale.y, 0,
      'Skip [ ' + PlayState.instance.controls.getDialogueNameFromToken("CUTSCENE_ADVANCE", true) + ' ]', 20);
    if (FlxG.onMobile)
    {
      skipText.text = 'Skip [Pause Button]';
      skipText.x -= 136;
    }
    skipText.setFormat(Paths.font('vcr.ttf'), 40, 0xFFFFFFFF, "right", FlxTextBorderStyle.OUTLINE, 0xFF000000);
    skipText.scrollFactor.set();
    skipText.borderSize = 2;
    skipText.alpha = 0;
    PlayState.instance.currentStage.add(skipText);

    skipText.cameras = [PlayState.instance.camCutscene];

    try
    {
      santaDead = PlayState.instance.currentStage.scriptGet('santaDead');
    }
    catch (e:Exception)
    {
      PlayState.instance.currentStage.getDad().visible = true;
      PlayState.instance.camHUD.visible = true;
      return;
    }

    spookBeforeChristmas = FunkinSound.load(Paths.sound("spookBeforeChristmas"), 1.0);
    spookBeforeChristmas.volume = 1.0;
    spookBeforeChristmas.play(false);

    santaDead.scriptCall('playMonsterComingOut');
    santaDead.anim.pause();

    var santaPos:Array<Float> = [
      PlayState.instance.currentStage.getDad().cameraFocusPoint.x - 200,
      PlayState.instance.currentStage.getDad().cameraFocusPoint.y + 150
    ];

    PlayState.instance.resetCamera(false, false, false);
    PlayState.instance.cancelCameraFollowTween();
    PlayState.instance.cameraFollowPoint.setPosition(santaPos[0], santaPos[1]);

    var durSeconds:Float = Conductor.instance.stepLengthMs * 8 / 1000;
    PlayState.instance.tweenCameraZoom(1.8, durSeconds, false, FlxEase.smootherStepOut);

    new FlxTimer(cutsceneTimerManager).start(3, (tmr:FlxTimer) -> {
      santaDead.anim.resume();
      santaDead.anim.onFinish.addOnce((anim:String) -> {
        this.endCutscene();
      });
    });
  }

  function onCountdownStart(event:ScriptEvent):Void
  {
    super.onCountdownStart(event);

    if (!ModuleHandler.getModule('RemnantsBumpers').scriptCall('getPlayedBumper')
      && ModuleHandler.getModule('RemnantsBumpers').scriptCall('setupBumper'))
    {
      event.cancel();
      PlayState.instance.camHUD.visible = false;
      ModuleHandler.getModule('RemnantsBumpers').scriptCall('setPlayedBumper', true);
    }
    else if (!hasPlayedCutscene)
    {
      hasPlayedCutscene = true;
      event.cancel();
      runCutscene();
    }
  }

  function endCutscene():Void
  {
    if (cutsceneTimerManager != null)
    {
      cutsceneTimerManager.clear();
      cutsceneTimerManager.destroy();
      cutsceneTimerManager = null;
    }

    onSpookChristmas = false;
    santaDead.scriptCall("playDeadSanta");
    PlayState.instance.currentStage.getDad().visible = true;
    PlayState.instance.camHUD.visible = true;
    PlayState.instance.startCountdown();

    var bfPos:Array<Float> = [
      PlayState.instance.currentStage.getBoyfriend().cameraFocusPoint.x,
      PlayState.instance.currentStage.getBoyfriend().cameraFocusPoint.y
    ];

    PlayState.instance.resetCamera(false, false, false);
    PlayState.instance.cancelCameraFollowTween();
    PlayState.instance.cameraFollowPoint.setPosition(bfPos[0], bfPos[1]);
  }

  function skipCutscene():Void
  {
    cutsceneSkipped = true;
    canSkipCutscene = false;

    spookBeforeChristmas?.fadeOut(0.5, 0, function() spookBeforeChristmas?.stop());
    PlayState.instance.camCutscene.fade(0xFF000000, 0.5, false, null, true);

    new FlxTimer(cutsceneTimerManager).start(0.5, function(_) {
      PlayState.instance.justUnpaused = true;
      PlayState.instance.camCutscene.fade(0xFF000000, 0.5, true, null, true);
      skipText.visible = false;
      this.endCutscene();
    });
  }

  public override function onPause(event:ScriptEvent)
  {
    super.onPause(event);

    FlxTweenUtil.pauseTweensOf(black);
  }

  public override function onResume(event:ScriptEvent)
  {
    super.onResume(event);

    FlxTweenUtil.resumeTweensOf(black);
  }

  function onBeatHit(event:ScriptEvent):Void
  {
    super.onBeatHit(event);

    if (PlayState.instance.isGameOverState) return;
    switch (event.beat)
    {
      case 222: // Red time :)
        FlxTween.tween(black, {alpha: 1}, 0.8,
          {
            ease: FlxEase.linear,
            onComplete: (_) -> {
              FlxTween.tween(black, {alpha: 0.5}, 3, {ease: FlxEase.linear});
              PlayState.instance.currentStage.getNamedProp('overlay').alpha = 1;
              PlayState.instance.camHUD.filters.push(redFilter);
              overlay = true;
            }
          });

      case 254:
        FlxTween.tween(black, {alpha: 0}, 0.75, {ease: FlxEase.backIn});

      case 351:
        FlxTween.tween(black, {alpha: 1}, 0.37, {ease: FlxEase.linear});
    }
  }
}
