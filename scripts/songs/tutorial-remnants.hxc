import funkin.modding.module.ModuleHandler;
import funkin.ui.FullScreenScaleMode;
import funkin.graphics.FunkinSprite;
import funkin.util.FlxTweenUtil;
import funkin.play.song.Song;
import flixel.tweens.FlxTween;
import flixel.tweens.FlxEase;
import funkin.play.PlayState;
import funkin.save.Save;
import flixel.FlxG;

class TutorialRemnantsMixSong extends Song
{
  var ending:Bool = false;

  public function new()
  {
    super('tutorial',
      {
        variation: 'remnants'
      });
  }

  public override function isSongNew(currentDifficulty:String, currentVariation:String):Bool
  {
    if (currentVariation == 'remnants') return !Save.instance.hasBeatenSong(this.id, null, this.variation);
    return false;
  }

  var black:FunkinSprite;
  var intro:FunkinSprite;
  var introTween:FlxTween;

  override public function onCreate(event:ScriptEvent):Void
  {
    super.onCreate(event);

    black = new FunkinSprite();
    black.makeSolidColor(FlxG.width * 1.5, FlxG.height * 1.5, 0xFF000000);
    black.screenCenter();
    black.cameras = [PlayState.instance?.camCutscene];
    black.zIndex = -1000;
    PlayState.instance?.add(black);
    PlayState.instace?.refresh();

    var magicEquationNumber:Float = FullScreenScaleMode.gameCutoutSize.x / FlxG.width;
    if (FullScreenScaleMode.gameCutoutSize.x == 0) magicEquationNumber = 1;
    intro = FunkinSprite.createTextureAtlas(-152.45 * magicEquationNumber, -52.15, 'remnants/gf opening', 'week1');
    intro.anim.addByTimeline('anim', intro.anim.getDefaultTimeline(), 24, false);
    intro.cameras = [PlayState.instance?.camCutscene];
    intro.alpha = 0;
    PlayState.instance?.add(intro);

    // Putting this here won't let the player skip the bumper.
    // PlayState.instance.disableKeys = true;

    ending = false;
  }

  function onCountdownStart(event:ScriptEvent):Void
  {
    super.onCountdownStart(event);

    if (!ModuleHandler.getModule('RemnantsBumpers').scriptCall('getPlayedBumper')
      && ModuleHandler.getModule('RemnantsBumpers').scriptCall('setupBumper'))
    {
      event.cancel();
      ModuleHandler.getModule('RemnantsBumpers').scriptCall('setPlayedBumper', true);
    }
    else
    {
      PlayState.instance.disableKeys = true;
    }
  }

  public function onSongEvent(event:SongEventScriptEvent)
  {
    if (event.eventData.eventKind != "tutorialEvents") return;

    switch (event.eventData.value)
    {
      case 'fadeAwayCam':
        // TODO: temporary for now, replace with a fade later?
        black.cameras = [PlayState.instance?.camCutscene];

      case 'playIntro':
        intro.anim.play('anim', true);
        intro.alpha = 1;

      case 'fadeAwayIntro':
        PlayState.instance.disableKeys = false;
        introTween = FlxTween.num(1, 0, 0.5,
          {
            onComplete: () -> introTween = null
          }, (value:Float) -> {
            intro.alpha = value;
            black.alpha = value;
          });

      case 'ending':
        if (PlayState.instance.currentChart.difficulty != 'hard')
        {
          FlxTween.tween(black, {alpha: 1}, 6.72, {ease: FlxEase.expoIn});
          ending = true;
        }
    }
  }

  public function onSongRetry(event:ScriptEvent):Void
  {
    PlayState.instance.disableKeys = true;

    if (black.alpha != 1 && !ending)
    {
      black.alpha = 0;
      intro.alpha = 0;
      introTween = FlxTween.tween(black, {alpha: 1}, 0.5, {onComplete: (_) -> introTween = null});
    }
    else
    {
      black.alpha = 1;
      ending = false;
    }
  }

  override function onPause(event:ScriptEvent):Void
  {
    super.onPause(event);

    FlxTweenUtil.pauseTweensOf(black);
  }

  function onResume(event:ScriptEvent):Void
  {
    super.onResume(event);

    FlxTweenUtil.resumeTweensOf(black);
  }

  function onGameOver(event:ScriptEvent):Void
  {
    PlayState.instance.camHUD.alpha = 0;
    ending = true;
  }
}
