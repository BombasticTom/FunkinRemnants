import flixel.FlxG;
import flixel.FlxSprite;
import flixel.util.FlxTimer;
import funkin.audio.FunkinSound;
import funkin.play.stage.Stage;
import flixel.addons.display.FlxRuntimeShader;
import flixel.sound.FlxSound;
import funkin.Conductor;
import funkin.modding.base.ScriptedFlxRuntimeShader;
import funkin.graphics.shaders.AdjustColorShader;
import funkin.play.PlayState;
import funkin.graphics.shaders.OverlayBlend;
import flixel.addons.display.FlxBackdrop;
import funkin.util.HapticUtil;
import funkin.util.Sequence;
import funkin.play.character.CharacterType;

class LimoRideRemnantsStage extends Stage
{
  function new()
  {
    super('limoRideRemnants');
  }

  var mist1:FlxBackdrop;
  var mist2:FlxBackdrop;
  var mist3:FlxBackdrop;
  var mist4:FlxBackdrop;
  var mist5:FlxBackdrop;

  var colorShaderBf:AdjustColorShader;
  var colorShaderDad:AdjustColorShader;
  var colorShaderGf:AdjustColorShader;

  function buildStage()
  {
    super.buildStage();

    colorShaderBf = new AdjustColorShader();
    colorShaderDad = new AdjustColorShader();
    colorShaderGf = new AdjustColorShader();

    colorShaderBf.brightness = -10;
    colorShaderBf.hue = 20;
    colorShaderBf.contrast = 0;
    colorShaderBf.saturation = -5;

    colorShaderGf.brightness = -5;
    colorShaderGf.hue = 15;
    colorShaderGf.contrast = 0;
    colorShaderGf.saturation = -15;

    colorShaderDad.brightness = -15;
    colorShaderDad.hue = 15;
    colorShaderDad.contrast = 0;
    colorShaderDad.saturation = -15;

    mist1 = new FlxBackdrop(Paths.image('stages/mistMid'), 0x01);
    mist1.setPosition(-650, -100);
    mist1.scrollFactor.set(1.1, 1.1);
    mist1.zIndex = 400;
    mist1.blend = 0;
    mist1.color = 0xFF4a6ee0;
    mist1.alpha = 0.4;
    mist1.velocity.x = 1700;

    mist2 = new FlxBackdrop(Paths.image('stages/mistBack'), 0x01);
    mist2.setPosition(-650, -100);
    mist2.scrollFactor.set(1.2, 1.2);
    mist2.zIndex = 401;
    mist2.blend = 0;
    mist2.color = 0xFF6a4da1;
    mist2.velocity.x = 2100;
    mist1.scale.set(1.3, 1.3);

    mist3 = new FlxBackdrop(Paths.image('stages/mistMid'), 0x01);
    mist3.setPosition(-650, -100);
    mist3.scrollFactor.set(0.8, 0.8);
    mist3.zIndex = 99;
    mist3.blend = 0;
    mist3.color = 0xFFa7d9be;
    mist3.alpha = 0.5;
    mist3.velocity.x = 900;
    mist3.scale.set(1.5, 1.5);

    mist4 = new FlxBackdrop(Paths.image('stages/mistBack'), 0x01);
    mist4.setPosition(-650, -380);
    mist4.scrollFactor.set(0.6, 0.6);
    mist4.zIndex = 98;
    mist4.blend = 0;
    mist4.color = 0xFF9c77c7;
    mist4.velocity.x = 700;
    mist4.scale.set(1.5, 1.5);

    mist5 = new FlxBackdrop(Paths.image('stages/mistMid'), 0x01);
    mist5.setPosition(-650, -400);
    mist5.scrollFactor.set(0.2, 0.2);
    mist5.zIndex = 15;
    mist5.blend = 0;
    mist5.color = 0xFF8a3675;
    mist5.velocity.x = 100;
    mist5.scale.set(1.5, 1.5);

    PlayState.instance.currentStage.add(mist1);
    PlayState.instance.currentStage.add(mist2);
    PlayState.instance.currentStage.add(mist3);
    PlayState.instance.currentStage.add(mist4);
    PlayState.instance.currentStage.add(mist5);
    PlayState.instance.currentStage.refresh(); // Apply z-index.

    // Apply sky shader.
    var skyOverlay:OverlayBlend = new OverlayBlend();
    var sunOverlay:FlxSprite = new FlxSprite().loadGraphic(Paths.image('remnants/limoOverlay'));
    sunOverlay.setGraphicSize(Std.int(sunOverlay.width * 2));
    sunOverlay.updateHitbox();
    skyOverlay.funnyShit.input = sunOverlay.pixels;
    var limoSunset:FlxSprite = getNamedProp('limoSunset');
    if (limoSunset == null)
    {
      trace('[WARN] Could not retrieve limoSunset');
    }
    else
    {
      limoSunset.shader = skyOverlay;
    }

    carSequenceData = [
      {time: 1, callback: () -> doCarHaptics = true},
      {time: 1.4, callback: () -> doCarHaptics = false},
      {time: 2, callback: () -> resetFastCar()}
    ];

    truckSequenceData = [
      {time: 1, callback: () -> doTruckHaptics = true},
      {time: 1.4, callback: () -> doTruckHaptics = false},
      {time: 2, callback: () -> resetFastTruck()}
    ];

    resetFastTruck();
    resetFastCar();
  }

  /**
   * The poles that are in the stage JSON (in this case, there are two).
   */
  var POLE_QUANTITY = 2;

  var _timer:Float = 0;

  var carSequence:Sequence;
  var carSequenceData:Array<SequenceData>;
  var doCarHaptics:Bool = false;

  var truckSequence:Sequence;
  var truckSequenceData:Array<SequenceData>;
  var doTruckHaptics:Bool = false;

  function onUpdate(event:ScriptEvent)
  {
    // Move background lamps.
    for (i in 1...(POLE_QUANTITY + 1))
    {
      var lamp = getNamedProp('bgLight' + i);
      lamp.active = true;
      lamp.velocity.x = 3000;
      if (lamp.x > 2000)
      {
        lamp.x = -1000;
      }

      var pole = getNamedProp('bgPole' + i);
      pole.x = lamp.x + 190;
    }

    _timer += event.elapsed;
    mist1.y = 200 + (Math.sin(_timer) * 200);
    mist2.y = 100 + (Math.sin(_timer * 0.8) * 100);
    mist3.y = 120 + (Math.sin(_timer * 0.5) * 200);
    mist4.y = -80 + (Math.sin(_timer * 0.4) * 300);
    mist5.y = -350 + (Math.sin(_timer * 0.2) * 150);

    if (doCarHaptics)
    {
      HapticUtil.vibrate(0, 0.05, 0.25, 0);
    }

    if (doTruckHaptics)
    {
      HapticUtil.vibrate(0, 0.05, 0.35, 0);
    }
  }

  function onBeatHit(event:SongTimeScriptEvent)
  {
    // When overriding onBeatHit, make sure to call super.onBeatHit,
    // otherwise boppers will not work.
    super.onBeatHit(event);

    if (FlxG.random.bool(10) && fastCarCanDrive && !isDeath) fastVehicleDrive(false);

    if (FlxG.random.bool(5) && fastTruckCanDrive && !isDeath) fastVehicleDrive(true);

    if (event.beat == 72 && PlayState.instance.currentSong.id == "satin-panties")
    {
      if (getBoyfriend() != null && getBoyfriend().hasAnimation('danceLeft-hold'))
      {
        getBoyfriend().shouldAlternate = true;
      }
    }

    if (event.beat == 168 && PlayState.instance.currentSong.id == "milf")
    {
      if (getBoyfriend() != null && getBoyfriend().hasAnimation('danceLeft-hold'))
      {
        getBoyfriend().shouldAlternate = true;
      }
    }
  }

  var fastCarCanDrive:Bool = false;
  var fastTruckCanDrive:Bool = false;

  function onStepHit(event:SongTimeScriptEvent):Void
  {
    super.onStepHit(event);
  }

  /**
   * Apply the shader to the character as it is added.
   */
  override function addCharacter(character:BaseCharacter, charType:CharacterType):Void
  {
    // Apply the shader automatically to each character as it gets added.
    super.addCharacter(character, charType);
    trace('Applied stage shader to ' + character.characterName);
    switch (charType)
    {
      case CharacterType.BF:
        character.shader = colorShaderBf;
      case CharacterType.DAD:
        character.shader = colorShaderDad;
      case CharacterType.GF:
        character.shader = colorShaderGf;
    }
  }

  function resetFastCar():Void
  {
    var fastCar = getNamedProp('fastCar');

    if (fastCar == null) return;

    // Props are inactive by default.
    // Set active to true so position is calculated based on velocity.
    fastCar.active = true;

    fastCar.x = -12600;
    fastCar.y = FlxG.random.int(140, 250);
    fastCar.velocity.x = 0;
    fastCarCanDrive = true;
  }

  function resetFastTruck():Void
  {
    var fastTruck = getNamedProp('fastTruck');

    if (fastTruck == null) return;

    // Props are inactive by default.
    // Set active to true so position is calculated based on velocity.
    fastTruck.active = true;

    fastTruck.x = 12600;
    fastTruck.y = FlxG.random.int(180, 290);
    fastTruck.velocity.x = 0;
    fastTruckCanDrive = true;
  }

  function fastVehicleDrive(isTruck:Bool):Void
  {
    var vehicleSound:String = isTruck ? 'truckPass' : 'carPass';
    FunkinSound.playOnce(Paths.soundRandom(vehicleSound, 0, 1), 0.7);

    var vehicle = isTruck ? getNamedProp('fastTruck') : getNamedProp('fastCar');
    vehicle.velocity.x = isTruck ? (FlxG.random.int(-170, -220) / FlxG.elapsed) * 3 : (FlxG.random.int(170, 220) / FlxG.elapsed) * 3;

    if (isTruck)
    {
      fastTruckCanDrive = false;
      truckSequence = new Sequence(truckSequenceData);
    }
    else
    {
      fastCarCanDrive = false;
      carSequence = new Sequence(carSequenceData);
    }
  }

  /**
   * If your stage uses additional assets not specified in the JSON,
   * make sure to specify them like this, or they won't get cached in the loading screen.
   */
  function fetchAssetPaths():Array<String>
  {
    var results:Array<String> = super.fetchAssetPaths();

    // This graphic is applied by shader to the background, so it's not included in the default stage function.
    results.push(Paths.image('remnants/limoOverlay'));
    results.push(Paths.sound('carPass0'));
    results.push(Paths.sound('carPass1'));

    results.push(Paths.sound('truckPass0'));
    results.push(Paths.sound('truckPass1'));

    return results;
  }

  var isDeath:Bool = false;

  /**
   * Make sure the fast car is reset when the song restarts.
   */
  function onSongRetry(event:ScriptEvent)
  {
    super.onSongRetry(event);

    resetFastTruck();
    resetFastCar();

    isDeath = false;
  }

  override function onGameOver(event:ScriptEvent):Void
  {
    super.onGameOver(event);

    resetFastTruck();
    resetFastCar();

    isDeath = true;
  }

  /**
   * Make sure the fast car is reset when the song restarts.
   */
  function onCountdownStart(event:ScriptEvent)
  {
    super.onCountdownStart(event);

    resetFastTruck();
    resetFastCar();

    isDeath = false;
  }

  override function onPause(event:PauseScriptEvent)
  {
    super.onPause(event);

    if (carSequence != null) carSequence.running = false;
    if (truckSequence != null) truckSequence.running = false;
  }

  override function onResume(event:ScriptEvent)
  {
    super.onResume(event);

    if (carSequence != null) carSequence.running = true;
    if (truckSequence != null) truckSequence.running = true;
  }
}

