import flixel.FlxG;
import flixel.math.FlxMath;
import flixel.tweens.FlxEase;
import flixel.tweens.FlxTween;
import flixel.util.FlxTimer;
import funkin.audio.FunkinSound;
import funkin.Conductor;
import funkin.graphics.shaders.RuntimeRainShader;
import funkin.play.character.CharacterType;
import funkin.play.PlayState;
import funkin.play.stage.Stage;
import openfl.filters.ShaderFilter;
import flixel.addons.display.FlxBackdrop;
import funkin.util.FlxTweenUtil;

using StringTools;

class PhillyStreetsRemnantsStage extends Stage
{
  var rainShader:RuntimeRainShader = new RuntimeRainShader();
  var rainShaderFilter:ShaderFilter;

  var paperInterruptable:Bool = true;

  // as song goes on, these are used to make the rain more intense throught the song
  // these values are also used for the rain sound effect volume intensity!
  var rainShaderStartIntensity:Float;
  var rainShaderEndIntensity:Float;

  function new()
  {
    super('phillyStreetsRemnants');
    resetPaper();
  }

  override function onCountdownStart(event:ScriptEvent)
  {
    super.onCountdownStart(event);
    resetPaper();
  }

  override function onCreate(event:ScriptEvent):Void
  {
    super.onCreate(event);

    if (!RemnantsOptions.getPerformanceOption())
    {
      rainShader.scale = FlxG.height / 200; // adjust this value so that the rain looks nice
      FlxG.console.registerObject("rainShader", rainShader);
      switch (PlayState.instance.currentSong.id)
      {
        case "darnell":
          rainShaderStartIntensity = 0;
          rainShaderEndIntensity = 0.1;
        case "lit-up":
          rainShaderStartIntensity = 0.1;
          rainShaderEndIntensity = 0.2;
        case "2hot":
          rainShaderStartIntensity = 0.2;
          rainShaderEndIntensity = 0.4;
      }

      rainShader.intensity = rainShaderStartIntensity;
      rainShader.rainColor = 0xFFa8adb5;

      // set the shader input
      rainShaderFilter = new ShaderFilter(rainShader);
      FlxG.camera.filters = [rainShaderFilter];
    }

    resetPaper();
  }

  var mist0:FlxBackdrop;
  var mist1:FlxBackdrop;
  var mist2:FlxBackdrop;
  var mist3:FlxBackdrop;
  var mist4:FlxBackdrop;
  var mist5:FlxBackdrop;

  override function buildStage()
  {
    super.buildStage();

    if (!RemnantsOptions.getPerformanceOption())
    {
      mist0 = new FlxBackdrop(Paths.image('stages/mistMid'), 0x01);
      mist0.setPosition(-650, 250);
      mist0.scrollFactor.set(1.2, 1.2);
      mist0.zIndex = 1000;
      mist0.blend = 0;
      mist0.color = 0xFF5c5c5c;
      mist0.alpha = 0.6;
      mist0.velocity.x = 172;
      PlayState.instance.currentStage.add(mist0);

      mist1 = new FlxBackdrop(Paths.image('stages/mistMid'), 0x01);
      mist1.setPosition(-650, 250);
      mist1.scrollFactor.set(1.1, 1.1);
      mist1.zIndex = 1000;
      mist1.blend = 0;
      mist1.color = 0xFF5c5c5c;
      mist1.alpha = 0.6;
      mist1.velocity.x = 150;
      PlayState.instance.currentStage.add(mist1);

      mist2 = new FlxBackdrop(Paths.image('stages/mistBack'), 0x01);
      mist2.setPosition(-650, 250);
      mist2.scrollFactor.set(1.2, 1.2);
      mist2.zIndex = 1001;
      mist2.blend = 0;
      mist2.color = 0xFF5c5c5c;
      mist2.alpha = 0.8;
      mist2.velocity.x = -80;
      PlayState.instance.currentStage.add(mist2);

      mist3 = new FlxBackdrop(Paths.image('stages/mistMid'), 0x01);
      mist3.setPosition(-650, 250);
      mist3.scrollFactor.set(0.95, 0.95);
      mist3.zIndex = 99;
      mist3.blend = 0;
      mist3.color = 0xFF5c5c5c;
      mist3.alpha = 0.5;
      mist3.velocity.x = -50;
      mist3.scale.set(0.8, 0.8);
      PlayState.instance.currentStage.add(mist3);

      mist4 = new FlxBackdrop(Paths.image('stages/mistBack'), 0x01);
      mist4.setPosition(-650, 250);
      mist4.scrollFactor.set(0.8, 0.8);
      mist4.zIndex = 88;
      mist4.blend = 0;
      mist4.color = 0xFF5c5c5c;
      mist4.alpha = 1;
      mist4.velocity.x = 40;
      mist4.scale.set(0.7, 0.7);

      PlayState.instance.currentStage.add(mist4);

      mist5 = new FlxBackdrop(Paths.image('stages/mistMid'), 0x01);
      mist5.setPosition(-650, 250);
      mist5.scrollFactor.set(0.5, 0.5);
      mist5.zIndex = 39;
      mist5.blend = 0;
      mist5.color = 0xFF5c5c5c;
      mist5.alpha = 1;
      mist5.velocity.x = 20;
      mist5.scale.set(1.1, 1.1);

      PlayState.instance.currentStage.add(mist5);
      PlayState.instance.currentStage.refresh(); // Apply z-index.
    }
  }

  override public function addCharacter(character:BaseCharacter, charType:CharacterType):Void
  {
    super.addCharacter(character, charType);

    if (character.characterId.startsWith('gf') && charType == CharacterType.GF)
    {
      character.x += 99;
      character.originalPosition.x = character.x;
    }
  }

  var _timer:Float = 0;

  override function onUpdate(event:UpdateScriptEvent)
  {
    super.onUpdate(event);

    if (!RemnantsOptions.getPerformanceOption())
    {
      _timer += event.elapsed;
      mist0.y = 900 + (Math.sin(_timer * 0.35) * 70);
      mist1.y = 950 + (Math.sin(_timer * 0.3) * 80);
      mist2.y = 975 + (Math.sin(_timer * 0.4) * 60);
      mist3.y = 830 + (Math.sin(_timer * 0.3) * 70);
      mist4.y = 770 + (Math.sin(_timer * 0.35) * 50);
      mist5.y = 790 + (Math.sin(_timer * 0.08) * 100);

      if (FlxG.sound.music != null)
      {
        var remappedIntensityValue:Float = FlxMath.remapToRange(Conductor.instance.songPosition, 0, FlxG.sound.music.length, rainShaderStartIntensity,
          rainShaderEndIntensity);
        rainShader.intensity = remappedIntensityValue;
        rainShader.updateViewInfo(FlxG.width, FlxG.height, FlxG.camera);
        rainShader.update(event.elapsed);
      }
      else
      {
        rainShader.intensity = rainShaderStartIntensity;
        rainShader.updateViewInfo(FlxG.width, FlxG.height, FlxG.camera);
        rainShader.update(event.elapsed);
      }
    }
  }

  var paperTimer:FlxTimer;

  function onBeatHit(event:SongTimeScriptEvent)
  {
    super.onBeatHit(event);

    var paper = getNamedProp('paper');
    if (FlxG.random.bool(1) && paperInterruptable == true)
    {
      if (paper != null)
      {
        paper.visible = true;
        paper.animation.play('paperBlow');
        paper.y = 608 + FlxG.random.float(-150, 150);
        FlxTween.tween(paper, {x: paper.x - 550}, 2);

        paperInterruptable = false;
        paperTimer = new FlxTimer().start(2, function(tmr:FlxTimer) {
          resetPaper();
        });
      }
    }
  }

  public override function onPause(event:PauseScriptEvent)
  {
    super.onPause(event);
    var paper = getNamedProp('paper');
    if (paper != null)
    {
      FlxTweenUtil.pauseTweensOf(paper);
      if (paperTimer != null) paperTimer.active = false;
    }
  }

  public override function onResume(event:ScriptEvent)
  {
    super.onResume(event);

    var paper = getNamedProp('paper');
    if (paper != null)
    {
      FlxTweenUtil.resumeTweensOf(paper);
      if (paperTimer != null) paperTimer.active = true;
    }
  }

  override function onGameOver(event:ScriptEvent):Void
  {
    super.onGameOver(event);

    // Make it so the rain shader doesn't show over the game over screen
    FlxG.camera.filters = [];
    paperInterruptable = true;
  }

  override function onSongRetry(event:ScriptEvent):Void
  {
    super.onSongRetry(event);

    if (!RemnantsOptions.getPerformanceOption())
    {
      // Make it so the rain shader doesn't show over the game over screen
      FlxG.camera.filters = [rainShaderFilter];
    }

    refresh(); // This game fucking sucks so I added this
  }

  function resetPaper()
  {
    var paper = getNamedProp('paper');
    if (paperTimer != null) paperTimer.destroy();
    paperInterruptable = true;

    if (paper != null)
    {
      FlxTween.cancelTweensOf(paper);
      paper.visible = false;
      paper.x = 550;
    }
  }
}
