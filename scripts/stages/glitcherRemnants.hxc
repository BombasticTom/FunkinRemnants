import funkin.play.stage.Stage;
import funkin.play.PlayState;
import funkin.play.character.CharacterType;
import funkin.data.character.CharacterDataParser;
import flixel.tweens.FlxEase;
import flixel.tweens.FlxTween;

class GlitcherRemnantsStage extends Stage
{
  var wentDark:Bool = false;
  var ambienceTween:FlxTween;

  function new()
  {
    super('glitcherRemnants');
  }

  override public function addCharacter(character:BaseCharacter, type:CharacterType):Void
  {
    super.addCharacter(character, type);

    var id:String = switch (type)
    {
      case CharacterType.BF: 'bf';
      case CharacterType.GF: 'gf';
      case CharacterType.DAD: 'dad';
      default: null;
    }

    if (this.characters.exists('remnants-' + id)) return;

    var remnantsChar:BaseCharacter = switch (type)
    {
      case CharacterType.BF: CharacterDataParser.fetchCharacter('bf-remnants');
      case CharacterType.GF: CharacterDataParser.fetchCharacter('gf-remnants');
      case CharacterType.DAD: CharacterDataParser.fetchCharacter('hex-glitcher-remnants');
      default: null;
    }

    if (remnantsChar == null) return;

    super.addCharacter(remnantsChar, type);
    this.characters.set(id, character);
    this.characters.set('remnants-' + id, remnantsChar);
  }

  public override function onCountdownStart(event:CountdownScriptEvent):Void
  {
    super.onCountdownStart(event);
    changeAmbiance(false, 0.0001);
  }

  function onBeatHit(event:SongTimeScriptEvent)
  {
    super.onBeatHit(event);

    if (PlayState.instance.currentSong != null)
    {
      if (PlayState.instance.currentSong.id == "glitcher-remnants")
      {
        switch (event.beat)
        {
          case 143:
            changeAmbiance(true, 0.2);
          case 207:
            changeAmbiance(false, 0.2);
          case 271:
            changeAmbiance(true, 0.2);
          case 335:
            changeAmbiance(false, 0.2);
        }
      }
    }
  }

  function changeAmbiance(goDark:Bool, duration:Float)
  {
    if (duration == null) duration = 1;
    var targetAlpha:Float = goDark ? 1 : 0;
    wentDark = goDark;

    ambienceTween?.cancel();
    ambienceTween = FlxTween.num(1 - targetAlpha, targetAlpha, duration, {
      onComplete: (_) -> {
        ambienceTween = null;
      }
    }, (curAlpha:Float) -> {
      for (id in ['bf', 'gf', 'dad'])
      {
        var remnantsChar:BaseCharacter = getCharacter('remnants-' + id);
        var char:BaseCharacter = getCharacter(id);

        if (remnantsChar != null)
        {
          remnantsChar.alpha = 1 - curAlpha;
          if (remnantsChar.alpha == 0) remnantsChar.alpha = 0.0001;
        }
        if (char != null)
        {
          char.alpha = curAlpha;
          if (char.alpha == 0) char.alpha = 0.0001;
        }
      }

      if (getNamedProp('Court Back') != null) getNamedProp('Court Back').alpha = curAlpha;
    });

    for (id in ['bf', 'dad'])
    {
      var remnantsChar:BaseCharacter = getCharacter('remnants-' + id);
      var char:BaseCharacter = getCharacter(id);

      var targetChar:BaseCharacter = goDark ? char : remnantsChar;
      targetChar.initHealthIcon(id == 'dad');
    }
  }

  function onPause(event:ScriptEvent):Void
  {
    if (ambienceTween != null) ambienceTween.active = false;
  }

  function onResume(event:ScriptEvent):Void
  {
    if (ambienceTween != null) ambienceTween.active = true;
  }

  function onGameOver(event:ScriptEvent)
  {
    // Swap Remnants BF and current BF temporarily for Game Over.
    var oldBF:BaseCharacter = getBoyfriend();
    this.characters.set('bf', getCharacter('remnants-bf'));
    this.characters.set('remnants-bf', oldBF);

    getBoyfriend()?.alpha = 1;
    oldBF?.alpha = 0;

    super.onGameOver(event);
  }

  function onSongRetry(event:ScriptEvent):Void
  {
    // Swap BF and Remnants BF back after Game Over.
    var remnantsBF:BaseCharacter = getBoyfriend();
    this.characters.set('bf', getCharacter('remnants-bf'));
    this.characters.set('remnants-bf', remnantsBF);

    // no alpha changes here, they get handled at countdown!
    super.onSongRetry();
  }
}

