import flixel.FlxG;
import flixel.tweens.FlxTween;
import funkin.audio.FunkinSound;
import funkin.graphics.FunkinSprite;
import funkin.ui.FullScreenScaleMode;
import funkin.play.stage.Stage;
import funkin.util.HapticUtil;
import funkin.play.PlayState;
import flixel.util.FlxTimer;

class SpookyMansionRemnantsStage extends Stage
{
	function new()
	{
		super('spookyMansionRemnants');
	}

	var lightningStrikeBeat:Int = 0;
	var lightningStrikeOffset:Int = 8;
	var halloweenWhite:FunkinSprite;

	override function buildStage()
	{
		super.buildStage();
		getNamedProp('sky').color = 0xFF000000;
		
		var gameScale = FullScreenScaleMode.wideScale;

		halloweenWhite = new FunkinSprite();
		halloweenWhite.makeSolidColor(gameScale.x * 2, gameScale.y * 2, 0xFFFFFFFF);
		halloweenWhite.alpha = 0.00001;
		halloweenWhite.zIndex = 500;
		halloweenWhite.blend = 0;

		PlayState.instance.currentStage.add(halloweenWhite);
		PlayState.instance.currentStage.refresh();
	}

	function doLightningStrike(playSound:Bool, beat:Int):Void
	{
		if (playSound)
		{
			FunkinSound.playOnce(Paths.soundRandom('thunder_', 1, 2), 1.0);
		}

		halloweenWhite.alpha = 0.4;
		FlxTween.tween(halloweenWhite, {alpha: 0.5}, 0.075);
		FlxTween.tween(halloweenWhite, {alpha: 0}, 0.25, {startDelay: 0.15});

		getNamedProp('sky').color = 0xFFFFFFFF;
		getNamedProp('mansionThunder').alpha = 1;
		getNamedProp('paintingThunder').alpha = 1;
		getNamedProp('coatThunder').alpha = 1;
		getNamedProp('cabinetThunder').alpha = 1;

		getGirlfriend().color = 0xFF000000;
		getBoyfriend().color = 0xFF000000;
		getDad().color = 0xFF000000;

		new FlxTimer().start(0.06, _ -> {
			getNamedProp('sky').color = 0xFF000000;
			getNamedProp('mansionThunder').alpha = 0;
			getNamedProp('paintingThunder').alpha = 0;
			getNamedProp('coatThunder').alpha = 0;
			getNamedProp('cabinetThunder').alpha = 0;

			getGirlfriend().color = 0xFFFFFFFF;
			getBoyfriend().color = 0xFFFFFFFF;
			getDad().color = 0xFFFFFFFF;
		});

		new FlxTimer().start(0.12, _ -> {
			getNamedProp('sky').color = 0xFFFFFFFF;
			getNamedProp('mansionThunder').alpha = 1;
			getNamedProp('paintingThunder').alpha = 1;
			getNamedProp('coatThunder').alpha = 1;
			getNamedProp('cabinetThunder').alpha = 1;

			getGirlfriend().color = 0xFF000000;
			getBoyfriend().color = 0xFF000000;
			getDad().color = 0xFF000000;

			FlxTween.color(getNamedProp('sky'), 1.5, 0xFFFFFFFF, 0xFF000000);
			FlxTween.tween(getNamedProp('mansionThunder'), {alpha: 0}, 1.5);
			FlxTween.tween(getNamedProp('paintingThunder'), {alpha: 0}, 1.5);
			FlxTween.tween(getNamedProp('coatThunder'), {alpha: 0}, 1.5);
			FlxTween.tween(getNamedProp('cabinetThunder'), {alpha: 0}, 1.5);

			FlxTween.color(getGirlfriend(), 1, 0xFF000000, 0xFFFFFFFF);
			FlxTween.color(getBoyfriend(), 1, 0xFF000000, 0xFFFFFFFF);
			FlxTween.color(getDad(), 1, 0xFF000000, 0xFFFFFFFF);
		});

		lightningStrikeBeat = beat;
		lightningStrikeOffset = FlxG.random.int(8, 24);

		if (getBoyfriend().hasAnimation('scared') && getBoyfriend().animation.name != 'cheer')
		{
			getBoyfriend().playAnimation('scared', true, true);
		}

		if (getGirlfriend().hasAnimation('scared'))
		{
			getGirlfriend().playAnimation('scared', true, true);
		}

		triggerLightningHaptics();
	}

	var doPostShockHaptics:Bool = false;

	function triggerLightningHaptics()
	{
		if (!HapticUtil.hapticsAvailable) return;
		HapticUtil.vibrate(0, 0.05, 1);
		doPostShockHaptics = true;
	}

	/**
	 * If your stage uses additional assets not specified in the JSON,
	 * make sure to specify them like this, or they won't get cached in the loading screen.
	 */
	function fetchAssetPaths():Array<String>
	{
		var results:Array<String> = super.fetchAssetPaths();
		results.push(Paths.sound('thunder_1'));
		results.push(Paths.sound('thunder_2'));
		return results;
	}

	function onBeatHit(event:SongTimeScriptEvent)
	{
		super.onBeatHit(event);

		// Play lightning at random intervals.
		if (FlxG.random.bool(10) && event.beat > (lightningStrikeBeat + lightningStrikeOffset))
		{
			doLightningStrike(true, event.beat);
		}
	}

	var postShockCounter:Int = 0;
	var counterTargetNum:Int = 10;

	function onStepHit(event:SongTimeScriptEvent)
	{
		super.onStepHit(event);

		if (doPostShockHaptics)
		{
			postShockCounter++;

			var postShockAmplitude:Float = 0.05 * (counterTargetNum - postShockCounter) * 2.5;
			HapticUtil.vibrate(0, 0.01, postShockAmplitude, 0);

			if (postShockCounter == counterTargetNum)
			{
				doPostShockHaptics = false;
				postShockCounter = 0;
			}
		}
	}

	function onSongRetry(event:ScriptEvent)
	{
		super.onSongRetry(event);

		// Properly reset lightning when restarting the song.
		lightningStrikeBeat = 0;
		lightningStrikeOffset = 8;
	}
}
