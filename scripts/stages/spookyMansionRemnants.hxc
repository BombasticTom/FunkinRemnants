import flixel.FlxG;
import flixel.tweens.FlxEase;
import flixel.tweens.FlxTween;
import flixel.util.FlxTimer;
import funkin.audio.FunkinSound;
import funkin.graphics.FunkinSprite;
import funkin.ui.FullScreenScaleMode;
import funkin.play.stage.Stage;
import funkin.play.PlayState;
import funkin.util.HapticUtil;
import funkin.util.FlxTweenUtil;

class SpookyMansionRemnantsStage extends Stage
{
  function new()
  {
    super('spookyMansionRemnants');
  }

  var lightningStrikeBeat:Int = 0;
  var lightningStrikeOffset:Int = 8;
  var halloweenWhite:FunkinSprite;

  // These are similar but are used for different things.
  var wentDark:Bool = false;
  var stopThunders:Bool = false;

  function onCreate(event:ScriptEvent):Void
  {
    super.onCreate(event);
    stopThunders = false;

    halloweenWhite = new FunkinSprite(-20, -20);
    halloweenWhite.makeSolidColor(Std.int(FlxG.width * 1.5), Std.int(FlxG.height * 1.5), 0xFFFFFFFF);
    halloweenWhite.cameras = [PlayState.instance.camCutscene];
    PlayState.instance.add(halloweenWhite);
    halloweenWhite.alpha = 0.0001;
    halloweenWhite.blend = 0;
  }

  override function buildStage()
  {
    super.buildStage();
    getNamedProp('sky')?.color = 0xFF000000;
  }

  function doLightningStrike(playSound:Bool, beat:Int):Void
  {
    if (playSound)
    {
      FunkinSound.playOnce(Paths.soundRandom('thunder_', 1, 2), 1.0);
    }

    if (!wentDark)
    {
      getNamedProp('sky')?.color = 0xFFFFFFFF;
      getNamedProp('mansionThunder')?.alpha = 1;
      getNamedProp('paintingThunder')?.alpha = 1;
      getNamedProp('coatThunder')?.alpha = 1;
      getNamedProp('cabinetThunder')?.alpha = 1;

      getGirlfriend()?.alpha = 1;
      getBoyfriend()?.alpha = 1;
      getDad()?.alpha = 1;

      new FlxTimer().start(0.06, _ -> {
        getNamedProp('sky')?.color = 0xFF000000;
        getNamedProp('mansionThunder')?.alpha = 0;
        getNamedProp('paintingThunder')?.alpha = 0;
        getNamedProp('coatThunder')?.alpha = 0;
        getNamedProp('cabinetThunder')?.alpha = 0;

        getGirlfriend()?.alpha = 0;
        getBoyfriend()?.alpha = 0;
        getDad()?.alpha = 0;
      });

      new FlxTimer().start(0.12, _ -> {
        getNamedProp('sky')?.color = 0xFFFFFFFF;
        getNamedProp('mansionThunder')?.alpha = 1;
        getNamedProp('paintingThunder')?.alpha = 1;
        getNamedProp('coatThunder')?.alpha = 1;
        getNamedProp('cabinetThunder')?.alpha = 1;

        getGirlfriend()?.alpha = 1;
        getBoyfriend()?.alpha = 1;
        getDad()?.alpha = 1;

        if (getNamedProp('sky') != null) FlxTween.color(getNamedProp('sky'), 1.5, 0xFFFFFFFF, 0xFF000000);
        if (getNamedProp('mansionThunder') != null) FlxTween.tween(getNamedProp('mansionThunder'), {alpha: 0}, 1.5);
        if (getNamedProp('paintingThunder') != null) FlxTween.tween(getNamedProp('paintingThunder'), {alpha: 0}, 1.5);
        if (getNamedProp('coatThunder') != null) FlxTween.tween(getNamedProp('coatThunder'), {alpha: 0}, 1.5);
        if (getNamedProp('cabinetThunder') != null) FlxTween.tween(getNamedProp('cabinetThunder'), {alpha: 0}, 1.5);

        if (getGirlfriend() != null) FlxTween.tween(getGirlfriend(), {alpha: 0}, 1.5);
        if (getBoyfriend() != null) FlxTween.tween(getBoyfriend(), {alpha: 0}, 1.5);
        if (getDad() != null) FlxTween.tween(getDad(), {alpha: 0}, 1.5);
      });
    }
    else // Only flash the sky
    {
      getNamedProp('sky')?.color = 0xFFFFFFFF;

      if (halloweenWhite != null)
      {
        halloweenWhite.alpha = 0.3;
        FlxTween.tween(halloweenWhite, {alpha: 0.4}, 0.075);
        FlxTween.tween(halloweenWhite, {alpha: 0}, 0.25, {startDelay: 0.15, ease: FlxEase.quadInOut});
      }

      new FlxTimer().start(0.06, _ -> {
        getNamedProp('sky')?.color = 0xFF000000;
      });

      new FlxTimer().start(0.12, _ -> {
        getNamedProp('sky')?.color = 0xFFFFFFFF;
        if (getNamedProp('sky') != null) FlxTween.color(getNamedProp('sky'), 1.5, 0xFFFFFFFF, 0xFF000000);
      });
    }

    lightningStrikeBeat = beat;
    lightningStrikeOffset = FlxG.random.int(8, 24);

    if (getBoyfriend().hasAnimation('scared')
      && getBoyfriend().animation.name != 'hey'
      && getBoyfriend().animation.name != 'cheer'
      && getBoyfriend().animation.name != 'lights')
    {
      getBoyfriend().playAnimation('scared', true, false);
    }

    if (getGirlfriend().hasAnimation('scared'))
    {
      getGirlfriend().playAnimation('scared', true, false);
    }

    triggerLightningHaptics();
  }

  public override function onCountdownStart(event:CountdownScriptEvent):Void
  {
    super.onCountdownStart(event);

    getBoyfriend()?.alpha = 0;
    getGirlfriend()?.alpha = 0;
    getDad()?.alpha = 0;
    changeAmbiance(false, 0.0001);
  }

  var doPostShockHaptics:Bool = false;

  function triggerLightningHaptics()
  {
    if (!HapticUtil.hapticsAvailable) return;
    HapticUtil.vibrate(0, 0.05, 1);
    doPostShockHaptics = true;
  }

  /**
   * If your stage uses additional assets not specified in the JSON,
   * make sure to specify them like this, or they won't get cached in the loading screen.
   */
  function fetchAssetPaths():Array<String>
  {
    var results:Array<String> = super.fetchAssetPaths();
    results.push(Paths.sound('thunder_1'));
    results.push(Paths.sound('thunder_2'));
    return results;
  }

  function onPause()
  {
    FlxTweenUtil.pauseTweensOf(getGirlfriend());
    FlxTweenUtil.pauseTweensOf(getBoyfriend());
    FlxTweenUtil.pauseTweensOf(getDad());
    FlxTweenUtil.pauseTweensOf(getNamedProp('sky'));
    FlxTweenUtil.pauseTweensOf(getNamedProp('mansionThunder'));
    FlxTweenUtil.pauseTweensOf(getNamedProp('paintingThunder'));
    FlxTweenUtil.pauseTweensOf(getNamedProp('coatThunder'));
    FlxTweenUtil.pauseTweensOf(getNamedProp('cabinetThunder'));
    FlxTweenUtil.pauseTweensOf(halloweenWhite);
  }

  function onResume()
  {
    FlxTweenUtil.resumeTweensOf(getGirlfriend());
    FlxTweenUtil.resumeTweensOf(getBoyfriend());
    FlxTweenUtil.resumeTweensOf(getDad());
    FlxTweenUtil.resumeTweensOf(getNamedProp('sky'));
    FlxTweenUtil.resumeTweensOf(getNamedProp('mansionThunder'));
    FlxTweenUtil.resumeTweensOf(getNamedProp('paintingThunder'));
    FlxTweenUtil.resumeTweensOf(getNamedProp('coatThunder'));
    FlxTweenUtil.resumeTweensOf(getNamedProp('cabinetThunder'));
    FlxTweenUtil.resumeTweensOf(halloweenWhite);
  }

  function onBeatHit(event:SongTimeScriptEvent)
  {
    super.onBeatHit(event);

    // Play lightning at random intervals.
    if (FlxG.random.bool(10) && event.beat > (lightningStrikeBeat + lightningStrikeOffset) && !stopThunders)
    {
      var leBeat = event.beat;
      new FlxTimer().start(0.1, _ -> {
        doLightningStrike(true, leBeat);
      });
    }

    if (PlayState.instance.currentSong != null)
    {
      if (event.beat == 139 && PlayState.instance.currentSong.id == "spookeez")
      {
        new FlxTimer().start(0.3, _ -> {
          changeAmbiance(true, 0.1);
        });
      }
      if (PlayState.instance.currentSong.id == "south")
      {
        switch (event.beat)
        {
          case 32:
            changeAmbiance(true, 0.0001);
          case 64:
            changeAmbiance(false, 1);
          case 96:
            changeAmbiance(true, 1);
          case 192:
            changeAmbiance(false, 1);
          case 222:
            changeAmbiance(true, 0.3);
        }
      }
      if (PlayState.instance.currentSong.id == "monster")
      {
        switch (event.beat)
        {
          case 0:
            changeAmbiance(true, 0.00001);
          case 128:
            changeAmbiance(false, 0.1);
          case 227:
            changeAmbiance(true, 0.5);
        }
      }
    }
  }

  function changeAmbiance(goDark:Bool, duration:Float)
  {
    if (duration == null) duration = 1;
    var targetAlpha:Float = goDark ? 1 : 0;
    wentDark = goDark;

    // Cancel tweens just in case thunder strikes so they can actaully work here
    FlxTween.cancelTweensOf(getGirlfriend());
    FlxTween.cancelTweensOf(getBoyfriend());
    FlxTween.cancelTweensOf(getDad());
    FlxTween.cancelTweensOf(getNamedProp('mansionThunder'));
    FlxTween.cancelTweensOf(getNamedProp('paintingThunder'));
    FlxTween.cancelTweensOf(getNamedProp('coatThunder'));
    FlxTween.cancelTweensOf(getNamedProp('cabinetThunder'));

    if (getGirlfriend() != null) FlxTween.tween(getGirlfriend(), {alpha: targetAlpha}, duration);
    if (getBoyfriend() != null) FlxTween.tween(getBoyfriend(), {alpha: targetAlpha}, duration);
    if (getDad() != null) FlxTween.tween(getDad(), {alpha: targetAlpha}, duration);

    if (getNamedProp('mansionThunder') != null) FlxTween.tween(getNamedProp('mansionThunder'), {alpha: targetAlpha}, duration);
    if (getNamedProp('paintingThunder') != null) FlxTween.tween(getNamedProp('paintingThunder'), {alpha: targetAlpha}, duration);
    if (getNamedProp('coatThunder') != null) FlxTween.tween(getNamedProp('coatThunder'), {alpha: targetAlpha}, duration);
    if (getNamedProp('cabinetThunder') != null) FlxTween.tween(getNamedProp('cabinetThunder'), {alpha: targetAlpha}, duration);
  }

  var postShockCounter:Int = 0;
  var counterTargetNum:Int = 10;

  function onStepHit(event:SongTimeScriptEvent)
  {
    super.onStepHit(event);

    if (doPostShockHaptics)
    {
      postShockCounter++;

      var postShockAmplitude:Float = 0.05 * (counterTargetNum - postShockCounter) * 2.5;
      HapticUtil.vibrate(0, 0.01, postShockAmplitude, 0);

      if (postShockCounter == counterTargetNum)
      {
        doPostShockHaptics = false;
        postShockCounter = 0;
      }
    }
  }

  function onGameOver(event:ScriptEvent)
  {
    super.onGameOver(event);

    stopThunders = true;

    // Makes bros death actaully visible
    FlxTween.cancelTweensOf(getBoyfriend());
    getBoyfriend()?.alpha = 1;
  }

  function onSongRetry(event:ScriptEvent)
  {
    super.onSongRetry(event);

    // Properly reset lightning when restarting the song.
    lightningStrikeBeat = 0;
    lightningStrikeOffset = 8;
    stopThunders = false;
  }
}

