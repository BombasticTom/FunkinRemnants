import funkin.play.PlayState;
import funkin.graphics.FunkinSprite;
import flixel.tweens.FlxEase;
import flixel.tweens.FlxTween;
import funkin.play.GameOverSubState;
import funkin.modding.module.Module;
import funkin.util.Constants;

using StringTools;

class RemnantsGameOver extends Module
{
  final CHAR_BLACKLIST:Array<String> = ["bf-remnants-blazin", "pico-remnants-blazin"];
  final SUFFIX_BLACKLIST:Array<String> = ["-bf-explode-remnants", "-bf-explode-remnants"];

  public var loseSprite:FunkinSprite;
  public var loseTween:FlxTween;

  public function new()
  {
    super('remnantsGameOver', 1000, {state: PlayState});
  }

  override public function onUpdate(event:UpdateScriptEvent):Void
  {
    super.onUpdate(event);

    // If game over doesn't exist, don't continue.
    if (GameOverSubState.instance == null) return;

    // If the song is not a remnants variant, don't continue.
    if (!GameOverSubState.musicSuffix.contains('remnants')) return;

    // If the character is blacklisted, don't continue.
    if (CHAR_BLACKLIST.contains(GameOverSubState.instance?.boyfriend?.characterId)) return;

    // If the suffix is blacklisted, don't continue.
    if (SUFFIX_BLACKLIST.contains(GameOverSubState.musicSuffix)) return;

    createDeathSprites();
    playFall();
    playFade();
  }

  public function createDeathSprites():Void
  {
    // If game over already has `loseSprite`, don't continue.
    if (GameOverSubState.instance.members.contains(loseSprite)) return;

    loseTween = null;

    if (GameOverSubState.instance?.boyfriend?.isPixel)
    {
      loseSprite = FunkinSprite.createSparrow(0, -350, "gameover/losePixel");
      loseSprite.animation.addByPrefix('fall', "lose0", 24, false);
      loseSprite.animation.addByPrefix('idle', "lose loop0", 12, true);
      loseSprite.antialiasing = false;
      loseSprite.pixelPerfectRender = true;
      loseSprite.pixelPerfectPosition = true;
      loseSprite.scale.set(Constants.PIXEL_ART_SCALE * 0.8, Constants.PIXEL_ART_SCALE * 0.8);
      loseSprite.updateHitbox();
    }
    else
    {
      loseSprite = FunkinSprite.createSparrow(0, -150, "gameover/loseText");
      loseSprite.animation.addByPrefix('fall', "lose0", 24, false);
      loseSprite.animation.addByPrefix('idle', "lose loop0", 24, true);
      loseSprite.scale.set(0.8, 0.8);
      loseSprite.updateHitbox();
    }

    loseSprite.screenCenter(0x01);
    loseSprite.cameras = [PlayState?.instance?.camCutscene];
    loseSprite.visible = false;

    // shader
    loseSprite.shader = GameOverSubState.instance?.boyfriend?.shader ?? null;

    loseSprite.animation.onFinish.add(function(name:String) {
      loseSprite.animation.play('idle', true);
    });

    GameOverSubState.instance.add(loseSprite);
  }

  public function playFall():Void
  {
    // If game over isn't playing the death loop, don't continue.
    if (!GameOverSubState.instance?.boyfriend?.getCurrentAnimation()?.startsWith('deathLoop') ?? false) return;

    // If the lose sprite is playing an animation, don't continue.
    if ((loseSprite?.getCurrentAnimation()?.length ?? 0) > 0) return;

    loseSprite.animation.play('fall', true);
    loseSprite.visible = true;
  }

  public function playFade():Void
  {
    // If game over isn't playing death confirm, don't continue.
    if (!GameOverSubState.instance?.boyfriend?.getCurrentAnimation()?.startsWith('deathConfirm') ?? false) return;

    // If the lose tween is already playing, don't continue.
    if (loseTween != null) return;

    loseTween = FlxTween.tween(loseSprite, {alpha: 0}, 1, {ease: FlxEase.quadInOut, onComplete: (_) -> {
      loseSprite.destroy();
      loseTween = null;
    }});
  }
}