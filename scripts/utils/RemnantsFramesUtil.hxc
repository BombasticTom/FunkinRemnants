import funkin.util.assets.FlxAnimationUtil;
import funkin.Assets;
import funkin.Paths;
import StringTools;

class RemnantsFramesUtil
{
  public static function buildMultiSparrowAnimateCharacterSpritesheet(character:BaseCharacter):Void
  {
    var sparrowList:Array<String> = [];
    var animateList:Array<String> = [];

    var addToList:String->Void = (assetPath:String) -> {
      var library:String = Paths.getLibrary(assetPath);
      var key:String = Paths.stripLibrary(assetPath);

      var atlasPath:String = Paths.animateAtlas(key, library);
      trace(atlasPath);

      if (Assets.exists(atlasPath + '/Animation.json'))
      {
        animateList.push(assetPath);
      }
      else
      {
        sparrowList.push(assetPath);
      }
    };

    addToList(character._data.assetPath);

    for (anim in character._data.animations)
    {
      if (anim.assetPath != null && !sparrowList.contains(anim.assetPath) && !animateList.contains(anim.assetPath))
      {
        addToList(anim.assetPath);
      }
    }

    buildMultiSparrowAnimateAtlas(character, sparrowList, animateList, character._data.atlasSettings);
  }

  public static function buildMultiSparrowAnimateCharacterAnimations(character:BaseCharacter):Void
  {
    trace('[MULTISPARROW+ANIMATE] Loading ' + character._data.animations.length + ' animations...');

    var frameNames:Array<String> = [for (frame in character.frames.frames) frame.name];
    var animNamesSupposed:Array<String> = [];

    for (anim in character._data.animations)
    {
      var isTextureAtlas:Bool = true;
      for (frameName in frameNames)
      {
        if (StringTools.startsWith(frameName, anim.prefix))
        {
          isTextureAtlas = false;
          break;
        }
      }

      if (isTextureAtlas) FlxAnimationUtil.addTextureAtlasAnimation(character, anim);
      else
        FlxAnimationUtil.addAtlasAnimation(character, anim);

      if (anim.offsets == null)
      {
        character.setAnimationOffsets(anim.name, 0, 0);
      }
      else
      {
        character.setAnimationOffsets(anim.name, anim.offsets[0], anim.offsets[1]);
      }

      animNamesSupposed.push(anim.name);
    }

    var animNames = character.animation.getNameList();
    trace('[MULTISPARROW+ANIMATE] Successfully loaded ' + animNames.length + ' animations');

    for (animName in animNamesSupposed)
    {
      if (!animNames.contains(animName)) trace('[MULTISPARROW+ANIMATE] Animation ' + animName + ' was not loaded.');
    }
  }

  public static function buildMultiSparrowAnimateAtlas(sprite:FunkinSprite, sparrowList:Array<String>, animateList:Array<String>,
      ?settings:AtlasSpriteSettings):Void
  {
    var baseAnimateAsset:String = animateList[0];
    if (baseAnimateAsset == null) throw 'You need at least one animate atlas for this to work!';

    if (settings == null) settings = {};
    settings.uniqueInCache = true;

    var baseAssetLibrary:String = Paths.getLibrary(baseAnimateAsset);
    var baseAssetPath:String = Paths.stripLibrary(baseAnimateAsset);

    trace('Concentrating PRIMARY animate atlas: ' + baseAnimateAsset);
    sprite.loadTextureAtlas(baseAssetPath, baseAssetLibrary, settings);

    if (sprite.library == null)
    {
      trace('Multi-Sparrow+Animate atlas could not load PRIMARY atlas: ' + baseAnimateAsset);
      return;
    }

    for (asset in animateList)
    {
      if (baseAnimateAsset == asset) continue;

      var subAssetLibrary:String = Paths.getLibrary(asset);
      var subAssetPath:String = Paths.stripLibrary(asset);

      var subTexture:FlxAnimateFrames = null;

      try
      {
        subTexture = Paths.getAnimateAtlas(subAssetPath, subAssetLibrary, settings);
      }
      catch (e:Exception) {}

      if (subTexture == null)
      {
        trace('Multi-Sparrow+Animate atlas could not load subtexture: ' + asset);
        continue;
      }

      trace('Concatenating animate atlas: ' + asset);
      subTexture.parent.destroyOnNoUse = false;

      sprite.library.addAtlas(subTexture);
    }

    for (asset in sparrowList)
    {
      var subTexture:FlxAtlasFrames = null;

      try
      {
        subTexture = Paths.getSparrowAtlas(asset);
      }
      catch (e:Exception) {}

      if (subTexture == null)
      {
        trace('Multi-Sparrow+Animate atlas could not load subtexture: ' + asset);
        continue;
      }

      trace('Concatenating sparrow atlas: ' + asset);
      subTexture.parent.destroyOnNoUse = false;
      sprite.frames.addAtlas(subTexture);
    }
  }
}
