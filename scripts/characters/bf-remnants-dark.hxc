import funkin.graphics.FunkinSprite;
import funkin.play.GameOverSubState;
import funkin.play.character.BaseCharacter;
import funkin.data.character.CharacterDataParser;
import funkin.play.character.CharacterType;
import funkin.play.character.MultiAnimateAtlasCharacter;
import funkin.play.PlayState;
import flixel.tweens.FlxEase;
import flixel.tweens.FlxTween;
import funkin.util.HapticUtil;

class BoyfriendRemnantsDarkCharacter extends MultiAnimateAtlasCharacter
{
  function new()
  {
    super('bf-remnants-dark');
  }

  function onAnimationFrame(name:String, frameNumber:Int, frameIndex:Int)
  {
    super.onAnimationFrame(name, frameNumber, frameIndex);

    if (!HapticUtil.hapticsAvailable) return;

    // BF's mic drops.
    if (name == 'firstDeath' && frameNumber == 27)
    {
      HapticUtil.vibrate(0, 0.1, 0.1, 1);
    }

    // BF's balls pulsating.
    if (name == 'deathLoop' && (frameNumber == 0 || frameNumber == 18))
    {
      HapticUtil.vibrate(0, 0.1, 0.1, 1);
    }
  }

  var normalChar:BaseCharacter;

  override function set_alpha(val:Float):Float
  {
    super.set_alpha(val);
    if (normalChar == null) return val;

    if (val == 1) normalChar.alpha = 0.0001;
    else
      normalChar.alpha = 1;
    return val;
  }

  var lastX:Float = null;
  var lastY:Float = null;

  override function playAnimation(name:String, restart:Bool, ignoreOther:Bool)
  {
    if (name == "hey" || name == "cheer" || name == "lights") PlayState.instance?.vocals?.playerVolume = 1;

    super.playAnimation(name, restart, ignoreOther);

    if (normalChar != null) normalChar.playAnimation(name, restart, ignoreOther);
  }

  function onAdd(event:ScriptEvent)
  {
    super.onAdd(event);

    if (!this.debug)
    {
      normalChar = CharacterDataParser.fetchCharacter('bf-remnants');
      PlayState.instance.currentStage.addCharacter(normalChar, characterType);

      for (char in PlayState.instance.currentStage.characters.keys())
      {
        if (PlayState.instance.currentStage.characters.get(char) == normalChar)
        {
          PlayState.instance.currentStage.characters.set(char, super);
          break;
        }
      }

      normalChar.zIndex = normalChar.zIndex - 1;
      alpha = alpha;
    }
  }

  function onNoteHit(event:HitNoteScriptEvent)
  {
    if (event.note.noteData.getMustHitNote() && characterType == CharacterType.BF)
    {
      // Override the hit note animation.
      switch (event.note.kind)
      {
        case "perfect":
          holdTimer = 0;
          this.playSingAnimation(event.note.noteData.getDirection(), false, 'perfect');
          return;

        default:
          super.onNoteHit(event);
      }
    }
  }
}
