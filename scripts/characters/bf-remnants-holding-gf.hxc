import flixel.FlxG;
import funkin.util.HapticUtil;
import funkin.graphics.FunkinSprite;
import funkin.audio.FunkinSound;
import funkin.play.PauseSubState;
import funkin.play.character.MultiSparrowCharacter;
import funkin.play.GameOverSubState;
import funkin.play.PlayState;
import funkin.Preferences;
import flixel.tweens.FlxEase;
import flixel.tweens.FlxTween;

class BoyfriendRemnantsHoldingGFCharacter extends MultiSparrowCharacter
{
  function new()
  {
    super('bf-remnants-holding-gf');
  }

  function onCreate(event:ScriptEvent)
  {
    super.onCreate(event);

    GameOverSubState.musicSuffix = '-remnants';
    PauseSubState.musicSuffix = '-remnants';
  }

  var loseSprite:FunkinSprite;

  /**
   * Initialize and cache sprites used for the death animation,
   */
  function createDeathSprites()
  {
    loseSprite = FunkinSprite.createSparrow(0, 0, "gameover/loseText");
    loseSprite.animation.addByPrefix('fall', "lose0", 24, false);
    loseSprite.animation.addByPrefix('idle', "lose loop0", 24, true);
    loseSprite.setPosition(this.getMidpoint().x - (loseSprite.width / 2) + 110, -200);
    loseSprite.animation.onFinish.add(function(name:String) {
      loseSprite.animation.play('idle', true);
    });

    // week 7 shader
    if (shader != null)
    {
      loseSprite.shader = shader;
    }

    loseSprite.zIndex = this.zIndex + 5;
    loseSprite.visible = false;
  }

  override function playAnimation(name:String, restart:Bool, ignoreOther:Bool)
  {
    if (name == "hey") PlayState.instance?.vocals?.playerVolume = 1;
    if (name == "firstDeath")
    {
      if (!this.debug)
      {
        // Lose...
        createDeathSprites();
        GameOverSubState.instance.add(loseSprite);
      }
    }
    else if (name == 'deathLoop')
    {
      if (loseSprite != null)
      {
        loseSprite.visible = true;
        loseSprite.animation.play('fall', true);
      }
    }
    else if (name == "deathConfirm")
    {
      if (loseSprite != null)
      {
        FlxTween.tween(loseSprite, {alpha: 0}, 1, {ease: FlxEase.quadInOut});
      }
    }

    super.playAnimation(name, restart, ignoreOther);
  }

  function onAnimationFrame(name:String, frameNumber:Int, frameIndex:Int)
  {
    super.onAnimationFrame(name, frameNumber, frameIndex);

    if (!HapticUtil.hapticsAvailable) return;

    // BF's mic drops.
    if (name == 'firstDeath' && frameNumber == 27)
    {
      HapticUtil.vibrate(0, 0.1, 0.1, 1);
    }

    // BF's balls pulsating.
    if (name == 'deathLoop' && (frameNumber == 0 || frameNumber == 18))
    {
      HapticUtil.vibrate(0, 0.1, 0.1, 1);
    }
  }

  override function getDeathQuote():Null<String>
  {
    if (PlayState.instance.currentStage == null) return;
    switch (PlayState.instance.currentStage.getDad()?.characterId)
    {
      case "tankman-remnants":
        var randomCensor:Array<Int> = [];
        if (!Preferences.naughtyness) randomCensor = [1, 3, 8, 13, 17, 21];
        return Paths.sound('jeffGameover/jeffGameover-' + FlxG.random.int(1, 25, randomCensor));
      default:
        return null;
    }
  }
}
