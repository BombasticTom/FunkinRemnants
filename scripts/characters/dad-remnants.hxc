import funkin.play.character.SparrowCharacter;
import funkin.play.character.CharacterType;
import funkin.graphics.FunkinSprite;
import funkin.audio.FunkinSound;
import funkin.util.FlxTweenUtil;
import funkin.play.PlayState;
import flixel.tweens.FlxTween;
import flixel.tweens.FlxEase;
import flixel.math.FlxMath;
import flixel.FlxG;

class DadRemnantsCharacter extends SparrowCharacter
{
  /**
   * How far dad should go when the kick animation is played.
   */
  var KICK_OFFSET:Float = 350;

  /**
   * Extra offset factor for kicking.
   */
  var curKickOffset:Float = 0;

  /**
   * Super serect :eyes:
   */
  var slip:Bool = false;

  public function new()
  {
    super('dad-remnants');
  }

  override function playAnimation(name:String, restart:Bool, ignoreOther:Bool)
  {
    super.playAnimation(name, restart, ignoreOther);
    if (name == 'preAttack' && !slip) playCockAura();
  }

  override public function onAnimationFrame(name:String = "", frameNumber:Int = -1, frameIndex:Int = -1):Void
  {
    curKickOffset = 0;
    super.onAnimationFrame(name, frameNumber, frameIndex);
    if (name != "attack" || slip) return;

    curKickOffset = KICK_OFFSET;

    if (frameNumber <= 3)
    {
      curKickOffset *= FlxEase.quintOut(frameNumber / 3);
      trace(curKickOffset);
    }
    else if (frameNumber >= 6 && frameNumber <= 14)
    {
      curKickOffset *= (1 - FlxEase.quadInOut(FlxMath.remapToRange(frameNumber, 6, 14, 0, 1)));
      trace(curKickOffset);
    }
  }

  override function getScreenPosition(?result:FlxPoint, ?camera:FlxCamera):FlxPoint
  {
    var output:FlxPoint = super.getScreenPosition(result, camera);
    output.x += curKickOffset; // BUG: This sometimes messes with the secret slip tween and I dont really know how this thingy works so womp womp
    return output;
  }

  var dadFade:FunkinSprite;
  var fadeTween:FlxTween;

  function playCockAura()
  {
    if (debug) return;

    if (dadFade != null) dadFade.destroy();
    dadFade = new FunkinSprite(this.x, this.y);
    dadFade.x -= this.animOffsets[0] + this.globalOffsets[0];
    dadFade.y -= this.animOffsets[1] + this.globalOffsets[1];
    dadFade.pixels = updateFramePixels();
    dadFade.updateHitbox();
    dadFade.alpha = 0.3;
    dadFade.zIndex = this.zIndex - 3;
    PlayState.instance?.currentStage?.add(dadFade);
    PlayState.instance?.currentStage?.refresh();

    if (fadeTween != null) fadeTween.cancel();
    fadeTween = FlxTween.num(0, 1, 0.4,
      {
        onComplete: (_) -> {
          fadeTween = null;
        }
      }, (num:Float) -> {
        dadFade.scale.set(1 + (0.3 * num), 1 + (0.3 * num));
        dadFade.alpha = 0.3 - (0.3 * num);
      });
  }

  override function onPause(event:ScriptEvent):Void
  {
    super.onPause(event);
    if (fadeTween != null) fadeTween.active = false;
    if (slip) FlxTweenUtil.pauseTweensOf(PlayState.instance.currentStage.getDad());
    FlxG.camera?.active = false;
  }

  override function onResume(event:ScriptEvent):Void
  {
    super.onResume(event);
    if (fadeTween != null) fadeTween.active = true;
    if (slip) FlxTweenUtil.resumeTweensOf(PlayState.instance.currentStage.getDad());
    FlxG.camera?.active = true;
  }

  function onNoteHit(event:HitNoteScriptEvent)
  {
    var hasSlip:Bool = false;
    for (i => param in event.note.params)
    {
      if (param.name == 'battle-slip') hasSlip = true;
    }

    if (!slip && hasSlip)
    {
      // Bros boutta go for a ride
      slip = true;
      FunkinSound.playOnce(Paths.sound('gameplay/secret/secret'), 1);
      FlxTween.tween(PlayState.instance.currentStage.getDad(), {x: PlayState.instance.currentStage.getDad().originalPosition.x + 3000}, 0.75,
        {
          onComplete: function(_) {
            this.visible = false;
            this.x = this.originalPosition.x;
            FlxG.camera.shake(0.05, 2.5);
            FunkinSound.playOnce(Paths.sound('crash'), 1);
          }
        });
    }

    if (characterType != CharacterType.DAD) return super.onNoteHit(event);

    if (!event.note.noteData.getMustHitNote() && slip)
    {
      PlayState.instance.vocals.opponentVolume = 0;
      event.cancel();
    }

    super.onNoteHit(event);
  }

  function onCreate(event:ScriptEvent):Void
  {
    super.onCreate(event);

    slip = false;
  }

  function onSongRetry(event:ScriptEvent)
  {
    super.onSongRetry(event);

    if (slip)
      this.visible = false;
  }

  function onCountdownStart(event:CountdownScriptEvent):Void
  {
    super.onCountdownStart(event);

    if (slip)
    {
      this.visible = true;
      slip = false;
    }
  }
}
