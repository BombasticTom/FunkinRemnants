import funkin.play.character.SparrowCharacter;
import funkin.play.character.CharacterType;
import funkin.audio.FunkinSound;
import flixel.tweens.FlxTween;
import flixel.tweens.FlxEase;
import funkin.play.PlayState;
import flixel.util.FlxTimer;
import funkin.Conductor;
import flixel.FlxG;

class DadRemnantsCharacter extends SparrowCharacter
{
  function new()
  {
    super('dad-remnants');
  }

  function onCreate(event:ScriptEvent)
  {
    super.onCreate(event);

    dadKickSound = FunkinSound.load(Paths.sound('dadKick'), 1.0);
  }

  /**
   * Stores the default x position of this character.
   */
  var defaultX:Float = 100;

  /**
   * Timer instance that will return this character to it's original position.
   */
  var returnTimer:FlxTimer = null;

  /**
   * Tween instance that will move this character.
   */
  var kickTween:FlxTween = null;

  function onNoteHit(event:HitNoteScriptEvent)
  {
    if (event.eventCanceled) return;

    if (event.note.noteData.getMustHitNote() && event.note.kind == "bf-attack") {
      holdTimer = 0;
      if (FlxG.random.bool(50))
        this.playHitAnim();
      else
        this.playBlockAnim();

      return;
    }

    if (!event.note.noteData.getMustHitNote() && characterType == CharacterType.DAD)
    {
      switch (event.note.kind)
      {
        case "dad-prepare":
          holdTimer = 0;
          playPreKickAnim();

        case "dad-attack":
          holdTimer = 0;
          playKickAnim();

          dadKickSound.play(true);

          // cancel any tween of this object.
          FlxTween.cancelTweensOf(PlayState.instance.currentStage.getDad());

          // then do the tween to move it.
          FlxTween.tween(PlayState.instance.currentStage.getDad(), {x: 450}, 0.5, {ease: FlxEase.quintOut});

          if (returnTimer != null)
            returnTimer.cancel();

          returnTimer = new FlxTimer().start(1, _ -> {
            // cancel any tween of this object.
            FlxTween.cancelTweensOf(PlayState.instance.currentStage.getDad());

            // then do the tween to move it.
            FlxTween.tween(PlayState.instance.currentStage.getDad(), {x: defaultX}, 0.5, {ease: FlxEase.quintIn});         
          });

        default:
          super.onNoteHit(event);
      }
    }
  }

  function onNoteIncoming(event:NoteScriptEvent)
  {
    if (!event.note.noteData.getMustHitNote()
      && characterType == CharacterType.DAD
      || event.note.noteData.getMustHitNote()
      && characterType == CharacterType.BF)
    {
      // Get how long until it's time to strum the note.
      var msTilStrum = event.note.strumTime - Conductor.instance.songPosition;

      switch (event.note.kind)
      {
        case "dad-attack":
          scheduleDadSound(msTilStrum - 22);
        default:
          super.onNoteIncoming(event);
      }
    }
  }

  var dadKickSound:FunkinSound;
  var loadedDadKickSound:Bool = false;

  /**
   * Schedule the can-lighting sound to play in X ms
   */
  function scheduleDadSound(timeToPlay:Float)
  {
    if (!loadedDadKickSound)
    {
      dadKickSound = FunkinSound.load(Paths.sound('dadKick'), 1.0);
      loadedDadKickSound = true;
    }

    dadKickSound.play(true, -timeToPlay);
  }

  /**
   * Play the animation where Dad prepares to kick BF's blueballs.
   */
  function playPreKickAnim()
  {
    this.playAnimation('preAttack', true, false);
  }

  /**
   * Play the animation where Dad blocks BF's microphone attack.
   */
  function playBlockAnim()
  {
    this.playAnimation('block', true, true);
  }

  /**
   * Play the animation where Dad gets hit by BF's microphone attack.
   */
  function playHitAnim()
  {
    this.playAnimation('hit', true, true);
  }

  /**
   * Play the animation where Dad kicks BF's blueballs.
   */
  function playKickAnim()
  {
    this.playAnimation('attack', true, true);
  }
}

