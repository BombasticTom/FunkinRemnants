import funkin.graphics.adobeanimate.FlxAtlasSprite;
import flixel.FlxG;
import funkin.audio.FunkinSound;
import funkin.play.GameOverSubState;
import funkin.play.character.BaseCharacter;
import funkin.data.character.CharacterDataParser;
import funkin.play.character.CharacterType;
import funkin.play.PlayState;
import funkin.play.character.SparrowCharacter;
import funkin.util.HapticUtil;

class GirlfriendRemnantsDarkCharacter extends SparrowCharacter
{
  function new()
  {
    super('gf-remnants-dark');
  }

  override function onAdd()
  {
    if (!debug && this.characterType == CharacterType.DAD)
    {
      // If Girlfriend is the opponent, set her position to the stage's assigned Girlfriend position,
      // while maintaining her status as an opponent. This allows the Girlfriend character to use a shared
      // character file without weird offsets.

      var pos:FlxPoint = PlayState.instance.currentStage.getGirlfriendPosition();

      this.originalPosition.x = pos.x - this.characterOrigin.x;
      this.originalPosition.y = pos.y - this.characterOrigin.y;

      this.resetPosition();
    }
  }

  override function dance(force:Bool)
  {
    // Fix animation glitches with Week 3.
    // Wait for 'hairBlow' to play, preventing dancing from interrupting it.
    if (!force && ['hairBlow'].contains(getCurrentAnimation())) return;
    // Wait for 'hairFall' to finish, preventing dancing from interrupting it.
    if (!force && ['hairFall'].contains(getCurrentAnimation()) && !isAnimationFinished()) return;

    super.dance(force);
  }

  var normalChar:BaseCharacter;

  override function set_alpha(val:Float):Float
  {
    super.set_alpha(val);
    if (val == 1) normalChar.alpha = 0.0001;
    else
      normalChar.alpha = 1;
    return val;
  }

  override function playAnimation(name:String, restart:Bool, ignoreOther:Bool)
  {
    super.playAnimation(name, restart, ignoreOther);
    if (normalChar != null)
    {
      normalChar.playAnimation(name, restart, ignoreOther);
      normalChar.setPosition(this.x, this.y);
    }
  }

  function onCreate(event:ScriptEvent)
  {
    super.onCreate(event);
    normalChar = CharacterDataParser.fetchCharacter('gf-remnants');
    normalChar.flipX = false;
    normalChar.zIndex = 99;

    PlayState.instance.currentStage.add(normalChar);
    PlayState.instance.currentStage.refresh(); // Apply z-index.

    if (!this.debug)
    { // Gotta do it like this because the editor freaks out.
      if (PlayState.instance.currentSong.id == null) return;
      if (['south'].contains(PlayState.instance.currentSong.id)) this.idleSuffix = '-spooky';
      else
        this.idleSuffix = '';
    }
  }
}

