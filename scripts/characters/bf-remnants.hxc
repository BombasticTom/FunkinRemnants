import flixel.FlxG;
import funkin.Conductor;
import funkin.util.HapticUtil;
import funkin.graphics.FunkinSprite;
import funkin.audio.FunkinSound;
import funkin.play.PauseSubState;
import flixel.effects.FlxFlicker;
import funkin.play.character.MultiAnimateAtlasCharacter;
import funkin.play.character.CharacterType;
import funkin.play.GameOverSubState;
import funkin.play.PlayState;
import flixel.tweens.FlxEase;
import flixel.tweens.FlxTween;
import flixel.util.FlxTimer;
import funkin.util.Constants;
import funkin.FunkinMemory;
import funkin.Preferences;

class BoyfriendRemnantsCharacter extends MultiAnimateAtlasCharacter
{
  function new()
  {
    super('bf-remnants');
  }

  function onCreate(event:ScriptEvent)
  {
    super.onCreate(event);

    GameOverSubState.musicSuffix = '-remnants';
    PauseSubState.musicSuffix = '-remnants';

    FunkinMemory.cacheSound(Paths.sound('Pico_Bonk', "weekend1"));
    this.shouldAlternate = ['south'].contains(PlayState?.instance?.currentSong?.id);
  }

  override function onSongRetry(event:ScriptEvent):Void
  {
    super.onSongRetry(event);

    // Reset to standard death animation.
    GameOverSubState.musicSuffix = '-remnants';
    GameOverSubState.blueBallSuffix = "";

    PauseSubState.musicSuffix = '-remnants';

    bfDeathExplosion = null;
    this.visible = true;
  }

  var singed:FunkinSound = null;
  var loseSprite:FunkinSprite = null;
  var bfDeathExplosion:FunkinSprite = null;

  /**
   * Initialize and cache sprites used for the death animation.
   */
  function createDeathSprites()
  {
    loseSprite = FunkinSprite.createSparrow(0, 0, "gameover/loseText");
    loseSprite.animation.addByPrefix('fall', "lose0", 24, false);
    loseSprite.animation.addByPrefix('idle', "lose loop0", 24, true);
    loseSprite.setPosition(this.getMidpoint().x - (loseSprite.width / 2) + 110, -200);
    loseSprite.animation.onFinish.add(function(name:String) {
      loseSprite.animation.play('idle', true);
    });

    // week 3 shader
    if (shader != null)
    {
      loseSprite.shader = shader;
    }

    loseSprite.zIndex = this.zIndex + 5;
    loseSprite.visible = false;
  }

  override function playAnimation(name:String, restart:Bool, ignoreOther:Bool)
  {
    if (["hey", "cheer", "ugh", "sigma", "remnants", "ligma", "lights"].contains(name))
    {
      PlayState.instance?.vocals?.playerVolume = 1;
    }

    // Do not create the sprites when on the animation editor.
    if (!this.debug)
    {
      if (name == 'fakeoutDeath')
      {
        doFakeoutDeath();
      }
      else if (name == "firstDeath")
      {
        if (GameOverSubState.blueBallSuffix == '-bf-explode-remnants')
        {
          // Explosion death animation... ngl idk if I even implented this right I only half know what im doing lmao. All I know is that the game over song dosent loop peroperly, so Till plz save me
          doExplosionDeath();
        }
        else
        {
          // Lose...
          createDeathSprites();
          GameOverSubState.instance.add(loseSprite);
        }
      }
      else if (name == 'deathLoop')
      {
        if (loseSprite != null)
        {
          loseSprite.visible = true;
          loseSprite.animation.play('fall', true);
        }
      }
      else if (name == "deathConfirm")
      {
        if (bfDeathExplosion != null || GameOverSubState.blueBallSuffix == '-bf-explode-remnants')
        {
          doExplosionConfirm();
        }
        else
        {
          if (loseSprite != null)
          {
            FlxTween.tween(loseSprite, {alpha: 0}, 1, {ease: FlxEase.quadInOut});
          }
        }
      }
    }

    super.playAnimation(name, restart, ignoreOther);

    if (name == 'preAttack')
    {
      if (!this.debug)
      {
        // Devious animation name...
        playCockAura();
      }
    }
  }

  function doExplosionDeath()
  {
    if (fadeTween != null && fadeTween.active) fadeTween.cancel();

    if (bfFlicker != null)
    {
      bfFlicker.stop(); // this sets visible to true, but we make it false a few lines down anyways
    }

    // Suffixed death sound will already play.
    GameOverSubState.instance.resetCameraZoom();

    // Move the camera up.
    GameOverSubState.instance.cameraFollowPoint.y -= 100;
    HapticUtil.vibrate(0, 0.5);

    // Death by Darnell's can.
    new FlxTimer().start(1.85, function(tmr:FlxTimer) {
      HapticUtil.vibrate(0, 0.1, 0.1, 1); // Pico falls on his knees.
    });

    bfDeathExplosion = FunkinSprite.createTextureAtlas(this.x - 1100, this.y - 1000, "characters/picoExplosionDeath", "weekend1");
    PlayState.instance.subState.add(bfDeathExplosion);
    bfDeathExplosion.zIndex = 1000;
    bfDeathExplosion.animation.onFinish.add(onExplosionFinishAnim);
    bfDeathExplosion.visible = true;
    this.visible = false;

    new FlxTimer().start(3.0, afterExplosionIntro);
    bfDeathExplosion.anim.play('intro');
  }

  function doExplosionConfirm()
  {
    // Suffixed confirm music will already play.
    bfDeathExplosion.animation.play('Confirm', true);
    if (singed != null)
    {
      singed.stop();
      singed = null;
    }
  }

  function onExplosionFinishAnim(animLabel:String)
  {
    if (animLabel == 'intro')
    {
      bfDeathExplosion.animation.play('Loop Start', true);
      bfDeathExplosion.animation.curAnim.looped = true;
    }
    else if (animLabel == 'Confirm')
    {
      // Do nothing, the animation will just play.
    }

    trace("Explosion animation finished: " + animLabel);
  }

  function afterExplosionIntro(timer:FlxTimer)
  {
    // Start the (standard) death music, 3.5 seconds after the explosion starts,
    // not when the explosion sound finishes or when the loop starts.
    GameOverSubState.instance.startDeathMusic(1, true);
    singed = FunkinSound.load(Paths.sound('singed_loop'), true, false, true);
  }

  function onAnimationFrame(name:String, frameNumber:Int, frameIndex:Int)
  {
    super.onAnimationFrame(name, frameNumber, frameIndex);

    if (!HapticUtil.hapticsAvailable) return;

    // BF's mic drops.
    if (name == 'firstDeath' && frameNumber == 27)
    {
      HapticUtil.vibrate(0, 0.1, 0.1, 1);
    }

    // BF's balls pulsating.
    if (name == 'deathLoop' && (frameNumber == 0 || frameNumber == 18))
    {
      HapticUtil.vibrate(0, 0.1, 0.1, 1);
    }
  }

  function doFakeoutDeath():Void
  {
    FunkinSound.playOnce(Paths.sound("gameplay/gameover/fakeout_death"), 1.0);

    // We don't want people to miss this.
    GameOverSubState.instance.mustNotExit = true;
    this.animation.onFinish.addOnce(() -> {
      GameOverSubState.instance.mustNotExit = false;
      this.playAnimation('firstDeath', true);
      // Play the "blue balled" sound. May play a variant if one has been assigned.
      GameOverSubState.playBlueBalledSFX();
    });
  }

  override function getDeathQuote():Null<String>
  {
    switch (PlayState.instance?.currentStage?.getDad()?.characterId)
    {
      case "tankman":
        var randomCensor:Array<Int> = [];

        if (!Preferences.naughtyness)
        {
          // 1 - cock
          // 3 - shitty
          // 8 - shit
          // 13 - fuck
          // 17 - shit, asshole
          // 21 - fucking
          randomCensor = [1, 3, 8, 13, 17, 21];
        }
        else if (Constants.CENSOR_EXPLETIVES)
        {
          randomCensor = [13, 21];
        }

        return Paths.sound('jeffGameover/jeffGameover-' + FlxG.random.int(1, 25, randomCensor));
      default:
        return null;
    }
  }

  function onNoteHit(event:HitNoteScriptEvent)
  {
    // onNoteHit event was cancelled by the gameplay module.
    if (event.eventCanceled) return;

    if (event.note.noteData.getMustHitNote() && characterType == CharacterType.BF)
    {
      // Override the hit note animation.
      switch (event.note.kind)
      {
        case "censor":
          holdTimer = 0;
          this.playSingAnimation(event.note.noteData.getDirection(), false, 'censor');
        case "perfect":
          holdTimer = 0;
          this.playSingAnimation(event.note.noteData.getDirection(), false, 'perfect');
        case "bf-prep":
          holdTimer = 0;
          this.playAnimation('preAttack', true, false);
          return;
        case "bf-attack":
          holdTimer = 0;
          this.playAnimation('attack', true, false);
          return;
        case "bf-dodge":
          holdTimer = 0;
          this.playAnimation('dodge', true, false);
          return;
        default:
          super.onNoteHit(event);
      }
    }
  }

  function onNoteMiss(event:NoteScriptEvent)
  {
    // Override the miss note animation.
    switch (event.note.kind)
    {
      case "bf-attack":
        playCanExplodeAnim();
      case "battle-dodge-remnants":
        this.playAnimation('hit', true);
      default:
        super.onNoteMiss(event);
    }
  }

  /**
   * Play the animation where BF is hit by the exploding can.
   */
  function playCanExplodeAnim()
  {
    this.playAnimation('hitCan', true, true);
    // Donk.
    FunkinSound.playOnce(Paths.sound('Pico_Bonk'), 1.0);
  }

  var bfFlicker:FlxFlicker = null;

  override function onAnimationFinished(name:String)
  {
    super.onAnimationFinished(name);

    if (name == 'hitCan' && PlayState.instance.health > 0.0 && !PlayState.instance.isPlayerDying)
    {
      // ERIC: You have to use super instead of this or it breaks.
      // This is because typeof(this) is PolymodAbstractClass.
      bfFlicker = FlxFlicker.flicker(super, 1, 1 / 30, true, true, function(_) {
        bfFlicker = FlxFlicker.flicker(super, 0.5, 1 / 60, true, true, function(_) {
          bfFlicker = null;
        });
      });
    }
  }

  var bfFade:FunkinSprite;
  var fadeTween:FlxTween;

  function playCockAura()
  {
    if (debug) return;

    if (bfFade != null) bfFade.destroy();
    bfFade = new FunkinSprite(this.x, this.y);
    bfFade.x -= this.animOffsets[0] + this.globalOffsets[0];
    bfFade.y -= this.animOffsets[1] + this.globalOffsets[1];
    bfFade.pixels = updateFramePixels();
    bfFade.updateHitbox();
    bfFade.alpha = 0.3;
    bfFade.zIndex = this.zIndex - 3;
    PlayState.instance?.currentStage?.add(bfFade);
    PlayState.instance?.currentStage?.refresh();

    if (fadeTween != null) fadeTween.cancel();
    fadeTween = FlxTween.num(0, 1, 0.4,
      {
        onComplete: (_) -> {
          fadeTween = null;
        }
      }, (num:Float) -> {
        bfFade.scale.set(1 + (0.3 * num), 1 + (0.3 * num));
        bfFade.alpha = 0.3 - (0.3 * num);
      });
  }

  override function onPause(event:ScriptEvent):Void
  {
    super.onPause(event);
    if (fadeTween != null) fadeTween.active = false;
    if (bfFlicker != null)
    {
      bfFlicker.pause();
      this.visible = true;
    }
  }

  override function onResume(event:ScriptEvent):Void
  {
    super.onResume(event);
    if (fadeTween != null) fadeTween.active = true;
    if (bfFlicker != null)
    {
      bfFlicker.resume();
    }
  }
}
