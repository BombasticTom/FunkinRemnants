import funkin.play.character.MultiSparrowCharacter;
import funkin.play.PlayState;
import funkin.util.HapticUtil;
import funkin.util.Constants;
import flixel.tweens.FlxEase;
import funkin.graphics.FunkinSprite;
import flixel.tweens.FlxTween;
import funkin.play.GameOverSubState;
import funkin.play.PauseSubState;
import flixel.FlxSprite;
import flixel.FlxG;

class BFRemnantsPixelCharacter extends MultiSparrowCharacter
{
  function new()
  {
    super('bf-remnants-pixel');
  }

  function onCreate(event:ScriptEvent)
  {
    super.onCreate(event);

    GameOverSubState.musicSuffix = '-remnants-pixel';
    GameOverSubState.blueBallSuffix = '-pixel';

    PauseSubState.musicSuffix = '-remnants-pixel';
  }

  var loseSprite:FunkinSprite;

  /**
   * Initialize and cache sprites used for the death animation,
   */
  function createDeathSprites()
  {
    loseSprite = FunkinSprite.createSparrow(1125, 15, "gameover/losePixel");
    loseSprite.setGraphicSize(Std.int(loseSprite.width * Constants.PIXEL_ART_SCALE - 1));
    loseSprite.antialiasing = false;
    loseSprite.pixelPerfectRender = true;
    loseSprite.pixelPerfectPosition = true;

    loseSprite.animation.addByPrefix('fall', "lose0", 24, false);
    loseSprite.animation.addByPrefix('idle', "lose loop0", 12, true);
    loseSprite.animation.onFinish.add(function(name:String) {
      loseSprite.animation.play('idle', true);
    });

    loseSprite.zIndex = this.zIndex + 5;
    loseSprite.visible = false;
  }

  function playAnimation(name:String, restart:Bool, ignoreOther:Bool)
  {
    if (name == "hey")
    {
      PlayState.instance?.vocals?.playerVolume = 1;
    }

    if (name == "firstDeath")
    {
      if (!this.debug)
      {
        // Remove the shader from this character.
        this.shader = null;

        // Lose...
        createDeathSprites();
        GameOverSubState.instance.add(loseSprite);
      }
    }
    else if (name == 'deathLoop')
    {
      if (loseSprite != null)
      {
        loseSprite.visible = true;
        loseSprite.animation.play('fall', true);
      }
    }
    else if (name == "deathConfirm")
    {
      if (loseSprite != null)
      {
        FlxTween.tween(loseSprite, {alpha: 0}, 1, {ease: FlxEase.quadInOut});
      }
    }

    super.playAnimation(name, restart, ignoreOther);
  }

  function onAnimationFrame(name:String, frameNumber:Int, frameIndex:Int)
  {
    super.onAnimationFrame(name, frameNumber, frameIndex);

    if (!HapticUtil.hapticsAvailable) return;

    // BF's mic drops.
    if (name == 'firstDeath' && frameNumber == 27)
    {
      HapticUtil.vibrate(0, 0.1, 0.1, 1);
    }

    // BF's balls pulsating.
    if (name == 'deathLoop' && (frameNumber == 0 || frameNumber == 18))
    {
      HapticUtil.vibrate(0, 0.1, 0.1, 1);
    }
  }
}

